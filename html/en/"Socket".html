<html>
<head><meta charset=utf-8/>
<title>"Socket" - networking constants and support functions</title></head>
<body><pre>

Socket(3pm)            User Contributed Perl Documentation           Socket(3pm)



NAME
       &quot;Socket&quot; - networking constants and support functions

SYNOPSIS
       &quot;Socket&quot; a low-level module used by, among other things, the IO::Socket
       family of modules. The following examples demonstrate some low-level uses
       but a practical program would likely use the higher-level API provided by
       &quot;IO::Socket&quot; or similar instead.

        use Socket qw(PF_INET SOCK_STREAM pack_sockaddr_in inet_aton);

        socket(my $socket, PF_INET, SOCK_STREAM, 0)
            or die &quot;socket: $!&quot;;

        my $port = getservbyname &quot;echo&quot;, &quot;tcp&quot;;
        connect($socket, pack_sockaddr_in($port, inet_aton(&quot;localhost&quot;)))
            or die &quot;connect: $!&quot;;

        print $socket &quot;Hello, world!\n&quot;;
        print &lt;$socket&gt;;

       See also the &quot;EXAMPLES&quot; section.

DESCRIPTION
       This module provides a variety of constants, structure manipulators and
       other functions related to socket-based networking. The values and
       functions provided are useful when used in conjunction with Perl core
       functions such as socket(), setsockopt() and bind(). It also provides
       several other support functions, mostly for dealing with conversions of
       network addresses between human-readable and native binary forms, and for
       hostname resolver operations.

       Some constants and functions are exported by default by this module; but
       for backward-compatibility any recently-added symbols are not exported by
       default and must be requested explicitly. When an import list is provided
       to the &quot;use Socket&quot; line, the default exports are not automatically
       imported. It is therefore best practice to always to explicitly list all
       the symbols required.

       Also, some common socket &quot;newline&quot; constants are provided: the constants
       &quot;CR&quot;, &quot;LF&quot;, and &quot;CRLF&quot;, as well as $CR, $LF, and $CRLF, which map to
       &quot;\015&quot;, &quot;\012&quot;, and &quot;\015\012&quot;. If you do not want to use the literal
       characters in your programs, then use the constants provided here. They
       are not exported by default, but can be imported individually, and with
       the &quot;:crlf&quot; export tag:

        use Socket qw(:DEFAULT :crlf);

        $sock-&gt;print(&quot;GET / HTTP/1.0$CRLF&quot;);

       The entire getaddrinfo() subsystem can be exported using the tag
       &quot;:addrinfo&quot;; this exports the getaddrinfo() and getnameinfo() functions,
       and all the &quot;AI_*&quot;, &quot;NI_*&quot;, &quot;NIx_*&quot; and &quot;EAI_*&quot; constants.

CONSTANTS
       In each of the following groups, there may be many more constants
       provided than just the ones given as examples in the section heading. If
       the heading ends &quot;...&quot; then this means there are likely more; the exact
       constants provided will depend on the OS and headers found at compile-
       time.

   PF_INET, PF_INET6, PF_UNIX, ...
       Protocol family constants to use as the first argument to socket() or the
       value of the &quot;SO_DOMAIN&quot; or &quot;SO_FAMILY&quot; socket option.

   AF_INET, AF_INET6, AF_UNIX, ...
       Address family constants used by the socket address structures, to pass
       to such functions as inet_pton() or getaddrinfo(), or are returned by
       such functions as sockaddr_family().

   SOCK_STREAM, SOCK_DGRAM, SOCK_RAW, ...
       Socket type constants to use as the second argument to socket(), or the
       value of the &quot;SO_TYPE&quot; socket option.

   SOCK_NONBLOCK. SOCK_CLOEXEC
       Linux-specific shortcuts to specify the &quot;O_NONBLOCK&quot; and &quot;FD_CLOEXEC&quot;
       flags during a socket(2) call.

        socket( my $sockh, PF_INET, SOCK_DGRAM|SOCK_NONBLOCK, 0 )

   SOL_SOCKET
       Socket option level constant for setsockopt() and getsockopt().

   SO_ACCEPTCONN, SO_BROADCAST, SO_ERROR, ...
       Socket option name constants for setsockopt() and getsockopt() at the
       &quot;SOL_SOCKET&quot; level.

   IP_OPTIONS, IP_TOS, IP_TTL, ...
       Socket option name constants for IPv4 socket options at the &quot;IPPROTO_IP&quot;
       level.

   MSG_BCAST, MSG_OOB, MSG_TRUNC, ...
       Message flag constants for send() and recv().

   SHUT_RD, SHUT_RDWR, SHUT_WR
       Direction constants for shutdown().

   INADDR_ANY, INADDR_BROADCAST, INADDR_LOOPBACK, INADDR_NONE
       Constants giving the special &quot;AF_INET&quot; addresses for wildcard, broadcast,
       local loopback, and invalid addresses.

       Normally equivalent to inet_aton(&apos;0.0.0.0&apos;),
       inet_aton(&apos;255.255.255.255&apos;), inet_aton(&apos;localhost&apos;) and
       inet_aton(&apos;255.255.255.255&apos;) respectively.

   IPPROTO_IP, IPPROTO_IPV6, IPPROTO_TCP, ...
       IP protocol constants to use as the third argument to socket(), the level
       argument to getsockopt() or setsockopt(), or the value of the
       &quot;SO_PROTOCOL&quot; socket option.

   TCP_CORK, TCP_KEEPALIVE, TCP_NODELAY, ...
       Socket option name constants for TCP socket options at the &quot;IPPROTO_TCP&quot;
       level.

   IN6ADDR_ANY, IN6ADDR_LOOPBACK
       Constants giving the special &quot;AF_INET6&quot; addresses for wildcard and local
       loopback.

       Normally equivalent to inet_pton(AF_INET6, &quot;::&quot;) and inet_pton(AF_INET6,
       &quot;::1&quot;) respectively.

   IPV6_ADD_MEMBERSHIP, IPV6_MTU, IPV6_V6ONLY, ...
       Socket option name constants for IPv6 socket options at the
       &quot;IPPROTO_IPV6&quot; level.

STRUCTURE MANIPULATORS
       The following functions convert between lists of Perl values and packed
       binary strings representing structures.

   $family = sockaddr_family $sockaddr
       Takes a packed socket address (as returned by pack_sockaddr_in(),
       pack_sockaddr_un() or the perl builtin functions getsockname() and
       getpeername()). Returns the address family tag. This will be one of the
       &quot;AF_*&quot; constants, such as &quot;AF_INET&quot; for a &quot;sockaddr_in&quot; addresses or
       &quot;AF_UNIX&quot; for a &quot;sockaddr_un&quot;. It can be used to figure out what unpack
       to use for a sockaddr of unknown type.

   $sockaddr = pack_sockaddr_in $port, $ip_address
       Takes two arguments, a port number and an opaque string (as returned by
       inet_aton(), or a v-string). Returns the &quot;sockaddr_in&quot; structure with
       those arguments packed in and &quot;AF_INET&quot; filled in. For Internet domain
       sockets, this structure is normally what you need for the arguments in
       bind(), connect(), and send().

   ($port, $ip_address) = unpack_sockaddr_in $sockaddr
       Takes a &quot;sockaddr_in&quot; structure (as returned by pack_sockaddr_in(),
       getpeername() or recv()). Returns a list of two elements: the port and an
       opaque string representing the IP address (you can use inet_ntoa() to
       convert the address to the four-dotted numeric format). Will croak if the
       structure does not represent an &quot;AF_INET&quot; address.

   $sockaddr = sockaddr_in $port, $ip_address
   ($port, $ip_address) = sockaddr_in $sockaddr
       A wrapper of pack_sockaddr_in() or unpack_sockaddr_in(). In list context,
       unpacks its argument and returns a list consisting of the port and IP
       address.  In scalar context, packs its port and IP address arguments as a
       &quot;sockaddr_in&quot; and returns it.

       Provided largely for legacy compatibility; it is better to use
       pack_sockaddr_in() or unpack_sockaddr_in() explicitly.

   $sockaddr = pack_sockaddr_in6 $port, $ip6_address, [$scope_id, [$flowinfo]]
       Takes two to four arguments, a port number, an opaque string (as returned
       by inet_pton()), optionally a scope ID number, and optionally a flow
       label number. Returns the &quot;sockaddr_in6&quot; structure with those arguments
       packed in and &quot;AF_INET6&quot; filled in. IPv6 equivalent of
       pack_sockaddr_in().

   ($port, $ip6_address, $scope_id, $flowinfo) = unpack_sockaddr_in6 $sockaddr
       Takes a &quot;sockaddr_in6&quot; structure. Returns a list of four elements: the
       port number, an opaque string representing the IPv6 address, the scope
       ID, and the flow label. (You can use inet_ntop() to convert the address
       to the usual string format). Will croak if the structure does not
       represent an &quot;AF_INET6&quot; address.

   $sockaddr = sockaddr_in6 $port, $ip6_address, [$scope_id, [$flowinfo]]
   ($port, $ip6_address, $scope_id, $flowinfo) = sockaddr_in6 $sockaddr
       A wrapper of pack_sockaddr_in6() or unpack_sockaddr_in6(). In list
       context, unpacks its argument according to unpack_sockaddr_in6(). In
       scalar context, packs its arguments according to pack_sockaddr_in6().

       Provided largely for legacy compatibility; it is better to use
       pack_sockaddr_in6() or unpack_sockaddr_in6() explicitly.

   $sockaddr = pack_sockaddr_un $path
       Takes one argument, a pathname. Returns the &quot;sockaddr_un&quot; structure with
       that path packed in with &quot;AF_UNIX&quot; filled in. For &quot;PF_UNIX&quot; sockets, this
       structure is normally what you need for the arguments in bind(),
       connect(), and send().

   ($path) = unpack_sockaddr_un $sockaddr
       Takes a &quot;sockaddr_un&quot; structure (as returned by pack_sockaddr_un(),
       getpeername() or recv()). Returns a list of one element: the pathname.
       Will croak if the structure does not represent an &quot;AF_UNIX&quot; address.

   $sockaddr = sockaddr_un $path
   ($path) = sockaddr_un $sockaddr
       A wrapper of pack_sockaddr_un() or unpack_sockaddr_un(). In a list
       context, unpacks its argument and returns a list consisting of the
       pathname. In a scalar context, packs its pathname as a &quot;sockaddr_un&quot; and
       returns it.

       Provided largely for legacy compatibility; it is better to use
       pack_sockaddr_un() or unpack_sockaddr_un() explicitly.

       These are only supported if your system has &lt;sys/un.h&gt;.

   $ip_mreq = pack_ip_mreq $multiaddr, $interface
       Takes an IPv4 multicast address and optionally an interface address (or
       &quot;INADDR_ANY&quot;). Returns the &quot;ip_mreq&quot; structure with those arguments
       packed in. Suitable for use with the &quot;IP_ADD_MEMBERSHIP&quot; and
       &quot;IP_DROP_MEMBERSHIP&quot; sockopts.

   ($multiaddr, $interface) = unpack_ip_mreq $ip_mreq
       Takes an &quot;ip_mreq&quot; structure. Returns a list of two elements; the IPv4
       multicast address and interface address.

   $ipv6_mreq = pack_ipv6_mreq $multiaddr6, $ifindex
       Takes an IPv6 multicast address and an interface number. Returns the
       &quot;ipv6_mreq&quot; structure with those arguments packed in. Suitable for use
       with the &quot;IPV6_ADD_MEMBERSHIP&quot; and &quot;IPV6_DROP_MEMBERSHIP&quot; sockopts.

   ($multiaddr6, $ifindex) = unpack_ipv6_mreq $ipv6_mreq
       Takes an &quot;ipv6_mreq&quot; structure. Returns a list of two elements; the IPv6
       address and an interface number.

FUNCTIONS
   $ip_address = inet_aton $string
       Takes a string giving the name of a host, or a textual representation of
       an IP address and translates that to an packed binary address structure
       suitable to pass to pack_sockaddr_in(). If passed a hostname that cannot
       be resolved, returns &quot;undef&quot;. For multi-homed hosts (hosts with more than
       one address), the first address found is returned.

       For portability do not assume that the result of inet_aton() is 32 bits
       wide, in other words, that it would contain only the IPv4 address in
       network order.

       This IPv4-only function is provided largely for legacy reasons. Newly-
       written code should use getaddrinfo() or inet_pton() instead for IPv6
       support.

   $string = inet_ntoa $ip_address
       Takes a packed binary address structure such as returned by
       unpack_sockaddr_in() (or a v-string representing the four octets of the
       IPv4 address in network order) and translates it into a string of the
       form &quot;d.d.d.d&quot; where the &quot;d&quot;s are numbers less than 256 (the normal
       human-readable four dotted number notation for Internet addresses).

       This IPv4-only function is provided largely for legacy reasons. Newly-
       written code should use getnameinfo() or inet_ntop() instead for IPv6
       support.

   $address = inet_pton $family, $string
       Takes an address family (such as &quot;AF_INET&quot; or &quot;AF_INET6&quot;) and a string
       containing a textual representation of an address in that family and
       translates that to an packed binary address structure.

       See also getaddrinfo() for a more powerful and flexible function to look
       up socket addresses given hostnames or textual addresses.

   $string = inet_ntop $family, $address
       Takes an address family and a packed binary address structure and
       translates it into a human-readable textual representation of the
       address; typically in &quot;d.d.d.d&quot; form for &quot;AF_INET&quot; or &quot;hhhh:hhhh::hhhh&quot;
       form for &quot;AF_INET6&quot;.

       See also getnameinfo() for a more powerful and flexible function to turn
       socket addresses into human-readable textual representations.

   ($err, @result) = getaddrinfo $host, $service, [$hints]
       Given both a hostname and service name, this function attempts to resolve
       the host name into a list of network addresses, and the service name into
       a protocol and port number, and then returns a list of address structures
       suitable to connect() to it.

       Given just a host name, this function attempts to resolve it to a list of
       network addresses, and then returns a list of address structures giving
       these addresses.

       Given just a service name, this function attempts to resolve it to a
       protocol and port number, and then returns a list of address structures
       that represent it suitable to bind() to. This use should be combined with
       the &quot;AI_PASSIVE&quot; flag; see below.

       Given neither name, it generates an error.

       If present, $hints should be a reference to a hash, where the following
       keys are recognised:

       flags =&gt; INT
           A bitfield containing &quot;AI_*&quot; constants; see below.

       family =&gt; INT
           Restrict to only generating addresses in this address family

       socktype =&gt; INT
           Restrict to only generating addresses of this socket type

       protocol =&gt; INT
           Restrict to only generating addresses for this protocol

       The return value will be a list; the first value being an error
       indication, followed by a list of address structures (if no error
       occurred).

       The error value will be a dualvar; comparable to the &quot;EI_*&quot; error
       constants, or printable as a human-readable error message string. If no
       error occurred it will be zero numerically and an empty string.

       Each value in the results list will be a hash reference containing the
       following fields:

       family =&gt; INT
           The address family (e.g. &quot;AF_INET&quot;)

       socktype =&gt; INT
           The socket type (e.g. &quot;SOCK_STREAM&quot;)

       protocol =&gt; INT
           The protocol (e.g. &quot;IPPROTO_TCP&quot;)

       addr =&gt; STRING
           The address in a packed string (such as would be returned by
           pack_sockaddr_in())

       canonname =&gt; STRING
           The canonical name for the host if the &quot;AI_CANONNAME&quot; flag was
           provided, or &quot;undef&quot; otherwise. This field will only be present on
           the first returned address.

       The following flag constants are recognised in the $hints hash. Other
       flag constants may exist as provided by the OS.

       AI_PASSIVE
           Indicates that this resolution is for a local bind() for a passive
           (i.e.  listening) socket, rather than an active (i.e. connecting)
           socket.

       AI_CANONNAME
           Indicates that the caller wishes the canonical hostname (&quot;canonname&quot;)
           field of the result to be filled in.

       AI_NUMERICHOST
           Indicates that the caller will pass a numeric address, rather than a
           hostname, and that getaddrinfo() must not perform a resolve operation
           on this name. This flag will prevent a possibly-slow network lookup
           operation, and instead return an error if a hostname is passed.

   ($err, $hostname, $servicename) = getnameinfo $sockaddr, [$flags, [$xflags]]
       Given a packed socket address (such as from getsockname(), getpeername(),
       or returned by getaddrinfo() in a &quot;addr&quot; field), returns the hostname and
       symbolic service name it represents. $flags may be a bitmask of &quot;NI_*&quot;
       constants, or defaults to 0 if unspecified.

       The return value will be a list; the first value being an error
       condition, followed by the hostname and service name.

       The error value will be a dualvar; comparable to the &quot;EI_*&quot; error
       constants, or printable as a human-readable error message string. The
       host and service names will be plain strings.

       The following flag constants are recognised as $flags. Other flag
       constants may exist as provided by the OS.

       NI_NUMERICHOST
           Requests that a human-readable string representation of the numeric
           address be returned directly, rather than performing a name resolve
           operation that may convert it into a hostname. This will also avoid
           potentially-blocking network IO.

       NI_NUMERICSERV
           Requests that the port number be returned directly as a number
           representation rather than performing a name resolve operation that
           may convert it into a service name.

       NI_NAMEREQD
           If a name resolve operation fails to provide a name, then this flag
           will cause getnameinfo() to indicate an error, rather than returning
           the numeric representation as a human-readable string.

       NI_DGRAM
           Indicates that the socket address relates to a &quot;SOCK_DGRAM&quot; socket,
           for the services whose name differs between TCP and UDP protocols.

       The following constants may be supplied as $xflags.

       NIx_NOHOST
           Indicates that the caller is not interested in the hostname of the
           result, so it does not have to be converted. &quot;undef&quot; will be returned
           as the hostname.

       NIx_NOSERV
           Indicates that the caller is not interested in the service name of
           the result, so it does not have to be converted. &quot;undef&quot; will be
           returned as the service name.

getaddrinfo() / getnameinfo() ERROR CONSTANTS
       The following constants may be returned by getaddrinfo() or
       getnameinfo().  Others may be provided by the OS.

       EAI_AGAIN
           A temporary failure occurred during name resolution. The operation
           may be successful if it is retried later.

       EAI_BADFLAGS
           The value of the &quot;flags&quot; hint to getaddrinfo(), or the $flags
           parameter to getnameinfo() contains unrecognised flags.

       EAI_FAMILY
           The &quot;family&quot; hint to getaddrinfo(), or the family of the socket
           address passed to getnameinfo() is not supported.

       EAI_NODATA
           The host name supplied to getaddrinfo() did not provide any usable
           address data.

       EAI_NONAME
           The host name supplied to getaddrinfo() does not exist, or the
           address supplied to getnameinfo() is not associated with a host name
           and the &quot;NI_NAMEREQD&quot; flag was supplied.

       EAI_SERVICE
           The service name supplied to getaddrinfo() is not available for the
           socket type given in the $hints.

EXAMPLES
   Lookup for connect()
       The getaddrinfo() function converts a hostname and a service name into a
       list of structures, each containing a potential way to connect() to the
       named service on the named host.

        use IO::Socket;
        use Socket qw(SOCK_STREAM getaddrinfo);

        my %hints = (socktype =&gt; SOCK_STREAM);
        my ($err, @res) = getaddrinfo(&quot;localhost&quot;, &quot;echo&quot;, \%hints);
        die &quot;Cannot getaddrinfo - $err&quot; if $err;

        my $sock;

        foreach my $ai (@res) {
            my $candidate = IO::Socket-&gt;new();

            $candidate-&gt;socket($ai-&gt;{family}, $ai-&gt;{socktype}, $ai-&gt;{protocol})
                or next;

            $candidate-&gt;connect($ai-&gt;{addr})
                or next;

            $sock = $candidate;
            last;
        }

        die &quot;Cannot connect to localhost:echo&quot; unless $sock;

        $sock-&gt;print(&quot;Hello, world!\n&quot;);
        print &lt;$sock&gt;;

       Because a list of potential candidates is returned, the &quot;while&quot; loop
       tries each in turn until it it finds one that succeeds both the socket()
       and connect() calls.

       This function performs the work of the legacy functions gethostbyname(),
       getservbyname(), inet_aton() and pack_sockaddr_in().

       In practice this logic is better performed by IO::Socket::IP.

   Making a human-readable string out of an address
       The getnameinfo() function converts a socket address, such as returned by
       getsockname() or getpeername(), into a pair of human-readable strings
       representing the address and service name.

        use IO::Socket::IP;
        use Socket qw(getnameinfo);

        my $server = IO::Socket::IP-&gt;new(LocalPort =&gt; 12345, Listen =&gt; 1) or
            die &quot;Cannot listen - $@&quot;;
&#12;        my $socket = $server-&gt;accept or die &quot;accept: $!&quot;;

        my ($err, $hostname, $servicename) = getnameinfo($socket-&gt;peername);
        die &quot;Cannot getnameinfo - $err&quot; if $err;

        print &quot;The peer is connected from $hostname\n&quot;;

       Since in this example only the hostname was used, the redundant
       conversion of the port number into a service name may be omitted by
       passing the &quot;NIx_NOSERV&quot; flag.

        use Socket qw(getnameinfo NIx_NOSERV);

        my ($err, $hostname) = getnameinfo($socket-&gt;peername, 0, NIx_NOSERV);

       This function performs the work of the legacy functions
       unpack_sockaddr_in(), inet_ntoa(), gethostbyaddr() and getservbyport().

       In practice this logic is better performed by IO::Socket::IP.

   Resolving hostnames into IP addresses
       To turn a hostname into a human-readable plain IP address use
       getaddrinfo() to turn the hostname into a list of socket structures, then
       getnameinfo() on each one to make it a readable IP address again.

        use Socket qw(:addrinfo SOCK_RAW);

        my ($err, @res) = getaddrinfo($hostname, &quot;&quot;, {socktype =&gt; SOCK_RAW});
        die &quot;Cannot getaddrinfo - $err&quot; if $err;

        while( my $ai = shift @res ) {
            my ($err, $ipaddr) = getnameinfo($ai-&gt;{addr}, NI_NUMERICHOST, NIx_NOSERV);
            die &quot;Cannot getnameinfo - $err&quot; if $err;

            print &quot;$ipaddr\n&quot;;
        }

       The &quot;socktype&quot; hint to getaddrinfo() filters the results to only include
       one socket type and protocol. Without this most OSes return three
       combinations, for &quot;SOCK_STREAM&quot;, &quot;SOCK_DGRAM&quot; and &quot;SOCK_RAW&quot;, resulting
       in triplicate output of addresses. The &quot;NI_NUMERICHOST&quot; flag to
       getnameinfo() causes it to return a string-formatted plain IP address,
       rather than reverse resolving it back into a hostname.

       This combination performs the work of the legacy functions
       gethostbyname() and inet_ntoa().

   Accessing socket options
       The many &quot;SO_*&quot; and other constants provide the socket option names for
       getsockopt() and setsockopt().

        use IO::Socket::INET;
        use Socket qw(SOL_SOCKET SO_RCVBUF IPPROTO_IP IP_TTL);

        my $socket = IO::Socket::INET-&gt;new(LocalPort =&gt; 0, Proto =&gt; &apos;udp&apos;)
            or die &quot;Cannot create socket: $@&quot;;

        $socket-&gt;setsockopt(SOL_SOCKET, SO_RCVBUF, 64*1024) or
            die &quot;setsockopt: $!&quot;;

        print &quot;Receive buffer is &quot;, $socket-&gt;getsockopt(SOL_SOCKET, SO_RCVBUF),
            &quot; bytes\n&quot;;

        print &quot;IP TTL is &quot;, $socket-&gt;getsockopt(IPPROTO_IP, IP_TTL), &quot;\n&quot;;

       As a convenience, IO::Socket&apos;s setsockopt() method will convert a number
       into a packed byte buffer, and getsockopt() will unpack a byte buffer of
       the correct size back into a number.

AUTHOR
       This module was originally maintained in Perl core by the Perl 5 Porters.
&#12;       It was extracted to dual-life on CPAN at version 1.95 by Paul Evans
       &lt;leonerd@leonerd.org.uk&gt;



perl v5.14.2                       2012-06-06                        Socket(3pm)

</pre></body></html>
