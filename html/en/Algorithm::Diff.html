<html>
<head><meta charset=utf-8/>
<title>Algorithm::Diff - Compute `intelligent' differences between two files / lists</title></head>
<body><pre>

Algorithm::Diff(3pm)   User Contributed Perl Documentation  Algorithm::Diff(3pm)



NAME
       Algorithm::Diff - Compute `intelligent&apos; differences between two files /
       lists

SYNOPSIS
           require Algorithm::Diff;

           # This example produces traditional &apos;diff&apos; output:

           my $diff = Algorithm::Diff-&gt;new( \@seq1, \@seq2 );

           $diff-&gt;Base( 1 );   # Return line numbers, not indices
           while(  $diff-&gt;Next()  ) {
               next   if  $diff-&gt;Same();
               my $sep = &apos;&apos;;
               if(  ! $diff-&gt;Items(2)  ) {
                   printf &quot;%d,%dd%d\n&quot;,
                       $diff-&gt;Get(qw( Min1 Max1 Max2 ));
               } elsif(  ! $diff-&gt;Items(1)  ) {
                   printf &quot;%da%d,%d\n&quot;,
                       $diff-&gt;Get(qw( Max1 Min2 Max2 ));
               } else {
                   $sep = &quot;---\n&quot;;
                   printf &quot;%d,%dc%d,%d\n&quot;,
                       $diff-&gt;Get(qw( Min1 Max1 Min2 Max2 ));
               }
               print &quot;&lt; $_&quot;   for  $diff-&gt;Items(1);
               print $sep;
               print &quot;&gt; $_&quot;   for  $diff-&gt;Items(2);
           }


           # Alternate interfaces:

           use Algorithm::Diff qw(
               LCS LCS_length LCSidx
               diff sdiff compact_diff
               traverse_sequences traverse_balanced );

           @lcs    = LCS( \@seq1, \@seq2 );
           $lcsref = LCS( \@seq1, \@seq2 );
           $count  = LCS_length( \@seq1, \@seq2 );

           ( $seq1idxref, $seq2idxref ) = LCSidx( \@seq1, \@seq2 );


           # Complicated interfaces:

           @diffs  = diff( \@seq1, \@seq2 );

           @sdiffs = sdiff( \@seq1, \@seq2 );

           @cdiffs = compact_diff( \@seq1, \@seq2 );

           traverse_sequences(
               \@seq1,
               \@seq2,
               {   MATCH     =&gt; \&amp;callback1,
                   DISCARD_A =&gt; \&amp;callback2,
                   DISCARD_B =&gt; \&amp;callback3,
               },
               \&amp;key_generator,
               @extra_args,
           );

           traverse_balanced(
               \@seq1,
               \@seq2,
               {   MATCH     =&gt; \&amp;callback1,
                   DISCARD_A =&gt; \&amp;callback2,
                   DISCARD_B =&gt; \&amp;callback3,
                   CHANGE    =&gt; \&amp;callback4,
               },
               \&amp;key_generator,
               @extra_args,
           );

INTRODUCTION
       (by Mark-Jason Dominus)

       I once read an article written by the authors of &quot;diff&quot;; they said that
       they worked very hard on the algorithm until they found the right one.

       I think what they ended up using (and I hope someone will correct me,
       because I am not very confident about this) was the `longest common
       subsequence&apos; method.  In the LCS problem, you have two sequences of
       items:

           a b c d f g h j q z

           a b c d e f g i j k r x y z

       and you want to find the longest sequence of items that is present in
       both original sequences in the same order.  That is, you want to find a
       new sequence S which can be obtained from the first sequence by deleting
       some items, and from the secend sequence by deleting other items.  You
       also want S to be as long as possible.  In this case S is

           a b c d f g j z

       From there it&apos;s only a small step to get diff-like output:

           e   h i   k   q r x y
           +   - +   +   - + + +

       This module solves the LCS problem.  It also includes a canned function
       to generate &quot;diff&quot;-like output.

       It might seem from the example above that the LCS of two sequences is
       always pretty obvious, but that&apos;s not always the case, especially when
       the two sequences have many repeated elements.  For example, consider

           a x b y c z p d q
           a b c a x b y c z

       A naive approach might start by matching up the &quot;a&quot; and &quot;b&quot; that appear
       at the beginning of each sequence, like this:

           a x b y c         z p d q
           a   b   c a b y c z

       This finds the common subsequence &quot;a b c z&quot;.  But actually, the LCS is &quot;a
       x b y c z&quot;:

                 a x b y c z p d q
           a b c a x b y c z

       or

           a       x b y c z p d q
           a b c a x b y c z

USAGE
       (See also the README file and several example scripts include with this
       module.)

       This module now provides an object-oriented interface that uses less
       memory and is easier to use than most of the previous procedural
       interfaces.  It also still provides several exportable functions.  We&apos;ll
       deal with these in ascending order of difficulty:  &quot;LCS&quot;, &quot;LCS_length&quot;,
       &quot;LCSidx&quot;, OO interface, &quot;prepare&quot;, &quot;diff&quot;, &quot;sdiff&quot;, &quot;traverse_sequences&quot;,
       and &quot;traverse_balanced&quot;.

   &quot;LCS&quot;
       Given references to two lists of items, LCS returns an array containing
       their longest common subsequence.  In scalar context, it returns a
       reference to such a list.

           @lcs    = LCS( \@seq1, \@seq2 );
           $lcsref = LCS( \@seq1, \@seq2 );

       &quot;LCS&quot; may be passed an optional third parameter; this is a CODE reference
       to a key generation function.  See &quot;KEY GENERATION FUNCTIONS&quot;.

           @lcs    = LCS( \@seq1, \@seq2, \&amp;keyGen, @args );
           $lcsref = LCS( \@seq1, \@seq2, \&amp;keyGen, @args );

       Additional parameters, if any, will be passed to the key generation
       routine.

   &quot;LCS_length&quot;
       This is just like &quot;LCS&quot; except it only returns the length of the longest
       common subsequence.  This provides a performance gain of about 9%
       compared to &quot;LCS&quot;.

   &quot;LCSidx&quot;
       Like &quot;LCS&quot; except it returns references to two arrays.  The first array
       contains the indices into @seq1 where the LCS items are located.  The
       second array contains the indices into @seq2 where the LCS items are
       located.

       Therefore, the following three lists will contain the same values:

           my( $idx1, $idx2 ) = LCSidx( \@seq1, \@seq2 );
           my @list1 = @seq1[ @$idx1 ];
           my @list2 = @seq2[ @$idx2 ];
           my @list3 = LCS( \@seq1, \@seq2 );

   &quot;new&quot;
           $diff = Algorithm::Diffs-&gt;new( \@seq1, \@seq2 );
           $diff = Algorithm::Diffs-&gt;new( \@seq1, \@seq2, \%opts );

       &quot;new&quot; computes the smallest set of additions and deletions necessary to
       turn the first sequence into the second and compactly records them in the
       object.

       You use the object to iterate over hunks, where each hunk represents a
       contiguous section of items which should be added, deleted, replaced, or
       left unchanged.

       The following summary of all of the methods looks a lot like Perl code
       but some of the symbols have different meanings:

           [ ]     Encloses optional arguments
           :       Is followed by the default value for an optional argument
           |       Separates alternate return results
&#12;       Method summary:

           $obj        = Algorithm::Diff-&gt;new( \@seq1, \@seq2, [ \%opts ] );
           $pos        = $obj-&gt;Next(  [ $count : 1 ] );
           $revPos     = $obj-&gt;Prev(  [ $count : 1 ] );
           $obj        = $obj-&gt;Reset( [ $pos : 0 ] );
           $copy       = $obj-&gt;Copy(  [ $pos, [ $newBase ] ] );
           $oldBase    = $obj-&gt;Base(  [ $newBase ] );

       Note that all of the following methods &quot;die&quot; if used on an object that is
       &quot;reset&quot; (not currently pointing at any hunk).

           $bits       = $obj-&gt;Diff(  );
           @items|$cnt = $obj-&gt;Same(  );
           @items|$cnt = $obj-&gt;Items( $seqNum );
           @idxs |$cnt = $obj-&gt;Range( $seqNum, [ $base ] );
           $minIdx     = $obj-&gt;Min(   $seqNum, [ $base ] );
           $maxIdx     = $obj-&gt;Max(   $seqNum, [ $base ] );
           @values     = $obj-&gt;Get(   @names );

       Passing in &quot;undef&quot; for an optional argument is always treated the same as
       if no argument were passed in.

       &quot;Next&quot;
               $pos = $diff-&gt;Next();    # Move forward 1 hunk
               $pos = $diff-&gt;Next( 2 ); # Move forward 2 hunks
               $pos = $diff-&gt;Next(-5);  # Move backward 5 hunks

           &quot;Next&quot; moves the object to point at the next hunk.  The object starts
           out &quot;reset&quot;, which means it isn&apos;t pointing at any hunk.  If the
           object is reset, then &quot;Next()&quot; moves to the first hunk.

           &quot;Next&quot; returns a true value iff the move didn&apos;t go past the last
           hunk.  So Next(0) will return true iff the object is not reset.

           Actually, &quot;Next&quot; returns the object&apos;s new position, which is a number
           between 1 and the number of hunks (inclusive), or returns a false
           value.

       &quot;Prev&quot;
           &quot;Prev($N)&quot; is almost identical to &quot;Next(-$N)&quot;; it moves to the $Nth
           previous hunk.  On a &apos;reset&apos; object, &quot;Prev()&quot; [and &quot;Next(-1)&quot;] move
           to the last hunk.

           The position returned by &quot;Prev&quot; is relative to the end of the hunks;
           -1 for the last hunk, -2 for the second-to-last, etc.

       &quot;Reset&quot;
               $diff-&gt;Reset();     # Reset the object&apos;s position
               $diff-&gt;Reset($pos); # Move to the specified hunk
               $diff-&gt;Reset(1);    # Move to the first hunk
               $diff-&gt;Reset(-1);   # Move to the last hunk

           &quot;Reset&quot; returns the object, so, for example, you could use
           &quot;$diff-&gt;Reset()-&gt;Next(-1)&quot; to get the number of hunks.

       &quot;Copy&quot;
               $copy = $diff-&gt;Copy( $newPos, $newBase );

           &quot;Copy&quot; returns a copy of the object.  The copy and the orignal object
           share most of their data, so making copies takes very little memory.
           The copy maintains its own position (separate from the original),
           which is the main purpose of copies.  It also maintains its own base.

           By default, the copy&apos;s position starts out the same as the original
           object&apos;s position.  But &quot;Copy&quot; takes an optional first argument to
           set the new position, so the following three snippets are equivalent:

               $copy = $diff-&gt;Copy($pos);
&#12;               $copy = $diff-&gt;Copy();
               $copy-&gt;Reset($pos);

               $copy = $diff-&gt;Copy()-&gt;Reset($pos);

           &quot;Copy&quot; takes an optional second argument to set the base for the
           copy.  If you wish to change the base of the copy but leave the
           position the same as in the original, here are two equivalent ways:

               $copy = $diff-&gt;Copy();
               $copy-&gt;Base( 0 );

               $copy = $diff-&gt;Copy(undef,0);

           Here are two equivalent way to get a &quot;reset&quot; copy:

               $copy = $diff-&gt;Copy(0);

               $copy = $diff-&gt;Copy()-&gt;Reset();

       &quot;Diff&quot;
               $bits = $obj-&gt;Diff();

           &quot;Diff&quot; returns a true value iff the current hunk contains items that
           are different between the two sequences.  It actually returns one of
           the follow 4 values:

           3   &quot;3==(1|2)&quot;.  This hunk contains items from @seq1 and the items
               from @seq2 that should replace them.  Both sequence 1 and 2
               contain changed items so both the 1 and 2 bits are set.

           2   This hunk only contains items from @seq2 that should be inserted
               (not items from @seq1).  Only sequence 2 contains changed items
               so only the 2 bit is set.

           1   This hunk only contains items from @seq1 that should be deleted
               (not items from @seq2).  Only sequence 1 contains changed items
               so only the 1 bit is set.

           0   This means that the items in this hunk are the same in both
               sequences.  Neither sequence 1 nor 2 contain changed items so
               neither the 1 nor the 2 bits are set.

       &quot;Same&quot;
           &quot;Same&quot; returns a true value iff the current hunk contains items that
           are the same in both sequences.  It actually returns the list of
           items if they are the same or an emty list if they aren&apos;t.  In a
           scalar context, it returns the size of the list.

       &quot;Items&quot;
               $count = $diff-&gt;Items(2);
               @items = $diff-&gt;Items($seqNum);

           &quot;Items&quot; returns the (number of) items from the specified sequence
           that are part of the current hunk.

           If the current hunk contains only insertions, then &quot;$diff-&gt;Items(1)&quot;
           will return an empty list (0 in a scalar conext).  If the current
           hunk contains only deletions, then &quot;$diff-&gt;Items(2)&quot; will return an
           empty list (0 in a scalar conext).

           If the hunk contains replacements, then both &quot;$diff-&gt;Items(1)&quot; and
           &quot;$diff-&gt;Items(2)&quot; will return different, non-empty lists.

           Otherwise, the hunk contains identical items and all of the following
           will return the same lists:

               @items = $diff-&gt;Items(1);
               @items = $diff-&gt;Items(2);
               @items = $diff-&gt;Same();
&#12;       &quot;Range&quot;
               $count = $diff-&gt;Range( $seqNum );
               @indices = $diff-&gt;Range( $seqNum );
               @indices = $diff-&gt;Range( $seqNum, $base );

           &quot;Range&quot; is like &quot;Items&quot; except that it returns a list of indices to
           the items rather than the items themselves.  By default, the index of
           the first item (in each sequence) is 0 but this can be changed by
           calling the &quot;Base&quot; method.  So, by default, the following two
           snippets return the same lists:

               @list = $diff-&gt;Items(2);
               @list = @seq2[ $diff-&gt;Range(2) ];

           You can also specify the base to use as the second argument.  So the
           following two snippets always return the same lists:

               @list = $diff-&gt;Items(1);
               @list = @seq1[ $diff-&gt;Range(1,0) ];

       &quot;Base&quot;
               $curBase = $diff-&gt;Base();
               $oldBase = $diff-&gt;Base($newBase);

           &quot;Base&quot; sets and/or returns the current base (usually 0 or 1) that is
           used when you request range information.  The base defaults to 0 so
           that range information is returned as array indices.  You can set the
           base to 1 if you want to report traditional line numbers instead.

       &quot;Min&quot;
               $min1 = $diff-&gt;Min(1);
               $min = $diff-&gt;Min( $seqNum, $base );

           &quot;Min&quot; returns the first value that &quot;Range&quot; would return (given the
           same arguments) or returns &quot;undef&quot; if &quot;Range&quot; would return an empty
           list.

       &quot;Max&quot;
           &quot;Max&quot; returns the last value that &quot;Range&quot; would return or &quot;undef&quot;.

       &quot;Get&quot;
               ( $n, $x, $r ) = $diff-&gt;Get(qw( min1 max1 range1 ));
               @values = $diff-&gt;Get(qw( 0min2 1max2 range2 same base ));

           &quot;Get&quot; returns one or more scalar values.  You pass in a list of the
           names of the values you want returned.  Each name must match one of
           the following regexes:

               /^(-?\d+)?(min|max)[12]$/i
               /^(range[12]|same|diff|base)$/i

           The 1 or 2 after a name says which sequence you want the information
           for (and where allowed, it is required).  The optional number before
           &quot;min&quot; or &quot;max&quot; is the base to use.  So the following equalities hold:

               $diff-&gt;Get(&apos;min1&apos;) == $diff-&gt;Min(1)
               $diff-&gt;Get(&apos;0min2&apos;) == $diff-&gt;Min(2,0)

           Using &quot;Get&quot; in a scalar context when you&apos;ve passed in more than one
           name is a fatal error (&quot;die&quot; is called).

   &quot;prepare&quot;
       Given a reference to a list of items, &quot;prepare&quot; returns a reference to a
       hash which can be used when comparing this sequence to other sequences
       with &quot;LCS&quot; or &quot;LCS_length&quot;.

           $prep = prepare( \@seq1 );
           for $i ( 0 .. 10_000 )
           {
               @lcs = LCS( $prep, $seq[$i] );
               # do something useful with @lcs
           }
&#12;       &quot;prepare&quot; may be passed an optional third parameter; this is a CODE
       reference to a key generation function.  See &quot;KEY GENERATION FUNCTIONS&quot;.

           $prep = prepare( \@seq1, \&amp;keyGen );
           for $i ( 0 .. 10_000 )
           {
               @lcs = LCS( $seq[$i], $prep, \&amp;keyGen );
               # do something useful with @lcs
           }

       Using &quot;prepare&quot; provides a performance gain of about 50% when calling LCS
       many times compared with not preparing.

   &quot;diff&quot;
           @diffs     = diff( \@seq1, \@seq2 );
           $diffs_ref = diff( \@seq1, \@seq2 );

       &quot;diff&quot; computes the smallest set of additions and deletions necessary to
       turn the first sequence into the second, and returns a description of
       these changes.  The description is a list of hunks; each hunk represents
       a contiguous section of items which should be added, deleted, or
       replaced.  (Hunks containing unchanged items are not included.)

       The return value of &quot;diff&quot; is a list of hunks, or, in scalar context, a
       reference to such a list.  If there are no differences, the list will be
       empty.

       Here is an example.  Calling &quot;diff&quot; for the following two sequences:

           a b c e h j l m n p
           b c d e f j k l m r s t

       would produce the following list:

           (
             [ [ &apos;-&apos;, 0, &apos;a&apos; ] ],

             [ [ &apos;+&apos;, 2, &apos;d&apos; ] ],

             [ [ &apos;-&apos;, 4, &apos;h&apos; ],
               [ &apos;+&apos;, 4, &apos;f&apos; ] ],

             [ [ &apos;+&apos;, 6, &apos;k&apos; ] ],

             [ [ &apos;-&apos;,  8, &apos;n&apos; ],
               [ &apos;-&apos;,  9, &apos;p&apos; ],
               [ &apos;+&apos;,  9, &apos;r&apos; ],
               [ &apos;+&apos;, 10, &apos;s&apos; ],
               [ &apos;+&apos;, 11, &apos;t&apos; ] ],
           )

       There are five hunks here.  The first hunk says that the &quot;a&quot; at position
       0 of the first sequence should be deleted (&quot;-&quot;).  The second hunk says
       that the &quot;d&quot; at position 2 of the second sequence should be inserted
       (&quot;+&quot;).  The third hunk says that the &quot;h&quot; at position 4 of the first
       sequence should be removed and replaced with the &quot;f&quot; from position 4 of
       the second sequence.  And so on.

       &quot;diff&quot; may be passed an optional third parameter; this is a CODE
       reference to a key generation function.  See &quot;KEY GENERATION FUNCTIONS&quot;.

       Additional parameters, if any, will be passed to the key generation
       routine.

   &quot;sdiff&quot;
           @sdiffs     = sdiff( \@seq1, \@seq2 );
           $sdiffs_ref = sdiff( \@seq1, \@seq2 );

       &quot;sdiff&quot; computes all necessary components to show two sequences and their
       minimized differences side by side, just like the Unix-utility sdiff
       does:

           same             same
           before     |     after
           old        &lt;     -
           -          &gt;     new
&#12;       It returns a list of array refs, each pointing to an array of display
       instructions. In scalar context it returns a reference to such a list. If
       there are no differences, the list will have one entry per item, each
       indicating that the item was unchanged.

       Display instructions consist of three elements: A modifier indicator
       (&quot;+&quot;: Element added, &quot;-&quot;: Element removed, &quot;u&quot;: Element unmodified, &quot;c&quot;:
       Element changed) and the value of the old and new elements, to be
       displayed side-by-side.

       An &quot;sdiff&quot; of the following two sequences:

           a b c e h j l m n p
           b c d e f j k l m r s t

       results in

           ( [ &apos;-&apos;, &apos;a&apos;, &apos;&apos;  ],
             [ &apos;u&apos;, &apos;b&apos;, &apos;b&apos; ],
             [ &apos;u&apos;, &apos;c&apos;, &apos;c&apos; ],
             [ &apos;+&apos;, &apos;&apos;,  &apos;d&apos; ],
             [ &apos;u&apos;, &apos;e&apos;, &apos;e&apos; ],
             [ &apos;c&apos;, &apos;h&apos;, &apos;f&apos; ],
             [ &apos;u&apos;, &apos;j&apos;, &apos;j&apos; ],
             [ &apos;+&apos;, &apos;&apos;,  &apos;k&apos; ],
             [ &apos;u&apos;, &apos;l&apos;, &apos;l&apos; ],
             [ &apos;u&apos;, &apos;m&apos;, &apos;m&apos; ],
             [ &apos;c&apos;, &apos;n&apos;, &apos;r&apos; ],
             [ &apos;c&apos;, &apos;p&apos;, &apos;s&apos; ],
             [ &apos;+&apos;, &apos;&apos;,  &apos;t&apos; ],
           )

       &quot;sdiff&quot; may be passed an optional third parameter; this is a CODE
       reference to a key generation function.  See &quot;KEY GENERATION FUNCTIONS&quot;.

       Additional parameters, if any, will be passed to the key generation
       routine.

   &quot;compact_diff&quot;
       &quot;compact_diff&quot; is much like &quot;sdiff&quot; except it returns a much more compact
       description consisting of just one flat list of indices.  An example
       helps explain the format:

           my @a = qw( a b c   e  h j   l m n p      );
           my @b = qw(   b c d e f  j k l m    r s t );
           @cdiff = compact_diff( \@a, \@b );
           # Returns:
           #   @a      @b       @a       @b
           #  start   start   values   values
           (    0,      0,   #       =
                0,      0,   #    a  !
                1,      0,   #  b c  =  b c
                3,      2,   #       !  d
                3,      3,   #    e  =  e
                4,      4,   #    f  !  h
                5,      5,   #    j  =  j
                6,      6,   #       !  k
                6,      7,   #  l m  =  l m
                8,      9,   #  n p  !  r s t
               10,     12,   #
           );

       The 0th, 2nd, 4th, etc. entries are all indices into @seq1 (@a in the
       above example) indicating where a hunk begins.  The 1st, 3rd, 5th, etc.
       entries are all indices into @seq2 (@b in the above example) indicating
       where the same hunk begins.

       So each pair of indices (except the last pair) describes where a hunk
       begins (in each sequence).  Since each hunk must end at the item just
       before the item that starts the next hunk, the next pair of indices can
       be used to determine where the hunk ends.

       So, the first 4 entries (0..3) describe the first hunk.  Entries 0 and 1
       describe where the first hunk begins (and so are always both 0).  Entries
       2 and 3 describe where the next hunk begins, so subtracting 1 from each
       tells us where the first hunk ends.  That is, the first hunk contains
       items $diff[0] through &quot;$diff[2] - 1&quot; of the first sequence and contains
       items $diff[1] through &quot;$diff[3] - 1&quot; of the second sequence.

       In other words, the first hunk consists of the following two lists of
       items:

                      #  1st pair     2nd pair
                      # of indices   of indices
           @list1 = @a[ $cdiff[0] .. $cdiff[2]-1 ];
           @list2 = @b[ $cdiff[1] .. $cdiff[3]-1 ];
                      # Hunk start   Hunk end

       Note that the hunks will always alternate between those that are part of
       the LCS (those that contain unchanged items) and those that contain
       changes.  This means that all we need to be told is whether the first
       hunk is a &apos;same&apos; or &apos;diff&apos; hunk and we can determine which of the other
       hunks contain &apos;same&apos; items or &apos;diff&apos; items.

       By convention, we always make the first hunk contain unchanged items.  So
       the 1st, 3rd, 5th, etc. hunks (all odd-numbered hunks if you start
       counting from 1) all contain unchanged items.  And the 2nd, 4th, 6th,
       etc. hunks (all even-numbered hunks if you start counting from 1) all
       contain changed items.

       Since @a and @b don&apos;t begin with the same value, the first hunk in our
       example is empty (otherwise we&apos;d violate the above convention).  Note
       that the first 4 index values in our example are all zero.  Plug these
       values into our previous code block and we get:

           @hunk1a = @a[ 0 .. 0-1 ];
           @hunk1b = @b[ 0 .. 0-1 ];

       And &quot;0..-1&quot; returns the empty list.

       Move down one pair of indices (2..5) and we get the offset ranges for the
       second hunk, which contains changed items.

       Since @diff[2..5] contains (0,0,1,0) in our example, the second hunk
       consists of these two lists of items:

               @hunk2a = @a[ $cdiff[2] .. $cdiff[4]-1 ];
               @hunk2b = @b[ $cdiff[3] .. $cdiff[5]-1 ];
           # or
               @hunk2a = @a[ 0 .. 1-1 ];
               @hunk2b = @b[ 0 .. 0-1 ];
           # or
               @hunk2a = @a[ 0 .. 0 ];
               @hunk2b = @b[ 0 .. -1 ];
           # or
               @hunk2a = ( &apos;a&apos; );
               @hunk2b = ( );

       That is, we would delete item 0 (&apos;a&apos;) from @a.

       Since @diff[4..7] contains (1,0,3,2) in our example, the third hunk
       consists of these two lists of items:

               @hunk3a = @a[ $cdiff[4] .. $cdiff[6]-1 ];
               @hunk3a = @b[ $cdiff[5] .. $cdiff[7]-1 ];
           # or
               @hunk3a = @a[ 1 .. 3-1 ];
               @hunk3a = @b[ 0 .. 2-1 ];
           # or
               @hunk3a = @a[ 1 .. 2 ];
               @hunk3a = @b[ 0 .. 1 ];
           # or
               @hunk3a = qw( b c );
               @hunk3a = qw( b c );

       Note that this third hunk contains unchanged items as our convention
       demands.

       You can continue this process until you reach the last two indices, which
       will always be the number of items in each sequence.  This is required so
       that subtracting one from each will give you the indices to the last
       items in each sequence.

   &quot;traverse_sequences&quot;
       &quot;traverse_sequences&quot; used to be the most general facility provided by
       this module (the new OO interface is more powerful and much easier to
       use).

       Imagine that there are two arrows.  Arrow A points to an element of
       sequence A, and arrow B points to an element of the sequence B.
       Initially, the arrows point to the first elements of the respective
       sequences.  &quot;traverse_sequences&quot; will advance the arrows through the
       sequences one element at a time, calling an appropriate user-specified
       callback function before each advance.  It willadvance the arrows in such
       a way that if there are equal elements $A[$i] and $B[$j] which are equal
       and which are part of the LCS, there will be some moment during the
       execution of &quot;traverse_sequences&quot; when arrow A is pointing to $A[$i] and
       arrow B is pointing to $B[$j].  When this happens, &quot;traverse_sequences&quot;
       will call the &quot;MATCH&quot; callback function and then it will advance both
       arrows.

       Otherwise, one of the arrows is pointing to an element of its sequence
       that is not part of the LCS.  &quot;traverse_sequences&quot; will advance that
       arrow and will call the &quot;DISCARD_A&quot; or the &quot;DISCARD_B&quot; callback,
       depending on which arrow it advanced.  If both arrows point to elements
       that are not part of the LCS, then &quot;traverse_sequences&quot; will advance one
       of them and call the appropriate callback, but it is not specified which
       it will call.

       The arguments to &quot;traverse_sequences&quot; are the two sequences to traverse,
       and a hash which specifies the callback functions, like this:

           traverse_sequences(
               \@seq1, \@seq2,
               {   MATCH =&gt; $callback_1,
                   DISCARD_A =&gt; $callback_2,
                   DISCARD_B =&gt; $callback_3,
               }
           );

       Callbacks for MATCH, DISCARD_A, and DISCARD_B are invoked with at least
       the indices of the two arrows as their arguments.  They are not expected
       to return any values.  If a callback is omitted from the table, it is not
       called.

       Callbacks for A_FINISHED and B_FINISHED are invoked with at least the
       corresponding index in A or B.

       If arrow A reaches the end of its sequence, before arrow B does,
       &quot;traverse_sequences&quot; will call the &quot;A_FINISHED&quot; callback when it advances
       arrow B, if there is such a function; if not it will call &quot;DISCARD_B&quot;
       instead.  Similarly if arrow B finishes first.  &quot;traverse_sequences&quot;
       returns when both arrows are at the ends of their respective sequences.
       It returns true on success and false on failure.  At present there is no
       way to fail.

       &quot;traverse_sequences&quot; may be passed an optional fourth parameter; this is
       a CODE reference to a key generation function.  See &quot;KEY GENERATION
       FUNCTIONS&quot;.

       Additional parameters, if any, will be passed to the key generation
       function.

       If you want to pass additional parameters to your callbacks, but don&apos;t
       need a custom key generation function, you can get the default by passing
       undef:

           traverse_sequences(
               \@seq1, \@seq2,
               {   MATCH =&gt; $callback_1,
                   DISCARD_A =&gt; $callback_2,
                   DISCARD_B =&gt; $callback_3,
               },
               undef,     # default key-gen
               $myArgument1,
               $myArgument2,
               $myArgument3,
           );

       &quot;traverse_sequences&quot; does not have a useful return value; you are
       expected to plug in the appropriate behavior with the callback functions.

   &quot;traverse_balanced&quot;
       &quot;traverse_balanced&quot; is an alternative to &quot;traverse_sequences&quot;. It uses a
       different algorithm to iterate through the entries in the computed LCS.
       Instead of sticking to one side and showing element changes as insertions
       and deletions only, it will jump back and forth between the two sequences
       and report changes occurring as deletions on one side followed immediatly
       by an insertion on the other side.

       In addition to the &quot;DISCARD_A&quot;, &quot;DISCARD_B&quot;, and &quot;MATCH&quot; callbacks
       supported by &quot;traverse_sequences&quot;, &quot;traverse_balanced&quot; supports a
       &quot;CHANGE&quot; callback indicating that one element got &quot;replaced&quot; by another:

           traverse_balanced(
               \@seq1, \@seq2,
               {   MATCH =&gt; $callback_1,
                   DISCARD_A =&gt; $callback_2,
                   DISCARD_B =&gt; $callback_3,
                   CHANGE    =&gt; $callback_4,
               }
           );

       If no &quot;CHANGE&quot; callback is specified, &quot;traverse_balanced&quot; will map
       &quot;CHANGE&quot; events to &quot;DISCARD_A&quot; and &quot;DISCARD_B&quot; actions, therefore
       resulting in a similar behaviour as &quot;traverse_sequences&quot; with different
       order of events.

       &quot;traverse_balanced&quot; might be a bit slower than &quot;traverse_sequences&quot;,
       noticeable only while processing huge amounts of data.

       The &quot;sdiff&quot; function of this module is implemented as call to
       &quot;traverse_balanced&quot;.

       &quot;traverse_balanced&quot; does not have a useful return value; you are expected
       to plug in the appropriate behavior with the callback functions.

KEY GENERATION FUNCTIONS
       Most of the functions accept an optional extra parameter.  This is a CODE
       reference to a key generating (hashing) function that should return a
       string that uniquely identifies a given element.  It should be the case
       that if two elements are to be considered equal, their keys should be the
       same (and the other way around).  If no key generation function is
       provided, the key will be the element as a string.

       By default, comparisons will use &quot;eq&quot; and elements will be turned into
       keys using the default stringizing operator &apos;&quot;&quot;&apos;.

       Where this is important is when you&apos;re comparing something other than
       strings.  If it is the case that you have multiple different objects that
       should be considered to be equal, you should supply a key generation
       function. Otherwise, you have to make sure that your arrays contain
       unique references.

       For instance, consider this example:

           package Person;

           sub new
           {
               my $package = shift;
               return bless { name =&gt; &apos;&apos;, ssn =&gt; &apos;&apos;, @_ }, $package;
           }

           sub clone
           {
               my $old = shift;
               my $new = bless { %$old }, ref($old);
           }

           sub hash
           {
               return shift()-&gt;{&apos;ssn&apos;};
           }

           my $person1 = Person-&gt;new( name =&gt; &apos;Joe&apos;, ssn =&gt; &apos;123-45-6789&apos; );
           my $person2 = Person-&gt;new( name =&gt; &apos;Mary&apos;, ssn =&gt; &apos;123-47-0000&apos; );
           my $person3 = Person-&gt;new( name =&gt; &apos;Pete&apos;, ssn =&gt; &apos;999-45-2222&apos; );
           my $person4 = Person-&gt;new( name =&gt; &apos;Peggy&apos;, ssn =&gt; &apos;123-45-9999&apos; );
           my $person5 = Person-&gt;new( name =&gt; &apos;Frank&apos;, ssn =&gt; &apos;000-45-9999&apos; );

       If you did this:

           my $array1 = [ $person1, $person2, $person4 ];
           my $array2 = [ $person1, $person3, $person4, $person5 ];
           Algorithm::Diff::diff( $array1, $array2 );
&#12;       everything would work out OK (each of the objects would be converted into
       a string like &quot;Person=HASH(0x82425b0)&quot; for comparison).

       But if you did this:

           my $array1 = [ $person1, $person2, $person4 ];
           my $array2 = [ $person1, $person3, $person4-&gt;clone(), $person5 ];
           Algorithm::Diff::diff( $array1, $array2 );

       $person4 and $person4-&gt;clone() (which have the same name and SSN) would
       be seen as different objects. If you wanted them to be considered
       equivalent, you would have to pass in a key generation function:

           my $array1 = [ $person1, $person2, $person4 ];
           my $array2 = [ $person1, $person3, $person4-&gt;clone(), $person5 ];
           Algorithm::Diff::diff( $array1, $array2, \&amp;Person::hash );

       This would use the &apos;ssn&apos; field in each Person as a comparison key, and so
       would consider $person4 and $person4-&gt;clone() as equal.

       You may also pass additional parameters to the key generation function if
       you wish.

ERROR CHECKING
       If you pass these routines a non-reference and they expect a reference,
       they will die with a message.

AUTHOR
       This version released by Tye McQueen (http://perlmonks.org/?node=tye).

LICENSE
       Parts Copyright (c) 2000-2004 Ned Konz.  All rights reserved.  Parts by
       Tye McQueen.

       This program is free software; you can redistribute it and/or modify it
       under the same terms as Perl.

MAILING LIST
       Mark-Jason still maintains a mailing list.  To join a low-volume mailing
       list for announcements related to diff and Algorithm::Diff, send an empty
       mail message to mjd-perl-diff-request@plover.com.

CREDITS
       Versions through 0.59 (and much of this documentation) were written by:

       Mark-Jason Dominus, mjd-perl-diff@plover.com

       This version borrows some documentation and routine names from Mark-
       Jason&apos;s, but Diff.pm&apos;s code was completely replaced.

       This code was adapted from the Smalltalk code of Mario Wolczko
       &lt;mario@wolczko.com&gt;, which is available at
       ftp://st.cs.uiuc.edu/pub/Smalltalk/MANCHESTER/manchester/4.0/diff.st

       &quot;sdiff&quot; and &quot;traverse_balanced&quot; were written by Mike Schilli
       &lt;m@perlmeister.com&gt;.

       The algorithm is that described in A Fast Algorithm for Computing Longest
       Common Subsequences, CACM, vol.20, no.5, pp.350-353, May 1977, with a few
       minor improvements to improve the speed.

       Much work was done by Ned Konz (perl@bike-nomad.com).

       The OO interface and some other changes are by Tye McQueen.



perl v5.10.1                       2010-07-29               Algorithm::Diff(3pm)

</pre></body></html>
