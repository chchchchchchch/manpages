<html>
<head><meta charset=utf-8/>
<title>Class::Singleton - Implementation of a "Singleton" class</title></head>
<body><pre>

Class::Singleton(3pm)  User Contributed Perl Documentation Class::Singleton(3pm)



NAME
       Class::Singleton - Implementation of a &quot;Singleton&quot; class

SYNOPSIS
           use Class::Singleton;

           my $one = Class::Singleton-&gt;instance();   # returns a new instance
           my $two = Class::Singleton-&gt;instance();   # returns same instance

DESCRIPTION
       This is the &quot;Class::Singleton&quot; module.  A Singleton describes an object
       class that can have only one instance in any system.  An example of a
       Singleton might be a print spooler or system registry.  This module
       implements a Singleton class from which other classes can be derived.  By
       itself, the &quot;Class::Singleton&quot; module does very little other than manage
       the instantiation of a single object.  In deriving a class from
       &quot;Class::Singleton&quot;, your module will inherit the Singleton instantiation
       method and can implement whatever specific functionality is required.

       For a description and discussion of the Singleton class, see &quot;Design
       Patterns&quot;, Gamma et al, Addison-Wesley, 1995, ISBN 0-201-63361-2.

PREREQUISITES
       &quot;Class::Singleton&quot; requires Perl version 5.004 or later. If you have an
       older version of Perl, please upgrade to latest version, available from
       your nearest CPAN site (see INSTALLATION below).

INSTALLATION
       The &quot;Class::Singleton&quot; module is available from CPAN. As the &apos;perlmod&apos;
       man page explains:

           CPAN stands for the Comprehensive Perl Archive Network.
           This is a globally replicated collection of all known Perl
           materials, including hundreds of unbunded modules.

           [...]

           For an up-to-date listing of CPAN sites, see
           http://www.perl.com/perl/ or ftp://ftp.perl.com/perl/ .

       The module is available in the following directories:

           /modules/by-module/Class/Class-Singleton-&lt;version&gt;.tar.gz
           /authors/id/ABW/Class-Singleton-&lt;version&gt;.tar.gz

       &quot;Class::Singleton&quot; is distributed as a single gzipped tar archive file:

           Class-Singleton-&lt;version&gt;.tar.gz

       Note that &quot;&lt;version&gt;&quot; represents the current version number, of the form
       &quot;1.23&quot;.  See VERSION below to determine the current version number for
       &quot;Class::Singleton&quot;.

       Unpack the archive to create an installation directory:

           gunzip Class-Singleton-&lt;version&gt;.tar.gz
           tar xvf Class-Singleton-&lt;version&gt;.tar

       &apos;cd&apos; into that directory, make, test and install the module:

           cd Class-Singleton-&lt;version&gt;
           perl Makefile.PL
           make
           make test
           make install
&#12;       The &apos;&quot;make install&quot;&apos; will install the module on your system.  You may
       need root access to perform this task.  If you install the module in a
       local directory (for example, by executing &quot;&quot;perl Makefile.PL LIB=~/lib&quot;&quot;
       in the above - see &quot;perldoc MakeMaker&quot; for full details), you will need
       to ensure that the &quot;PERL5LIB&quot; environment variable is set to include the
       location, or add a line to your scripts explicitly naming the library
       location:

           use lib &apos;/local/path/to/lib&apos;;

USING THE CLASS::SINGLETON MODULE
       To import and use the &quot;Class::Singleton&quot; module the following line should
       appear in your Perl program:

           use Class::Singleton;

       The instance() method is used to create a new &quot;Class::Singleton&quot;
       instance, or return a reference to an existing instance. Using this
       method, it is only possible to have a single instance of the class in any
       system.

           my $highlander = Class::Singleton-&gt;instance();

       Assuming that no &quot;Class::Singleton&quot; object currently exists, this first
       call to instance() will create a new &quot;Class::Singleton&quot; and return a
       reference to it. Future invocations of instance() will return the same
       reference.

           my $macleod    = Class::Singleton-&gt;instance();

       In the above example, both $highlander and $macleod contain the same
       reference to a &quot;Class::Singleton&quot; instance.  There can be only one.

DERIVING SINGLETON CLASSES
       A module class may be derived from &quot;Class::Singleton&quot; and will inherit
       the instance() method that correctly instantiates only one object.

           package PrintSpooler;
           use base &apos;Class::Singleton&apos;;

           # derived class specific code
           sub submit_job {
               ...
           }

           sub cancel_job {
               ...
           }

       The &quot;PrintSpooler&quot; class defined above could be used as follows:

           use PrintSpooler;

           my $spooler = PrintSpooler-&gt;instance();

           $spooler-&gt;submit_job(...);

       The instance() method calls the _new_instance() constructor method the
       first and only time a new instance is created. All parameters passed to
       the instance() method are forwarded to _new_instance(). In the base class
       the _new_instance() method returns a blessed reference to a hash array
       containing any arguments passed as either a hash reference or list of
       named parameters.

           package MyConfig;
           use base &apos;Class::Singleton&apos;;

           sub foo {
               shift-&gt;{ foo };
           }

           sub bar {
               shift-&gt;{ bar };
           }

           package main;

           # either: hash reference of named parameters
           my $config = MyConfig-&gt;instance({ foo =&gt; 10, bar =&gt; 20 });

           # or: list of named parameters
           my $config = MyConfig-&gt;instance( foo =&gt; 10, bar =&gt; 20 );

           print $config-&gt;foo();   # 10
           print $config-&gt;bar();   # 20

       Derived classes may redefine the _new_instance() method to provide more
       specific object initialisation or change the underlying object type (to a
       list reference, for example).

           package MyApp::Database;
           use base &apos;Class::Singleton&apos;;
           use DBI;

           # this only gets called the first time instance() is called
           sub _new_instance {
               my $class = shift;
               my $self  = bless { }, $class;
               my $db    = shift || &quot;myappdb&quot;;
               my $host  = shift || &quot;localhost&quot;;

               $self-&gt;{ DB } = DBI-&gt;connect(&quot;DBI:mSQL:$db:$host&quot;)
                   || die &quot;Cannot connect to database: $DBI::errstr&quot;;

               # any other initialisation...

               return $self;
           }

       The above example might be used as follows:

           use MyApp::Database;

           # first use - database gets initialised
           my $database = MyApp::Database-&gt;instance();

       Some time later on in a module far, far away...

           package MyApp::FooBar
           use MyApp::Database;

           # this FooBar object needs access to the database; the Singleton
           # approach gives a nice wrapper around global variables.

           sub new {
               my $class = shift;
               bless {
                   database =&gt; MyApp::Database-&gt;instance(),
               }, $class;
           }

       The &quot;Class::Singleton&quot; instance() method uses a package variable to store
       a reference to any existing instance of the object. This variable,
       &quot;&quot;_instance&quot;&quot;, is coerced into the derived class package rather than the
       base class package.

       Thus, in the &quot;MyApp::Database&quot; example above, the instance variable would
       be:

           $MyApp::Database::_instance;
&#12;       This allows different classes to be derived from &quot;Class::Singleton&quot; that
       can co-exist in the same system, while still allowing only one instance
       of any one class to exists. For example, it would be possible to derive
       both &apos;&quot;PrintSpooler&quot;&apos; and &apos;&quot;MyApp::Database&quot;&apos; from &quot;Class::Singleton&quot; and
       have a single instance of each in a system, rather than a single instance
       of either.

       You can use the has_instance() method to find out if a particular class
       already has an instance defined.  A reference to the instance is returned
       or &quot;undef&quot; if none is currently defined.

           my $instance = MyApp::Database-&gt;has_instance()
               || warn &quot;No instance is defined yet&quot;;

METHODS
   instance()
       This method is called to return a current object instance or create a new
       one by calling _new_instance().

   has_instance()
       This method returns a reference to any existing instance or &quot;undef&quot; if
       none is defined.

           my $testing = MySingleton1-&gt;has_instance()
               || warn &quot;No instance defined for MySingleton1&quot;;

   _new_instance()
       This &quot;private&quot; method is called by instance() to create a new object
       instance if one doesn&apos;t already exist. It is not intended to be called
       directly (although there&apos;s nothing to stop you from calling it if you&apos;re
       really determined to do so).

       It creates a blessed hash reference containing any arguments passed to
       the method as either a hash reference or list of named parameters.

           # either: hash reference of named parameters
           my $example1 = MySingleton1-&gt;new({ pi =&gt; 3.14, e =&gt; 2.718 });

           # or: list of named parameters
           my $example2 = MySingleton2-&gt;new( pi =&gt; 3.14, e =&gt; 2.718 );

       It is important to remember that the instance() method will only call the
       _new_instance() method once, so any arguments you pass may be silently
       ignored if an instance already exists. You can use the has_instance()
       method to determine if an instance is already defined.

AUTHOR
       Andy Wardley &lt;abw@wardley.org&gt; &lt;http://wardley.org/&gt;

       Thanks to Andreas Koenig for providing some significant speedup patches
       and other ideas.

VERSION
       This is version 1.4, released September 2007

COPYRIGHT
       Copyright Andy Wardley 1998-2007.  All Rights Reserved.

       This module is free software; you can redistribute it and/or modify it
       under the same terms as Perl itself.



perl v5.10.0                       2007-09-28              Class::Singleton(3pm)

</pre></body></html>
