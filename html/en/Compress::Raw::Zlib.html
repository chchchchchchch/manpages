<html>
<head><meta charset=utf-8/>
<title>Compress::Raw::Zlib - Low-Level Interface to zlib compression library</title></head>
<body><pre>

Compress::Raw::Zlib(3pmUser Contributed Perl DocumentatiCompress::Raw::Zlib(3pm)



NAME
       Compress::Raw::Zlib - Low-Level Interface to zlib compression library

SYNOPSIS
           use Compress::Raw::Zlib ;

           ($d, $status) = new Compress::Raw::Zlib::Deflate( [OPT] ) ;
           $status = $d-&gt;deflate($input, $output) ;
           $status = $d-&gt;flush($output [, $flush_type]) ;
           $d-&gt;deflateReset() ;
           $d-&gt;deflateParams(OPTS) ;
           $d-&gt;deflateTune(OPTS) ;
           $d-&gt;dict_adler() ;
           $d-&gt;crc32() ;
           $d-&gt;adler32() ;
           $d-&gt;total_in() ;
           $d-&gt;total_out() ;
           $d-&gt;msg() ;
           $d-&gt;get_Strategy();
           $d-&gt;get_Level();
           $d-&gt;get_BufSize();

           ($i, $status) = new Compress::Raw::Zlib::Inflate( [OPT] ) ;
           $status = $i-&gt;inflate($input, $output [, $eof]) ;
           $status = $i-&gt;inflateSync($input) ;
           $i-&gt;inflateReset() ;
           $i-&gt;dict_adler() ;
           $d-&gt;crc32() ;
           $d-&gt;adler32() ;
           $i-&gt;total_in() ;
           $i-&gt;total_out() ;
           $i-&gt;msg() ;
           $d-&gt;get_BufSize();

           $crc = adler32($buffer [,$crc]) ;
           $crc = crc32($buffer [,$crc]) ;

           $crc = adler32_combine($crc1, $crc2, $len2)l
           $crc = crc32_combine($adler1, $adler2, $len2)

           my $version = Compress::Raw::Zlib::zlib_version();
           my $flags = Compress::Raw::Zlib::zlibCompileFlags();

DESCRIPTION
       The Compress::Raw::Zlib module provides a Perl interface to the zlib
       compression library (see &quot;AUTHOR&quot; for details about where to get zlib).

Compress::Raw::Zlib::Deflate
       This section defines an interface that allows in-memory compression using
       the deflate interface provided by zlib.

       Here is a definition of the interface available:

   ($d, $status) = new Compress::Raw::Zlib::Deflate( [OPT] )
       Initialises a deflation object.

       If you are familiar with the zlib library, it combines the features of
       the zlib functions &quot;deflateInit&quot;, &quot;deflateInit2&quot; and
       &quot;deflateSetDictionary&quot;.

       If successful, it will return the initialised deflation object, $d and a
       $status of &quot;Z_OK&quot; in a list context. In scalar context it returns the
       deflation object, $d, only.

       If not successful, the returned deflation object, $d, will be undef and
       $status will hold the a zlib error code.

       The function optionally takes a number of named options specified as
       &quot;Name =&gt; value&quot; pairs. This allows individual options to be tailored
       without having to specify them all in the parameter list.

       For backward compatibility, it is also possible to pass the parameters as
       a reference to a hash containing the name=&gt;value pairs.

       Below is a list of the valid options:

       -Level
            Defines the compression level. Valid values are 0 through 9,
            &quot;Z_NO_COMPRESSION&quot;, &quot;Z_BEST_SPEED&quot;, &quot;Z_BEST_COMPRESSION&quot;, and
            &quot;Z_DEFAULT_COMPRESSION&quot;.

            The default is &quot;Z_DEFAULT_COMPRESSION&quot;.

       -Method
            Defines the compression method. The only valid value at present (and
            the default) is &quot;Z_DEFLATED&quot;.

       -WindowBits
            To compress an RFC 1950 data stream, set &quot;WindowBits&quot; to a positive
            number between 8 and 15.

            To compress an RFC 1951 data stream, set &quot;WindowBits&quot; to
            &quot;-MAX_WBITS&quot;.

            To compress an RFC 1952 data stream (i.e. gzip), set &quot;WindowBits&quot; to
            &quot;WANT_GZIP&quot;.

            For a definition of the meaning and valid values for &quot;WindowBits&quot;
            refer to the zlib documentation for deflateInit2.

            Defaults to &quot;MAX_WBITS&quot;.

       -MemLevel
            For a definition of the meaning and valid values for &quot;MemLevel&quot;
            refer to the zlib documentation for deflateInit2.

            Defaults to MAX_MEM_LEVEL.

       -Strategy
            Defines the strategy used to tune the compression. The valid values
            are &quot;Z_DEFAULT_STRATEGY&quot;, &quot;Z_FILTERED&quot;, &quot;Z_RLE&quot;, &quot;Z_FIXED&quot; and
            &quot;Z_HUFFMAN_ONLY&quot;.

            The default is &quot;Z_DEFAULT_STRATEGY&quot;.

       -Dictionary
            When a dictionary is specified Compress::Raw::Zlib will
            automatically call &quot;deflateSetDictionary&quot; directly after calling
            &quot;deflateInit&quot;. The Adler32 value for the dictionary can be obtained
            by calling the method &quot;$d-&gt;dict_adler()&quot;.

            The default is no dictionary.

       -Bufsize
            Sets the initial size for the output buffer used by the
            &quot;$d-&gt;deflate&quot; and &quot;$d-&gt;flush&quot; methods. If the buffer has to be
            reallocated to increase the size, it will grow in increments of
            &quot;Bufsize&quot;.

            The default buffer size is 4096.

       -AppendOutput
            This option controls how data is written to the output buffer by the
            &quot;$d-&gt;deflate&quot; and &quot;$d-&gt;flush&quot; methods.

            If the &quot;AppendOutput&quot; option is set to false, the output buffers in
            the &quot;$d-&gt;deflate&quot; and &quot;$d-&gt;flush&quot;  methods will be truncated before
            uncompressed data is written to them.

            If the option is set to true, uncompressed data will be appended to
            the output buffer in the &quot;$d-&gt;deflate&quot; and &quot;$d-&gt;flush&quot; methods.

            This option defaults to false.

       -CRC32
            If set to true, a crc32 checksum of the uncompressed data will be
            calculated. Use the &quot;$d-&gt;crc32&quot; method to retrieve this value.

            This option defaults to false.

       -ADLER32
            If set to true, an adler32 checksum of the uncompressed data will be
            calculated. Use the &quot;$d-&gt;adler32&quot; method to retrieve this value.

            This option defaults to false.

       Here is an example of using the &quot;Compress::Raw::Zlib::Deflate&quot; optional
       parameter list to override the default buffer size and compression level.
       All other options will take their default values.

           my $d = new Compress::Raw::Zlib::Deflate ( -Bufsize =&gt; 300,
                                                      -Level   =&gt; Z_BEST_SPEED ) ;

   $status = $d-&gt;deflate($input, $output)
       Deflates the contents of $input and writes the compressed data to
       $output.

       The $input and $output parameters can be either scalars or scalar
       references.

       When finished, $input will be completely processed (assuming there were
       no errors). If the deflation was successful it writes the deflated data
       to $output and returns a status value of &quot;Z_OK&quot;.

       On error, it returns a zlib error code.

       If the &quot;AppendOutput&quot; option is set to true in the constructor for the $d
       object, the compressed data will be appended to $output. If it is false,
       $output will be truncated before any compressed data is written to it.

       Note: This method will not necessarily write compressed data to $output
       every time it is called. So do not assume that there has been an error if
       the contents of $output is empty on returning from this method. As long
       as the return code from the method is &quot;Z_OK&quot;, the deflate has succeeded.

   $status = $d-&gt;flush($output [, $flush_type])
       Typically used to finish the deflation. Any pending output will be
       written to $output.

       Returns &quot;Z_OK&quot; if successful.

       Note that flushing can seriously degrade the compression ratio, so it
       should only be used to terminate a decompression (using &quot;Z_FINISH&quot;) or
       when you want to create a full flush point (using &quot;Z_FULL_FLUSH&quot;).

       By default the &quot;flush_type&quot; used is &quot;Z_FINISH&quot;. Other valid values for
       &quot;flush_type&quot; are &quot;Z_NO_FLUSH&quot;, &quot;Z_PARTIAL_FLUSH&quot;, &quot;Z_SYNC_FLUSH&quot; and
       &quot;Z_FULL_FLUSH&quot;. It is strongly recommended that you only set the
       &quot;flush_type&quot; parameter if you fully understand the implications of what
       it does. See the &quot;zlib&quot; documentation for details.

       If the &quot;AppendOutput&quot; option is set to true in the constructor for the $d
       object, the compressed data will be appended to $output. If it is false,
       $output will be truncated before any compressed data is written to it.

   $status = $d-&gt;deflateReset()
       This method will reset the deflation object $d. It can be used when you
       are compressing multiple data streams and want to use the same object to
       compress each of them. It should only be used once the previous data
       stream has been flushed successfully, i.e. a call to
       &quot;$d-&gt;flush(Z_FINISH)&quot; has returned &quot;Z_OK&quot;.

       Returns &quot;Z_OK&quot; if successful.

   $status = $d-&gt;deflateParams([OPT])
       Change settings for the deflate object $d.

       The list of the valid options is shown below. Options not specified will
       remain unchanged.

       -Level
            Defines the compression level. Valid values are 0 through 9,
            &quot;Z_NO_COMPRESSION&quot;, &quot;Z_BEST_SPEED&quot;, &quot;Z_BEST_COMPRESSION&quot;, and
            &quot;Z_DEFAULT_COMPRESSION&quot;.

       -Strategy
            Defines the strategy used to tune the compression. The valid values
            are &quot;Z_DEFAULT_STRATEGY&quot;, &quot;Z_FILTERED&quot; and &quot;Z_HUFFMAN_ONLY&quot;.

       -BufSize
            Sets the initial size for the output buffer used by the
            &quot;$d-&gt;deflate&quot; and &quot;$d-&gt;flush&quot; methods. If the buffer has to be
            reallocated to increase the size, it will grow in increments of
            &quot;Bufsize&quot;.

   $status = $d-&gt;deflateTune($good_length, $max_lazy, $nice_length, $max_chain)
       Tune the internal settings for the deflate object $d. This option is only
       available if you are running zlib 1.2.2.3 or better.

       Refer to the documentation in zlib.h for instructions on how to fly
       &quot;deflateTune&quot;.

   $d-&gt;dict_adler()
       Returns the adler32 value for the dictionary.

   $d-&gt;crc32()
       Returns the crc32 value for the uncompressed data to date.

       If the &quot;CRC32&quot; option is not enabled in the constructor for this object,
       this method will always return 0;

   $d-&gt;adler32()
       Returns the adler32 value for the uncompressed data to date.

   $d-&gt;msg()
       Returns the last error message generated by zlib.

   $d-&gt;total_in()
       Returns the total number of bytes uncompressed bytes input to deflate.
&#12;   $d-&gt;total_out()
       Returns the total number of compressed bytes output from deflate.

   $d-&gt;get_Strategy()
       Returns the deflation strategy currently used. Valid values are
       &quot;Z_DEFAULT_STRATEGY&quot;, &quot;Z_FILTERED&quot; and &quot;Z_HUFFMAN_ONLY&quot;.

   $d-&gt;get_Level()
       Returns the compression level being used.

   $d-&gt;get_BufSize()
       Returns the buffer size used to carry out the compression.

   Example
       Here is a trivial example of using &quot;deflate&quot;. It simply reads standard
       input, deflates it and writes it to standard output.

           use strict ;
           use warnings ;

           use Compress::Raw::Zlib ;

           binmode STDIN;
           binmode STDOUT;
           my $x = new Compress::Raw::Zlib::Deflate
              or die &quot;Cannot create a deflation stream\n&quot; ;

           my ($output, $status) ;
           while (&lt;&gt;)
           {
               $status = $x-&gt;deflate($_, $output) ;

               $status == Z_OK
                   or die &quot;deflation failed\n&quot; ;

               print $output ;
           }

           $status = $x-&gt;flush($output) ;

           $status == Z_OK
               or die &quot;deflation failed\n&quot; ;

           print $output ;

Compress::Raw::Zlib::Inflate
       This section defines an interface that allows in-memory uncompression
       using the inflate interface provided by zlib.

       Here is a definition of the interface:

    ($i, $status) = new Compress::Raw::Zlib::Inflate( [OPT] )
       Initialises an inflation object.

       In a list context it returns the inflation object, $i, and the zlib
       status code ($status). In a scalar context it returns the inflation
       object only.

       If successful, $i will hold the inflation object and $status will be
       &quot;Z_OK&quot;.

       If not successful, $i will be undef and $status will hold the zlib error
       code.

       The function optionally takes a number of named options specified as
       &quot;-Name =&gt; value&quot; pairs. This allows individual options to be tailored
       without having to specify them all in the parameter list.

       For backward compatibility, it is also possible to pass the parameters as
       a reference to a hash containing the &quot;name=&gt;value&quot; pairs.

       Here is a list of the valid options:

       -WindowBits
            To uncompress an RFC 1950 data stream, set &quot;WindowBits&quot; to a
            positive number between 8 and 15.

            To uncompress an RFC 1951 data stream, set &quot;WindowBits&quot; to
            &quot;-MAX_WBITS&quot;.

            To uncompress an RFC 1952 data stream (i.e. gzip), set &quot;WindowBits&quot;
            to &quot;WANT_GZIP&quot;.

            To auto-detect and uncompress an RFC 1950 or RFC 1952 data stream
            (i.e.  gzip), set &quot;WindowBits&quot; to &quot;WANT_GZIP_OR_ZLIB&quot;.

            For a full definition of the meaning and valid values for
            &quot;WindowBits&quot; refer to the zlib documentation for inflateInit2.

            Defaults to &quot;MAX_WBITS&quot;.

       -Bufsize
            Sets the initial size for the output buffer used by the
            &quot;$i-&gt;inflate&quot; method. If the output buffer in this method has to be
            reallocated to increase the size, it will grow in increments of
            &quot;Bufsize&quot;.

            Default is 4096.

       -Dictionary
            The default is no dictionary.

       -AppendOutput
            This option controls how data is written to the output buffer by the
            &quot;$i-&gt;inflate&quot; method.

            If the option is set to false, the output buffer in the
            &quot;$i-&gt;inflate&quot; method will be truncated before uncompressed data is
            written to it.

            If the option is set to true, uncompressed data will be appended to
            the output buffer by the &quot;$i-&gt;inflate&quot; method.

            This option defaults to false.

       -CRC32
            If set to true, a crc32 checksum of the uncompressed data will be
            calculated. Use the &quot;$i-&gt;crc32&quot; method to retrieve this value.

            This option defaults to false.

       -ADLER32
            If set to true, an adler32 checksum of the uncompressed data will be
            calculated. Use the &quot;$i-&gt;adler32&quot; method to retrieve this value.

            This option defaults to false.

       -ConsumeInput
            If set to true, this option will remove compressed data from the
            input buffer of the &quot;$i-&gt;inflate&quot; method as the inflate progresses.

            This option can be useful when you are processing compressed data
            that is embedded in another file/buffer. In this case the data that
            immediately follows the compressed stream will be left in the input
            buffer.

            This option defaults to true.

       -LimitOutput
            The &quot;LimitOutput&quot; option changes the behavior of the &quot;$i-&gt;inflate&quot;
            method so that the amount of memory used by the output buffer can be
            limited.

            When &quot;LimitOutput&quot; is used the size of the output buffer used will
            either be the value of the &quot;Bufsize&quot; option or the amount of memory
            already allocated to $output, whichever is larger. Predicting the
            output size available is tricky, so don&apos;t rely on getting an exact
            output buffer size.

            When &quot;LimitOutout&quot; is not specified &quot;$i-&gt;inflate&quot; will use as much
            memory as it takes to write all the uncompressed data it creates by
            uncompressing the input buffer.

            If &quot;LimitOutput&quot; is enabled, the &quot;ConsumeInput&quot; option will also be
            enabled.

            This option defaults to false.

            See &quot;The LimitOutput option&quot; for a discussion on why &quot;LimitOutput&quot;
            is needed and how to use it.

       Here is an example of using an optional parameter to override the default
       buffer size.

           my ($i, $status) = new Compress::Raw::Zlib::Inflate( -Bufsize =&gt; 300 ) ;

    $status = $i-&gt;inflate($input, $output [,$eof])
       Inflates the complete contents of $input and writes the uncompressed data
       to $output. The $input and $output parameters can either be scalars or
       scalar references.

       Returns &quot;Z_OK&quot; if successful and &quot;Z_STREAM_END&quot; if the end of the
       compressed data has been successfully reached.

       If not successful $status will hold the zlib error code.

       If the &quot;ConsumeInput&quot; option has been set to true when the
       &quot;Compress::Raw::Zlib::Inflate&quot; object is created, the $input parameter is
       modified by &quot;inflate&quot;. On completion it will contain what remains of the
       input buffer after inflation. In practice, this means that when the
       return status is &quot;Z_OK&quot; the $input parameter will contain an empty
       string, and when the return status is &quot;Z_STREAM_END&quot; the $input parameter
       will contains what (if anything) was stored in the input buffer after the
       deflated data stream.

       This feature is useful when processing a file format that encapsulates a
       compressed data stream (e.g. gzip, zip) and there is useful data
       immediately after the deflation stream.

       If the &quot;AppendOutput&quot; option is set to true in the constructor for this
       object, the uncompressed data will be appended to $output. If it is
       false, $output will be truncated before any uncompressed data is written
       to it.

       The $eof parameter needs a bit of explanation.

       Prior to version 1.2.0, zlib assumed that there was at least one trailing
       byte immediately after the compressed data stream when it was carrying
       out decompression. This normally isn&apos;t a problem because the majority of
       zlib applications guarantee that there will be data directly after the
       compressed data stream.  For example, both gzip (RFC 1950) and zip both
       define trailing data that follows the compressed data stream.

       The $eof parameter only needs to be used if all of the following
       conditions apply

       1.   You are either using a copy of zlib that is older than version 1.2.0
            or you want your application code to be able to run with as many
            different versions of zlib as possible.

       2.   You have set the &quot;WindowBits&quot; parameter to &quot;-MAX_WBITS&quot; in the
            constructor for this object, i.e. you are uncompressing a raw
            deflated data stream (RFC 1951).

       3.   There is no data immediately after the compressed data stream.

       If all of these are the case, then you need to set the $eof parameter to
       true on the final call (and only the final call) to &quot;$i-&gt;inflate&quot;.

       If you have built this module with zlib &gt;= 1.2.0, the $eof parameter is
       ignored. You can still set it if you want, but it won&apos;t be used behind
       the scenes.

   $status = $i-&gt;inflateSync($input)
       This method can be used to attempt to recover good data from a compressed
       data stream that is partially corrupt.  It scans $input until it reaches
       either a full flush point or the end of the buffer.

       If a full flush point is found, &quot;Z_OK&quot; is returned and $input will be
       have all data up to the flush point removed. This data can then be passed
       to the &quot;$i-&gt;inflate&quot; method to be uncompressed.

       Any other return code means that a flush point was not found. If more
       data is available, &quot;inflateSync&quot; can be called repeatedly with more
       compressed data until the flush point is found.

       Note full flush points are not present by default in compressed data
       streams. They must have been added explicitly when the data stream was
       created by calling &quot;Compress::Deflate::flush&quot;  with &quot;Z_FULL_FLUSH&quot;.

   $status = $i-&gt;inflateReset()
       This method will reset the inflation object $i. It can be used when you
       are uncompressing multiple data streams and want to use the same object
       to uncompress each of them.

       Returns &quot;Z_OK&quot; if successful.

   $i-&gt;dict_adler()
       Returns the adler32 value for the dictionary.

   $i-&gt;crc32()
       Returns the crc32 value for the uncompressed data to date.

       If the &quot;CRC32&quot; option is not enabled in the constructor for this object,
       this method will always return 0;

   $i-&gt;adler32()
       Returns the adler32 value for the uncompressed data to date.

       If the &quot;ADLER32&quot; option is not enabled in the constructor for this
       object, this method will always return 0;

   $i-&gt;msg()
       Returns the last error message generated by zlib.
&#12;   $i-&gt;total_in()
       Returns the total number of bytes compressed bytes input to inflate.

   $i-&gt;total_out()
       Returns the total number of uncompressed bytes output from inflate.

   $d-&gt;get_BufSize()
       Returns the buffer size used to carry out the decompression.

   Examples
       Here is an example of using &quot;inflate&quot;.

           use strict ;
           use warnings ;

           use Compress::Raw::Zlib;

           my $x = new Compress::Raw::Zlib::Inflate()
              or die &quot;Cannot create a inflation stream\n&quot; ;

           my $input = &apos;&apos; ;
           binmode STDIN;
           binmode STDOUT;

           my ($output, $status) ;
           while (read(STDIN, $input, 4096))
           {
               $status = $x-&gt;inflate($input, $output) ;

               print $output ;

               last if $status != Z_OK ;
           }

           die &quot;inflation failed\n&quot;
               unless $status == Z_STREAM_END ;

       The next example show how to use the &quot;LimitOutput&quot; option. Notice the use
       of two nested loops in this case. The outer loop reads the data from the
       input source - STDIN and the inner loop repeatedly calls &quot;inflate&quot; until
       $input is exhausted, we get an error, or the end of the stream is
       reached. One point worth remembering is by using the &quot;LimitOutput&quot; option
       you also get &quot;ConsumeInput&quot; set as well - this makes the code below much
       simpler.

           use strict ;
           use warnings ;

           use Compress::Raw::Zlib;

           my $x = new Compress::Raw::Zlib::Inflate(LimitOutput =&gt; 1)
              or die &quot;Cannot create a inflation stream\n&quot; ;

           my $input = &apos;&apos; ;
           binmode STDIN;
           binmode STDOUT;

           my ($output, $status) ;

         OUTER:
           while (read(STDIN, $input, 4096))
           {
               do
               {
                   $status = $x-&gt;inflate($input, $output) ;

                   print $output ;

                   last OUTER
                       unless $status == Z_OK || $status == Z_BUF_ERROR ;
               }
               while ($status == Z_OK &amp;&amp; length $input);
           }

           die &quot;inflation failed\n&quot;
               unless $status == Z_STREAM_END ;

CHECKSUM FUNCTIONS
       Two functions are provided by zlib to calculate checksums. For the Perl
       interface, the order of the two parameters in both functions has been
       reversed. This allows both running checksums and one off calculations to
       be done.

           $crc = adler32($buffer [,$crc]) ;
           $crc = crc32($buffer [,$crc]) ;

       The buffer parameters can either be a scalar or a scalar reference.

       If the $crc parameters is &quot;undef&quot;, the crc value will be reset.

       If you have built this module with zlib 1.2.3 or better, two more CRC-
       related functions are available.

           $crc = adler32_combine($crc1, $crc2, $len2)l
           $crc = crc32_combine($adler1, $adler2, $len2)

       These functions allow checksums to be merged.

Misc
   my $version = Compress::Raw::Zlib::zlib_version();
       Returns the version of the zlib library.

   my $flags = Compress::Raw::Zlib::zlibCompileFlags();
       Returns the flags indicating compile-time options that were used to build
       the zlib library. See the zlib documentation for a description of the
       flags returned by &quot;zlibCompileFlags&quot;.

       Note that when the zlib sources are built along with this module the
       &quot;sprintf&quot; flags (bits 24, 25 and 26) should be ignored.

       If you are using zlib 1.2.0 or older, &quot;zlibCompileFlags&quot; will return 0.

The LimitOutput option.
       By default &quot;$i-&gt;inflate($input, $output)&quot; will uncompress all data in
       $input and write all of the uncompressed data it has generated to
       $output. This makes the interface to &quot;inflate&quot; much simpler - if the
       method has uncompressed $input successfully all compressed data in $input
       will have been dealt with. So if you are reading from an input source and
       uncompressing as you go the code will look something like this

           use strict ;
           use warnings ;

           use Compress::Raw::Zlib;

           my $x = new Compress::Raw::Zlib::Inflate()
              or die &quot;Cannot create a inflation stream\n&quot; ;

           my $input = &apos;&apos; ;

           my ($output, $status) ;
           while (read(STDIN, $input, 4096))
           {
               $status = $x-&gt;inflate($input, $output) ;

               print $output ;

               last if $status != Z_OK ;
           }

           die &quot;inflation failed\n&quot;
               unless $status == Z_STREAM_END ;

       The points to note are

       &#183;    The main processing loop in the code handles reading of compressed
            data from STDIN.

       &#183;    The status code returned from &quot;inflate&quot; will only trigger
            termination of the main processing loop if it isn&apos;t &quot;Z_OK&quot;. When
            &quot;LimitOutput&quot; has not been used the &quot;Z_OK&quot; status means means that
            the end of the compressed data stream has been reached or there has
            been an error in uncompression.

       &#183;    After the call to &quot;inflate&quot; all of the uncompressed data in $input
            will have been processed. This means the subsequent call to &quot;read&quot;
            can overwrite it&apos;s contents without any problem.

       For most use-cases the behavior described above is acceptable (this
       module and it&apos;s predecessor, &quot;Compress::Zlib&quot;, have used it for over 10
       years without an issue), but in a few very specific use-cases the amount
       of memory required for $output can prohibitively large. For example, if
       the compressed data stream contains the same pattern repeated thousands
       of times, a relatively small compressed data stream can uncompress into
       hundreds of megabytes.  Remember &quot;inflate&quot; will keep allocating memory
       until all the uncompressed data has been written to the output buffer -
       the size of $output is unbounded.

       The &quot;LimitOutput&quot; option is designed to help with this use-case.

       The main difference in your code when using &quot;LimitOutput&quot; is having to
       deal with cases where the $input parameter still contains some
       uncompressed data that &quot;inflate&quot; hasn&apos;t processed yet. The status code
       returned from &quot;inflate&quot; will be &quot;Z_OK&quot; if uncompression took place and
       &quot;Z_BUF_ERROR&quot; if the output buffer is full.

       Below is typical code that shows how to use &quot;LimitOutput&quot;.

           use strict ;
           use warnings ;

           use Compress::Raw::Zlib;

           my $x = new Compress::Raw::Zlib::Inflate(LimitOutput =&gt; 1)
              or die &quot;Cannot create a inflation stream\n&quot; ;

           my $input = &apos;&apos; ;
           binmode STDIN;
           binmode STDOUT;

           my ($output, $status) ;

         OUTER:
           while (read(STDIN, $input, 4096))
           {
               do
               {
                   $status = $x-&gt;inflate($input, $output) ;

                   print $output ;
&#12;                   last OUTER
                       unless $status == Z_OK || $status == Z_BUF_ERROR ;
               }
               while ($status == Z_OK &amp;&amp; length $input);
           }

           die &quot;inflation failed\n&quot;
               unless $status == Z_STREAM_END ;

       Points to note this time:

       &#183;    There are now two nested loops in the code: the outer loop for
            reading the compressed data from STDIN, as before; and the inner
            loop to carry out the uncompression.

       &#183;    There are two exit points from the inner uncompression loop.

            Firstly when &quot;inflate&quot; has returned a status other than &quot;Z_OK&quot; or
            &quot;Z_BUF_ERROR&quot;.  This means that either the end of the compressed
            data stream has been reached (&quot;Z_STREAM_END&quot;) or there is an error
            in the compressed data. In either of these cases there is no point
            in continuing with reading the compressed data, so both loops are
            terminated.

            The second exit point tests if there is any data left in the input
            buffer, $input - remember that the &quot;ConsumeInput&quot; option is
            automatically enabled when &quot;LimitOutput&quot; is used.  When the input
            buffer has been exhausted, the outer loop can run again and
            overwrite a now empty $input.

ACCESSING ZIP FILES
       Although it is possible (with some effort on your part) to use this
       module to access .zip files, there are other perl modules available that
       will do all the hard work for you. Check out &quot;Archive::Zip&quot;,
       &quot;IO::Compress::Zip&quot; and &quot;IO::Uncompress::Unzip&quot;.

FAQ
   Compatibility with Unix compress/uncompress.
       This module is not compatible with Unix &quot;compress&quot;.

       If you have the &quot;uncompress&quot; program available, you can use this to read
       compressed files

           open F, &quot;uncompress -c $filename |&quot;;
           while (&lt;F&gt;)
           {
               ...

       Alternatively, if you have the &quot;gunzip&quot; program available, you can use
       this to read compressed files

           open F, &quot;gunzip -c $filename |&quot;;
           while (&lt;F&gt;)
           {
               ...

       and this to write compress files, if you have the &quot;compress&quot; program
       available

           open F, &quot;| compress -c $filename &quot;;
           print F &quot;data&quot;;
           ...
           close F ;

   Accessing .tar.Z files
       See previous FAQ item.
&#12;       If the &quot;Archive::Tar&quot; module is installed and either the &quot;uncompress&quot; or
       &quot;gunzip&quot; programs are available, you can use one of these workarounds to
       read &quot;.tar.Z&quot; files.

       Firstly with &quot;uncompress&quot;

           use strict;
           use warnings;
           use Archive::Tar;

           open F, &quot;uncompress -c $filename |&quot;;
           my $tar = Archive::Tar-&gt;new(*F);
           ...

       and this with &quot;gunzip&quot;

           use strict;
           use warnings;
           use Archive::Tar;

           open F, &quot;gunzip -c $filename |&quot;;
           my $tar = Archive::Tar-&gt;new(*F);
           ...

       Similarly, if the &quot;compress&quot; program is available, you can use this to
       write a &quot;.tar.Z&quot; file

           use strict;
           use warnings;
           use Archive::Tar;
           use IO::File;

           my $fh = new IO::File &quot;| compress -c &gt;$filename&quot;;
           my $tar = Archive::Tar-&gt;new();
           ...
           $tar-&gt;write($fh);
           $fh-&gt;close ;

   Zlib Library Version Support
       By default &quot;Compress::Raw::Zlib&quot; will build with a private copy of
       version 1.2.5 of the zlib library. (See the README file for details of
       how to override this behaviour)

       If you decide to use a different version of the zlib library, you need to
       be aware of the following issues

       &#183;    First off, you must have zlib 1.0.5 or better.

       &#183;    You need to have zlib 1.2.1 or better if you want to use the
            &quot;-Merge&quot; option with &quot;IO::Compress::Gzip&quot;, &quot;IO::Compress::Deflate&quot;
            and &quot;IO::Compress::RawDeflate&quot;.

CONSTANTS
       All the zlib constants are automatically imported when you make use of
       Compress::Raw::Zlib.

SEE ALSO
       Compress::Zlib, IO::Compress::Gzip, IO::Uncompress::Gunzip,
       IO::Compress::Deflate, IO::Uncompress::Inflate, IO::Compress::RawDeflate,
       IO::Uncompress::RawInflate, IO::Compress::Bzip2, IO::Uncompress::Bunzip2,
       IO::Compress::Lzma, IO::Uncompress::UnLzma, IO::Compress::Xz,
       IO::Uncompress::UnXz, IO::Compress::Lzop, IO::Uncompress::UnLzop,
       IO::Compress::Lzf, IO::Uncompress::UnLzf, IO::Uncompress::AnyInflate,
       IO::Uncompress::AnyUncompress

       IO::Compress::FAQ
&#12;       File::GlobMapper, Archive::Zip, Archive::Tar, IO::Zlib

       For RFC 1950, 1951 and 1952 see http://www.faqs.org/rfcs/rfc1950.html,
       http://www.faqs.org/rfcs/rfc1951.html and
       http://www.faqs.org/rfcs/rfc1952.html

       The zlib compression library was written by Jean-loup Gailly
       gzip@prep.ai.mit.edu and Mark Adler madler@alumni.caltech.edu.

       The primary site for the zlib compression library is http://www.zlib.org.

       The primary site for gzip is http://www.gzip.org.

AUTHOR
       This module was written by Paul Marquess, pmqs@cpan.org.

MODIFICATION HISTORY
       See the Changes file.

COPYRIGHT AND LICENSE
       Copyright (c) 2005-2012 Paul Marquess. All rights reserved.

       This program is free software; you can redistribute it and/or modify it
       under the same terms as Perl itself.



perl v5.14.2                       2012-04-29           Compress::Raw::Zlib(3pm)

</pre></body></html>
