<html>
<head><meta charset=utf-8/>
<title>Date::Simple - a simple date object</title></head>
<body><pre>

Date::Simple(3pm)      User Contributed Perl Documentation     Date::Simple(3pm)



NAME
       Date::Simple - a simple date object

SYNOPSIS
           use Date::Simple (&apos;date&apos;, &apos;today&apos;);

           # Difference in days between two dates:
           $diff = date(&apos;2001-08-27&apos;) - date(&apos;1977-10-05&apos;);

           # Offset $n days from now:
           $date = today() + $n;
           print &quot;$date\n&quot;;  # uses ISO 8601 format (YYYY-MM-DD)

           use Date::Simple ();
           my $date  = Date::Simple-&gt;new(&apos;1972-01-17&apos;);
           my $year  = $date-&gt;year;
           my $month = $date-&gt;month;
           my $day   = $date-&gt;day;

           use Date::Simple (&apos;:all&apos;);
           my $date2 = ymd($year, $month, $day);
           my $date3 = d8(&apos;19871218&apos;);
           my $today = today();
           my $tomorrow = $today + 1;
           if ($tomorrow-&gt;year != $today-&gt;year) {
               print &quot;Today is New Year&apos;s Eve!\n&quot;;
           }

           if ($today &gt; $tomorrow) {
               die &quot;warp in space-time continuum&quot;;
           }

           print &quot;Today is &quot;;
           print((&apos;Sun&apos;,&apos;Mon&apos;,&apos;Tues&apos;,&apos;Wednes&apos;,&apos;Thurs&apos;,&apos;Fri&apos;,&apos;Satur&apos;)
                 [$today-&gt;day_of_week]);
           print &quot;day.\n&quot;;

           # you can also do this:
           ($date cmp &quot;2001-07-01&quot;)
           # and this
           ($date &lt;=&gt; [2001, 7, 1])

DESCRIPTION
       Dates are complex enough without times and timezones.  This module may be
       used to create simple date objects.  It handles:

       Validation.
           Reject 1999-02-29 but accept 2000-02-29.

       Interval arithmetic.
           How many days were between two given dates?  What date comes N days
           after today?

       Day-of-week calculation.
           What day of the week is a given date?

       Transparent date formatting.
           How should a date object be formatted.

       It does not deal with hours, minutes, seconds, and time zones.

       A date is uniquely identified by year, month, and day integers within
       valid ranges.  This module will not allow the creation of objects for
       invalid dates.  Attempting to create an invalid date will return undef.
       Month numbering starts at 1 for January, unlike in C and Java.  Years are
       4-digit.

       Gregorian dates up to year 9999 are handled correctly, but we rely on
       Perl&apos;s builtin &quot;localtime&quot; function when the current date is requested.
       On some platforms, &quot;localtime&quot; may be vulnerable to rollovers such as the
       Unix &quot;time_t&quot; wraparound of 18 January 2038.

       Overloading is used so you can compare or subtract two dates using
       standard numeric operators such as &quot;==&quot;, and the sum of a date object and
       an integer is another date object.

       Date::Simple objects are immutable.  After assigning $date1 to $date2, no
       change to $date1 can affect $date2.  This means, for example, that there
       is nothing like a &quot;set_year&quot; operation, and &quot;$date++&quot; assigns a new
       object to $date.

       This module contains various undocumented functions.  They may not be
       available on all platforms and are likely to change or disappear in
       future releases.  Please let the author know if you think any of them
       should be public.

   Controlling output format.
       As of version 3.0 new ways of controlling the output formats of
       Date::Simple objects has been provided. However Date::Simple has
       traditionally provided few ways of stringification, a primary one via the
       format() method and another primary one via direct stringification.
       However the later is currently implemented as an XS routine and the
       former is implemented through a perl routine.  This means that using
       format() is more expensive than stringification and that the
       stringification format is class specific.

       In order to alleviate some of these problems a new mechanism has been
       introduced to Date::Simple that allows for a per object level format
       default. In addition a set of utility classes that have different
       stringification overloads provided.  These classes are simple subclasses
       of Date::Simple and beside the default format() and the overloaded
       stringification behaviour are identical to Date::Simple. In fact one is
       totally identical to Date::Simple and is provided mostly for
       completeness.

       The classes included are:

       Date::Simple::ISO
           Identical to Date::Simple in every respect but name.

       Date::Simple::D8
           Uses the D8 format (%Y%m%d) as the default format for printing. Uses
           XS for the overloaded stringification.

       Date::Simple::Fmt
           Uses the perl implemented format() as the default stringification
           mechanism. The first argument to the constructor is expected to be
           the format to use for the object.

       NOTE its important to remember that the primary difference between the
       behaviour of objects of the different classes is how they are stringified
       when quoted, and what date format is used by default when the format()
       method is called. Nothing else differs.

CONSTRUCTORS
       Several functions take a string or numeric representation and generate a
       corresponding date object.  The most general is &quot;new&quot;, whose argument
       list may be empty (returning the current date), a string in format YYYY-
       MM-DD or YYYYMMDD, a list or arrayref of year, month, and day number, or
       an existing date object.

       Date::Simple-&gt;new ([ARG, ...])
       date ([ARG, ...])
               my $date = Date::Simple-&gt;new(&apos;1972-01-17&apos;);

           The &quot;new&quot; method will return a date object if the values passed in
           specify a valid date.  (See above.)  If an invalid date is passed,
           the method returns undef.  If the argument is invalid in form as
           opposed to numeric range, &quot;new&quot; dies.

           The &quot;date&quot; function provides the same functionality but must be
           imported or qualified as &quot;Date::Simple::date&quot;.  (To import all public
           functions, do &quot;use Date::Simple (&apos;:all&apos;);&quot;.)  This function returns
           undef on all invalid input, rather than dying in some cases like
           &quot;new&quot;.

       date_fmt (FMT,[ARG, ...])
           Equivelent to &quot;date&quot; but creates a Date::Simple::Fmt object instead.
           The format is expected to be a valid POSIX::strftime format string.

       date_iso ([ARG, ...])
           Identical to &quot;date&quot; but creates a Date::Simple::ISO object instead.

       date_d8 ([ARG, ...])
           Equivelent to &quot;date&quot; but creates a Date::Simple::D8 object instead.

       today()
           Returns the current date according to &quot;localtime&quot;.

           Caution: To get tomorrow&apos;s date (or any fixed offset from today), do
           not use &quot;today + 1&quot;.  Perl parses this as &quot;today(+1)&quot;.  You need to
           put empty parentheses after the function: &quot;today() + 1&quot;.

       ymd (YEAR, MONTH, DAY)
           Returns a date object with the given year, month, and day numbers.
           If the arguments do not specify a valid date, undef is returned.

           Example:

               use Date::Simple (&apos;ymd&apos;);
               $pbd = ymd(1987, 12, 18);

       d8 (STRING)
           Parses STRING as &quot;YYYYMMDD&quot; and returns the corresponding date
           object, or undef if STRING has the wrong format or specifies an
           invalid date.

           Example:

               use Date::Simple (&apos;d8&apos;);
               $doi = d8(&apos;17760704&apos;);

           Mnemonic: The string matches &quot;/\d{8}/&quot;.  Also, &quot;d8&quot; spells &quot;date&quot;, if
           8 is expanded phonetically.

INSTANCE METHODS
       DATE-&gt;next
               my $tomorrow = $today-&gt;next;

           Returns an object representing tomorrow.

       DATE-&gt;prev
              my $yesterday = $today-&gt;prev;
&#12;           Returns an object representing yesterday.

       DATE-&gt;year
               my $year  = $date-&gt;year;

           Return the year of DATE as an integer.

       DATE-&gt;month
               my $month = $date-&gt;month;

           Return the month of DATE as an integer from 1 to 12.

       DATE-&gt;day
               my $day   = $date-&gt;day;

           Return the DATE&apos;s day of the month as an integer from 1 to 31.

       DATE-&gt;day_of_week
           Return a number representing DATE&apos;s day of the week from 0 to 6,
           where 0 means Sunday.

       DATE-&gt;as_ymd
               my ($year, $month, $day) = $date-&gt;as_ymd;

           Returns a list of three numbers: year, month, and day.

       DATE-&gt;as_d8
           Returns the &quot;d8&quot; representation (see &quot;d8&quot;), like
           &quot;$date-&gt;format(&quot;%Y%m%d&quot;)&quot;.

       DATE-&gt;as_iso
           Returns the ISO 8601 representation of the date (eg &apos;2004-01-01&apos;),
           like &quot;$date-&gt;format(&quot;%Y-%m-%d&quot;)&quot;. This is in fact the default
           overloaded stringification mechanism and is provided mostly so other
           subclasses with different overloading can still do fast ISO style
           date output.

       DATE-&gt;as_str ([STRING])
       DATE-&gt;format ([STRING])
       DATE-&gt;strftime ([STRING])
           These functions are equivalent.  Return a string representing the
           date, in the format specified.  If you don&apos;t pass a parameter, the
           default date format for the object is used if one has been specified,
           otherwise uses the default date format for the class the object is a
           member of, or as a last fallback uses the
           $Date::Simple::Standard_Format which is changeable, but probably
           shouldn&apos;t be modified. See &quot;default_format&quot; for details.

               my $change_date = $date-&gt;format(&quot;%d %b %y&quot;);
               my $iso_date1 = $date-&gt;format(&quot;%Y-%m-%d&quot;);
               my $iso_date2 = $date-&gt;format;

           The formatting parameter is similar to one you would pass to
           strftime(3).  This is because we actually do pass it to strftime to
           format the date.  This may result in differing behavior across
           platforms and locales and may not even work everywhere.

       DATE-&gt;default_format ([FORMAT])
           This method sets or gets the default_format for the DATE object or
           class that it is called on.

OPERATORS
       Some operators can be used with Date::Simple instances.  If one side of
       an expression is a date object, and the operator expects two date
       objects, the other side is interpreted as &quot;date(ARG)&quot;, so an array
       reference or ISO 8601 string will work.

       DATE + NUMBER
       DATE - NUMBER
           You can construct a new date offset by a number of days using the &quot;+&quot;
           and &quot;-&quot; operators.

       DATE1 - DATE2
           You can subtract two dates to find the number of days between them.

       DATE1 == DATE2
       DATE1 &lt; DATE2
       DATE1 &lt;=&gt; DATE2
       DATE1 cmp DATE2
       etc.
           You can compare two dates using the arithmetic or string comparison
           operators.  Equality tests (&quot;==&quot; and &quot;eq&quot;) return false when one of
           the expressions can not be converted to a date.  Other comparison
           tests die in such cases.  This is intentional, because in a sense,
           all non-dates are not &quot;equal&quot; to all dates, but in no sense are they
           &quot;greater&quot; or &quot;less&quot; than dates.

       DATE += NUMBER
       DATE -= NUMBER
           You can increment or decrement a date by a number of days using the
           += and -= operators.  This actually generates a new date object and
           is equivalent to &quot;$date = $date + $number&quot;.

       &quot;$date&quot;
           You can interpolate a date instance directly into a string, in the
           format specified by ISO 8601 (eg: 2000-01-17) for Date::Simple and
           Date::Simple::ISO, for Date::Simple::D8 this is the same as calling
           as_d8() on the object, and for Date::Simple::Fmt this is the same as
           calling format() on the object.

UTILITIES
       leap_year (YEAR)
           Returns true if YEAR is a leap year.

       days_in_month (YEAR, MONTH)
           Returns the number of days in MONTH, YEAR.

       leap_year (YEAR)
           Returns true if YEAR is a leap year.

       days_in_month (YEAR, MONTH)
           Returns the number of days in MONTH, YEAR.

AUTHOR
           Marty Pauley &lt;marty@kasei.com&gt;
           John Tobey &lt;jtobey@john-edwin-tobey.org&gt;
           Yves Orton &lt;demerphq@hotmail.com&gt;

COPYRIGHT
             Copyright (C) 2001  Kasei.
             Copyright (C) 2001,2002 John Tobey.
             Copyright (C) 2004 Yves Orton.

             This program is free software; you can redistribute it and/or
             modify it under the terms of either:

             a) the GNU General Public License;
                either version 2 of the License, or (at your option) any later
                version.  You should have received a copy of the GNU General
                Public License along with this program; see the file COPYING.
                If not, write to the Free Software Foundation, Inc., 59
                Temple Place, Suite 330, Boston, MA 02111-1307 USA

             b) the Perl Artistic License.

             This program is distributed in the hope that it will be useful,
             but WITHOUT ANY WARRANTY; without even the implied warranty of
             MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.

SEE ALSO
       Date::Simple::Fmt Date::Simple::ISO Date::Simple::D8 and of course perl



perl v5.14.2                       2008-01-11                  Date::Simple(3pm)

</pre></body></html>
