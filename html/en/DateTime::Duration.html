<html>
<head><meta charset=utf-8/>
<title>DateTime::Duration - Duration objects for date math</title></head>
<body><pre>

DateTime::Duration(3pm)User Contributed Perl DocumentatioDateTime::Duration(3pm)



NAME
       DateTime::Duration - Duration objects for date math

VERSION
       version 0.75

SYNOPSIS
         use DateTime::Duration;

         $dur = DateTime::Duration-&gt;new(
             years       =&gt; 3,
             months      =&gt; 5,
             weeks       =&gt; 1,
             days        =&gt; 1,
             hours       =&gt; 6,
             minutes     =&gt; 15,
             seconds     =&gt; 45,
             nanoseconds =&gt; 12000
         );

         my ( $days, $hours, $seconds ) = $dur-&gt;in_units(&apos;days&apos;, &apos;hours&apos;, &apos;seconds&apos;);

         # Human-readable accessors, always positive, but consider using
         # DateTime::Format::Duration instead
         $dur-&gt;years;
         $dur-&gt;months;
         $dur-&gt;weeks;
         $dur-&gt;days;
         $dur-&gt;hours;
         $dur-&gt;minutes;
         $dur-&gt;seconds;
         $dur-&gt;nanoseconds;

         $dur-&gt;is_wrap_mode
         $dur-&gt;is_limit_mode
         $dur-&gt;is_preserve_mode

         print $dur-&gt;end_of_month_mode;

         # Multiply all values by -1
         my $opposite = $dur-&gt;inverse;

         my $bigger  = $dur1 + $dur2;
         my $smaller = $dur1 - $dur2; # the result could be negative
         my $bigger  = $dur1 * 3;

         my $base_dt = DateTime-&gt;new( year =&gt; 2000 );
         my @sorted =
             sort { DateTime::Duration-&gt;compare( $a, $b, $base_dt ) } @durations;

         if ( $dur-&gt;is_positive ) { ... }
         if ( $dur-&gt;is_zero )     { ... }
         if ( $dur-&gt;is_negative ) { ... }

DESCRIPTION
       This is a simple class for representing duration objects. These objects
       are used whenever you do date math with DateTime.pm.

       See the How DateTime Math Works section of the DateTime.pm documentation
       for more details. The short course: One cannot in general convert between
       seconds, minutes, days, and months, so this class will never do so.
       Instead, create the duration with the desired units to begin with, for
       example by calling the appropriate subtraction/delta method on a
       &quot;DateTime.pm&quot; object.

METHODS
       Like &quot;DateTime&quot; itself, &quot;DateTime::Duration&quot; returns the object from
       mutator methods in order to make method chaining possible.

       &quot;DateTime::Duration&quot; has the following methods:

       &#183;   new( ... )

           This method takes the parameters &quot;years&quot;, &quot;months&quot;, &quot;weeks&quot;, &quot;days&quot;,
           &quot;hours&quot;, &quot;minutes&quot;, &quot;seconds&quot;, &quot;nanoseconds&quot;, and &quot;end_of_month&quot;. All
           of these except &quot;end_of_month&quot; are numbers. If any of the numbers are
           negative, the entire duration is negative.

           All of the numbers must be integers.

           Internally, years as just treated as 12 months. Similarly, weeks are
           treated as 7 days, and hours are converted to minutes. Seconds and
           nanoseconds are both treated separately.

           The &quot;end_of_month&quot; parameter must be either &quot;wrap&quot;, &quot;limit&quot;, or
           &quot;preserve&quot;. This parameter specifies how date math that crosses the
           end of a month is handled.

           In &quot;wrap&quot; mode, adding months or years that result in days beyond the
           end of the new month will roll over into the following month. For
           instance, adding one year to Feb 29 will result in Mar 1.

           If you specify &quot;end_of_month&quot; mode as &quot;limit&quot;, the end of the month
           is never crossed. Thus, adding one year to Feb 29, 2000 will result
           in Feb 28, 2001. If you were to then add three more years this will
           result in Feb 28, 2004.

           If you specify &quot;end_of_month&quot; mode as &quot;preserve&quot;, the same
           calculation is done as for &quot;limit&quot; except that if the original date
           is at the end of the month the new date will also be. For instance,
           adding one month to Feb 29, 2000 will result in Mar 31, 2000.

           For positive durations, the &quot;end_of_month&quot; parameter defaults to
           wrap.  For negative durations, the default is &quot;limit&quot;. This should
           match how most people &quot;intuitively&quot; expect datetime math to work.

       &#183;   clone

           Returns a new object with the same properties as the object on which
           this method was called.

       &#183;   in_units( ... )

           Returns the length of the duration in the units (any of those that
           can be passed to &quot;new&quot;) given as arguments. All lengths are integral,
           but may be negative. Smaller units are computed from what remains
           after taking away the larger units given, so for example:

             my $dur = DateTime::Duration-&gt;new( years =&gt; 1, months =&gt; 15 );

             $dur-&gt;in_units( &apos;years&apos; );            # 2
             $dur-&gt;in_units( &apos;months&apos; );           # 27
             $dur-&gt;in_units( &apos;years&apos;, &apos;months&apos; );  # (2, 3)
             $dur-&gt;in_units( &apos;weeks&apos;, &apos;days&apos; );    # (0, 0) !

           The last example demonstrates that there will not be any conversion
           between units which don&apos;t have a fixed conversion rate. The only
           conversions possible are:

           &#183;       years &lt;=&gt; months

           &#183;       weeks &lt;=&gt; days

           &#183;       hours &lt;=&gt; minutes

           &#183;       seconds &lt;=&gt; nanoseconds

           For the explanation of why this is the case, please see the How
           DateTime Math Works section of the DateTime.pm documentation

           Note that the numbers returned by this method may not match the
           values given to the constructor.

           In list context, in_units returns the lengths in the order of the
           units given. In scalar context, it returns the length in the first
           unit (but still computes in terms of all given units).

           If you need more flexibility in presenting information about
           durations, please take a look a &quot;DateTime::Format::Duration&quot;.

       &#183;   is_positive, is_zero, is_negative

           Indicates whether or not the duration is positive, zero, or negative.

           If the duration contains both positive and negative units, then it
           will return false for all of these methods.

       &#183;   is_wrap_mode, is_limit_mode, is_preserve_mode

           Indicates what mode is used for end of month wrapping.

       &#183;   end_of_month_mode

           Returns one of &quot;wrap&quot;, &quot;limit&quot;, or &quot;preserve&quot;.

       &#183;   calendar_duration

           Returns a new object with the same calendar delta (months and days
           only) and end of month mode as the current object.

       &#183;   clock_duration

           Returns a new object with the same clock deltas (minutes, seconds,
           and nanoseconds) and end of month mode as the current object.

       &#183;   inverse( ... )

           Returns a new object with the same deltas as the current object, but
           multiple by -1. The end of month mode for the new object will be the
           default end of month mode, which depends on whether the new duration
           is positive or negative.

           You can set the end of month mode in the inverted duration explicitly
           by passing &quot;end_of_month =&gt; ...&quot; to the &quot;inverse()&quot; method.

       &#183;   add_duration( $duration_object ), subtract_duration( $duration_object
           )

           Adds or subtracts one duration from another.

       &#183;   add( ... ), subtract( ... )

           Syntactic sugar for addition and subtraction. The parameters given to
           these methods are used to create a new object, which is then passed
           to &quot;add_duration()&quot; or &quot;subtract_duration()&quot;, as appropriate.

       &#183;   multiply( $number )

           Multiplies each unit in the by the specified number.

       &#183;   DateTime::Duration-&gt;compare( $duration1, $duration2, $base_datetime )

           This is a class method that can be used to compare or sort durations.
           Comparison is done by adding each duration to the specified
           &quot;DateTime.pm&quot; object and comparing the resulting datetimes. This is
           necessary because without a base, many durations are not comparable.
           For example, 1 month may or may not be longer than 29 days, depending
           on what datetime it is added to.

           If no base datetime is given, then the result of &quot;DateTime-&gt;now&quot; is
           used instead. Using this default will give non-repeatable results if
           used to compare two duration objects containing different units.  It
           will also give non-repeatable results if the durations contain
           multiple types of units, such as months and days.

           However, if you know that both objects only consist of one type of
           unit (months or days or hours, etc.), and each duration contains the
           same type of unit, then the results of the comparison will be
           repeatable.

       &#183;   delta_months, delta_days, delta_minutes, delta_seconds,
           delta_nanoseconds

           These methods provide the information &quot;DateTime.pm&quot; needs for doing
           date math. The numbers returned may be positive or negative. This is
           mostly useful for doing date math in DateTime.

       &#183;   deltas

           Returns a hash with the keys &quot;months&quot;, &quot;days&quot;, &quot;minutes&quot;, &quot;seconds&quot;,
           and &quot;nanoseconds&quot;, containing all the delta information for the
           object. This is mostly useful for doing date math in DateTime.

       &#183;   years, months, weeks, days, hours, minutes, seconds, nanoseconds

           These methods return numbers indicating how many of the given unit
           the object represents, after having done a conversion to any larger
           units.  For example, days are first converted to weeks, and then the
           remainder is returned. These numbers are always positive.

           Here&apos;s what each method returns:

            $dur-&gt;years()       == abs( $dur-&gt;in_units(&apos;years&apos;) )
            $dur-&gt;months()      == abs( ( $dur-&gt;in_units( &apos;months&apos;, &apos;years&apos; ) )[0] )
            $dur-&gt;weeks()       == abs( $dur-&gt;in_units( &apos;weeks&apos; ) )
            $dur-&gt;days()        == abs( ( $dur-&gt;in_units( &apos;days&apos;, &apos;weeks&apos; ) )[0] )
            $dur-&gt;hours()       == abs( $dur-&gt;in_units( &apos;hours&apos; ) )
            $dur-&gt;minutes       == abs( ( $dur-&gt;in_units( &apos;minutes&apos;, &apos;hours&apos; ) )[0] )
            $dur-&gt;seconds       == abs( $dur-&gt;in_units( &apos;seconds&apos; ) )
            $dur-&gt;nanoseconds() == abs( ( $dur-&gt;in_units( &apos;nanoseconds&apos;, &apos;seconds&apos; ) )[0] )

           If this seems confusing, remember that you can always use the
           &quot;in_units()&quot; method to specify exactly what you want.

           Better yet, if you are trying to generate output suitable for humans,
           use the &quot;DateTime::Format::Duration&quot; module.

   Overloading
       This class overloads addition, subtraction, and mutiplication.
&#12;       Comparison is not overloaded. If you attempt to compare durations using
       &quot;&lt;=&gt;&quot; or &quot;cmp&quot;, then an exception will be thrown!  Use the &quot;compare()&quot;
       class method instead.

SUPPORT
       Support for this module is provided via the datetime@perl.org email list.
       See http://lists.perl.org/ for more details.

SEE ALSO
       datetime@perl.org mailing list

       http://datetime.perl.org/

AUTHOR
       Dave Rolsky &lt;autarch@urth.org&gt;

COPYRIGHT AND LICENSE
       This software is Copyright (c) 2012 by Dave Rolsky.

       This is free software, licensed under:

         The Artistic License 2.0 (GPL Compatible)



perl v5.14.2                       2012-06-11            DateTime::Duration(3pm)

</pre></body></html>
