<html>
<head><meta charset=utf-8/>
<title>HTML::Tree::Scanning -- article: "Scanning HTML"</title></head>
<body><pre>

HTML::Tree::Scanning(3pUser Contributed Perl DocumentatHTML::Tree::Scanning(3pm)



NAME
       HTML::Tree::Scanning -- article: &quot;Scanning HTML&quot;

SYNOPSIS
         # This an article, not a module.

DESCRIPTION
       The following article by Sean M. Burke first appeared in The Perl Journal
       #19 and is copyright 2000 The Perl Journal. It appears courtesy of Jon
       Orwant and The Perl Journal.  This document may be distributed under the
       same terms as Perl itself.

       (Note that this is discussed in chapters 6 through 10 of the book Perl
       and LWP &lt;http://lwp.interglacial.com/&gt; which was written after the
       following documentation, and which is available free online.)

Scanning HTML
       -- Sean M. Burke

       In The Perl Journal issue 17, Ken MacFarlane&apos;s article &quot;Parsing HTML with
       HTML::Parser&quot; describes how the HTML::Parser module scans HTML source as
       a stream of start-tags, end-tags, text, comments, etc.  In TPJ #18, my
       &quot;Trees&quot; article kicked around the idea of tree-shaped data structures.
       Now I&apos;ll try to tie it together, in a discussion of HTML trees.

       The CPAN module HTML::TreeBuilder takes the tags that HTML::Parser picks
       out, and builds a parse tree -- a tree-shaped network of objects...

           Footnote: And if you need a quick explanation of objects, see my
           TPJ17 article &quot;A User&apos;s View of Object-Oriented Modules&quot;; or go whole
           hog and get Damian Conway&apos;s excellent book Object-Oriented Perl, from
           Manning Publications.

       ...representing the structured content of the HTML document.  And once
       the document is parsed as a tree, you&apos;ll find the common tasks of
       extracting data from that HTML document/tree to be quite straightforward.

   HTML::Parser, HTML::TreeBuilder, and HTML::Element
       You use HTML::TreeBuilder to make a parse tree out of an HTML source
       file, by simply saying:

         use HTML::TreeBuilder;
         my $tree = HTML::TreeBuilder-&gt;new();
         $tree-&gt;parse_file(&apos;foo.html&apos;);

       and then $tree contains a parse tree built from the HTML source from the
       file &quot;foo.html&quot;.  The way this parse tree is represented is with a
       network of objects -- $tree is the root, an element with tag-name &quot;html&quot;,
       and its children typically include a &quot;head&quot; and &quot;body&quot; element, and so
       on.  Elements in the tree are objects of the class HTML::Element.

       So, if you take this source:

         &lt;html&gt;&lt;head&gt;&lt;title&gt;Doc 1&lt;/title&gt;&lt;/head&gt;
         &lt;body&gt;
         Stuff &lt;hr&gt; 2000-08-17
         &lt;/body&gt;&lt;/html&gt;

       and feed it to HTML::TreeBuilder, it&apos;ll return a tree of objects that
       looks like this:

                      html
                    /      \
                head        body
               /          /   |  \
            title    &quot;Stuff&quot;  hr  &quot;2000-08-17&quot;
              |
           &quot;Doc 1&quot;
&#12;       This is a pretty simple document, but if it were any more complex, it&apos;d
       be a bit hard to draw in that style, since it&apos;s sprawl left and right.
       The same tree can be represented a bit more easily sideways, with
       indenting:

         . html
            . head
               . title
                  . &quot;Doc 1&quot;
            . body
               . &quot;Stuff&quot;
               . hr
               . &quot;2000-08-17&quot;

       Either way expresses the same structure.  In that structure, the root
       node is an object of the class HTML::Element

           Footnote: Well actually, the root is of the class HTML::TreeBuilder,
           but that&apos;s just a subclass of HTML::Element, plus the few extra
           methods like &quot;parse_file&quot; that elaborate the tree

       , with the tag name &quot;html&quot;, and with two children: an HTML::Element
       object whose tag names are &quot;head&quot; and &quot;body&quot;.  And each of those elements
       have children, and so on down.  Not all elements (as we&apos;ll call the
       objects of class HTML::Element) have children -- the &quot;hr&quot; element
       doesn&apos;t.  And note all nodes in the tree are elements -- the text nodes
       (&quot;Doc 1&quot;, &quot;Stuff&quot;, and &quot;2000-08-17&quot;) are just strings.

       Objects of the class HTML::Element each have three noteworthy attributes:

       &quot;_tag&quot; -- (best accessed as &quot;$e-&gt;tag&quot;) this element&apos;s tag-name,
       lowercased (e.g., &quot;em&quot; for an &quot;em&quot; element).
               Footnote: Yes, this is misnamed.  In proper SGML terminology,
               this is instead called a &quot;GI&quot;, short for &quot;generic identifier&quot;;
               and the term &quot;tag&quot; is used for a token of SGML source that
               represents either the start of an element (a start-tag like &quot;&lt;em
               lang=&apos;fr&apos;&gt;&quot;) or the end of an element (an end-tag like &quot;&lt;/em&gt;&quot;.
               However, since more people claim to have been abducted by aliens
               than to have ever seen the SGML standard, and since both
               encounters typically involve a feeling of &quot;missing time&quot;, it&apos;s
               not surprising that the terminology of the SGML standard is not
               closely followed.

       &quot;_parent&quot; -- (best accessed as &quot;$e-&gt;parent&quot;) the element that is $obj&apos;s
       parent, or undef if this element is the root of its tree.
       &quot;_content&quot; -- (best accessed as &quot;$e-&gt;content_list&quot;) the list of nodes
       (i.e., elements or text segments) that are $e&apos;s children.

       Moreover, if an element object has any attributes in the SGML sense of
       the word, then those are readable as &quot;$e-&gt;attr(&apos;name&apos;)&quot; -- for example,
       with the object built from having parsed &quot;&lt;a id=&apos;foo&apos;&gt;bar&lt;/a&gt;&quot;,
       &quot;$e-&gt;attr(&apos;id&apos;)&quot; will return the string &quot;foo&quot;.  Moreover, &quot;$e-&gt;tag&quot; on
       that object returns the string &quot;a&quot;, &quot;$e-&gt;content_list&quot; returns a list
       consisting of just the single scalar &quot;bar&quot;, and &quot;$e-&gt;parent&quot; returns the
       object that&apos;s this node&apos;s parent -- which may be, for example, a &quot;p&quot;
       element.

       And that&apos;s all that there is to it -- you throw HTML source at
       TreeBuilder, and it returns a tree built of HTML::Element objects and
       some text strings.

       However, what do you do with a tree of objects?  People code information
       into HTML trees not for the fun of arranging elements, but to represent
       the structure of specific text and images -- some text is in this &quot;li&quot;
       element, some other text is in that heading, some images are in that
       other table cell that has those attributes, and so on.

       Now, it may happen that you&apos;re rendering that whole HTML tree into some
       layout format.  Or you could be trying to make some systematic change to
       the HTML tree before dumping it out as HTML source again.  But, in my
       experience, by far the most common programming task that Perl programmers
       face with HTML is in trying to extract some piece of information from a
       larger document.  Since that&apos;s so common (and also since it involves
       concepts that are basic to more complex tasks), that is what the rest of
       this article will be about.

   Scanning HTML trees
       Suppose you have a thousand HTML documents, each of them a press release.
       They all start out:

         [...lots of leading images and junk...]
         &lt;h1&gt;ConGlomCo to Open New Corporate Office in Ougadougou&lt;/h1&gt;
         BAKERSFIELD, CA, 2000-04-24 -- ConGlomCo&apos;s vice president in charge
         of world conquest, Rock Feldspar, announced today the opening of a
         new office in Ougadougou, the capital city of Burkino Faso, gateway
         to the bustling &quot;Silicon Sahara&quot; of Africa...
         [...etc...]

       ...and what you&apos;ve got to do is, for each document, copy whatever text is
       in the &quot;h1&quot; element, so that you can, for example, make a table of
       contents of it.  Now, there are three ways to do this:

       &#183;   You can just use a regexp to scan the file for a text pattern.

           For many very simple tasks, this will do fine.  Many HTML documents
           are, in practice, very consistently formatted as far as placement of
           linebreaks and whitespace, so you could just get away with scanning
           the file like so:

             sub get_heading {
               my $filename = $_[0];
               local *HTML;
               open(HTML, $filename)
                 or die &quot;Couldn&apos;t open $filename);
               my $heading;
              Line:
               while(&lt;HTML&gt;) {
                 if( m{&lt;h1&gt;(.*?)&lt;/h1&gt;}i ) {  # match it!
                   $heading = $1;
                   last Line;
                 }
               }
               close(HTML);
               warn &quot;No heading in $filename?&quot;
                unless defined $heading;
               return $heading;
             }

           This is quick and fast, but awfully fragile -- if there&apos;s a newline
           in the middle of a heading&apos;s text, it won&apos;t match the above regexp,
           and you&apos;ll get an error.  The regexp will also fail if the &quot;h1&quot;
           element&apos;s start-tag has any attributes.  If you have to adapt your
           code to fit more kinds of start-tags, you&apos;ll end up basically
           reinventing part of HTML::Parser, at which point you should probably
           just stop, and use HTML::Parser itself:

       &#183;   You can use HTML::Parser to scan the file for an &quot;h1&quot; start-tag
           token, then capture all the text tokens until the &quot;h1&quot; close-tag.
           This approach is extensively covered in the Ken MacFarlane&apos;s TPJ17
           article &quot;Parsing HTML with HTML::Parser&quot;.  (A variant of this
           approach is to use HTML::TokeParser, which presents a different and
           rather handier interface to the tokens that HTML::Parser picks out.)

           Using HTML::Parser is less fragile than our first approach, since
           it&apos;s not sensitive to the exact internal formatting of the start-tag
           (much less whether it&apos;s split across two lines).  However, when you
           need more information about the context of the &quot;h1&quot; element, or if
           you&apos;re having to deal with any of the tricky bits of HTML, such as
           parsing of tables, you&apos;ll find out the flat list of tokens that
           HTML::Parser returns isn&apos;t immediately useful.  To get something
           useful out of those tokens, you&apos;ll need to write code that knows some
           things about what elements take no content (as with &quot;hr&quot; elements),
           and that a &quot;&lt;/p&gt;&quot; end-tags are omissible, so a &quot;&lt;p&gt;&quot; will end any
           currently open paragraph -- and you&apos;re well on your way to
           pointlessly reinventing much of the code in HTML::TreeBuilder
&#12;               Footnote: And, as the person who last rewrote that module, I can
               attest that it wasn&apos;t terribly easy to get right!  Never
               underestimate the perversity of people coding HTML.

           , at which point you should probably just stop, and use
           HTML::TreeBuilder itself:

       &#183;   You can use HTML::Treebuilder, and scan the tree of element objects
           that you get back.

       The last approach, using HTML::TreeBuilder, is the diametric opposite of
       first approach:  The first approach involves just elementary Perl and one
       regexp, whereas the TreeBuilder approach involves being at home with the
       concept of tree-shaped data structures and modules with object-oriented
       interfaces, as well as with the particular interfaces that
       HTML::TreeBuilder and HTML::Element provide.

       However, what the TreeBuilder approach has going for it is that it&apos;s the
       most robust, because it involves dealing with HTML in its &quot;native&quot; format
       -- it deals with the tree structure that HTML code represents, without
       any consideration of how the source is coded and with what tags omitted.

       So, to extract the text from the &quot;h1&quot; elements of an HTML document:

         sub get_heading {
           my $tree = HTML::TreeBuilder-&gt;new;
           $tree-&gt;parse_file($_[0]);   # !
           my $heading;
           my $h1 = $tree-&gt;look_down(&apos;_tag&apos;, &apos;h1&apos;);  # !
           if($h1) {
             $heading = $h1-&gt;as_text;   # !
           } else {
             warn &quot;No heading in $_[0]?&quot;;
           }
           $tree-&gt;delete; # clear memory!
           return $heading;
         }

       This uses some unfamiliar methods that need explaining.  The &quot;parse_file&quot;
       method that we&apos;ve seen before, builds a tree based on source from the
       file given.  The &quot;delete&quot; method is for marking a tree&apos;s contents as
       available for garbage collection, when you&apos;re done with the tree.  The
       &quot;as_text&quot; method returns a string that contains all the text bits that
       are children (or otherwise descendants) of the given node -- to get the
       text content of the $h1 object, we could just say:

         $heading = join &apos;&apos;, $h1-&gt;content_list;

       but that will work only if we&apos;re sure that the &quot;h1&quot; element&apos;s children
       will be only text bits -- if the document contained:

         &lt;h1&gt;Local Man Sees &lt;cite&gt;Blade&lt;/cite&gt; Again&lt;/h1&gt;

       then the sub-tree would be:

         . h1
           . &quot;Local Man Sees &quot;
           . cite
             . &quot;Blade&quot;
           . &quot; Again&apos;

       so &quot;join &apos;&apos;, $h1-&gt;content_list&quot; will be something like:

         Local Man Sees HTML::Element=HASH(0x15424040) Again

       whereas &quot;$h1-&gt;as_text&quot; would yield:

         Local Man Sees Blade Again

       and depending on what you&apos;re doing with the heading text, you might want
       the &quot;as_HTML&quot; method instead.  It returns the (sub)tree represented as
       HTML source.  &quot;$h1-&gt;as_HTML&quot; would yield:
&#12;         &lt;h1&gt;Local Man Sees &lt;cite&gt;Blade&lt;/cite&gt; Again&lt;/h1&gt;

       However, if you wanted the contents of $h1 as HTML, but not the $h1
       itself, you could say:

         join &apos;&apos;,
           map(
             ref($_) ? $_-&gt;as_HTML : $_,
             $h1-&gt;content_list
           )

       This &quot;map&quot; iterates over the nodes in $h1&apos;s list of children; and for
       each node that&apos;s just a text bit (as &quot;Local Man Sees &quot; is), it just
       passes through that string value, and for each node that&apos;s an actual
       object (causing &quot;ref&quot; to be true), &quot;as_HTML&quot; will used instead of the
       string value of the object itself (which would be something quite
       useless, as most object values are).  So that &quot;as_HTML&quot; for the &quot;cite&quot;
       element will be the string &quot;&lt;cite&gt;Blade&lt;/cite&gt;&quot;.  And then, finally,
       &quot;join&quot; just puts into one string all the strings that the &quot;map&quot; returns.

       Last but not least, the most important method in our &quot;get_heading&quot; sub is
       the &quot;look_down&quot; method.  This method looks down at the subtree starting
       at the given object ($h1), looking for elements that meet criteria you
       provide.

       The criteria are specified in the method&apos;s argument list.  Each criterion
       can consist of two scalars, a key and a value, which express that you
       want elements that have that attribute (like &quot;_tag&quot;, or &quot;src&quot;) with the
       given value (&quot;h1&quot;); or the criterion can be a reference to a subroutine
       that, when called on the given element, returns true if that is a node
       you&apos;re looking for.  If you specify several criteria, then that&apos;s taken
       to mean that you want all the elements that each satisfy all the
       criteria.  (In other words, there&apos;s an &quot;implicit AND&quot;.)

       And finally, there&apos;s a bit of an optimization -- if you call the
       &quot;look_down&quot; method in a scalar context, you get just the first node (or
       undef if none) -- and, in fact, once &quot;look_down&quot; finds that first
       matching element, it doesn&apos;t bother looking any further.

       So the example:

         $h1 = $tree-&gt;look_down(&apos;_tag&apos;, &apos;h1&apos;);

       returns the first element at-or-under $tree whose &quot;_tag&quot; attribute has
       the value &quot;h1&quot;.

   Complex Criteria in Tree Scanning
       Now, the above &quot;look_down&quot; code looks like a lot of bother, with barely
       more benefit than just grepping the file!  But consider if your criteria
       were more complicated -- suppose you found that some of the press
       releases that you were scanning had several &quot;h1&quot; elements, possibly
       before or after the one you actually want.  For example:

         &lt;h1&gt;&lt;center&gt;Visit Our Corporate Partner
          &lt;br&gt;&lt;a href=&quot;/dyna/clickthru&quot;
            &gt;&lt;img src=&quot;/dyna/vend_ad&quot;&gt;&lt;/a&gt;
         &lt;/center&gt;&lt;/h1&gt;
         &lt;h1&gt;&lt;center&gt;ConGlomCo President Schreck to Visit Regional HQ
          &lt;br&gt;&lt;a href=&quot;/photos/Schreck_visit_large.jpg&quot;
            &gt;&lt;img src=&quot;/photos/Schreck_visit.jpg&quot;&gt;&lt;/a&gt;
         &lt;/center&gt;&lt;/h1&gt;

       Here, you want to ignore the first &quot;h1&quot; element because it contains an
       ad, and you want the text from the second &quot;h1&quot;.  The problem is in
       formalizing the way you know that it&apos;s an ad.  Since ad banners are
       always entreating you to &quot;visit&quot; the sponsoring site, you could exclude
       &quot;h1&quot; elements that contain the word &quot;visit&quot; under them:

         my $real_h1 = $tree-&gt;look_down(
           &apos;_tag&apos;, &apos;h1&apos;,
           sub {
             $_[0]-&gt;as_text !~ m/\bvisit/i
           }
         );
&#12;       The first criterion looks for &quot;h1&quot; elements, and the second criterion
       limits those to only the ones whose text content doesn&apos;t match
       &quot;m/\bvisit/&quot;.  But unfortunately, that won&apos;t work for our example, since
       the second &quot;h1&quot; mentions &quot;ConGlomCo President Schreck to Visit Regional
       HQ&quot;.

       Instead you could try looking for the first &quot;h1&quot; element that doesn&apos;t
       contain an image:

         my $real_h1 = $tree-&gt;look_down(
           &apos;_tag&apos;, &apos;h1&apos;,
           sub {
             not $_[0]-&gt;look_down(&apos;_tag&apos;, &apos;img&apos;)
           }
         );

       This criterion sub might seem a bit odd, since it calls &quot;look_down&quot; as
       part of a larger &quot;look_down&quot; operation, but that&apos;s fine.  Note that when
       considered as a boolean value, a &quot;look_down&quot; in a scalar context value
       returns false (specifically, undef) if there&apos;s no matching element at or
       under the given element; and it returns the first matching element
       (which, being a reference and object, is always a true value), if any
       matches.  So, here,

         sub {
           not $_[0]-&gt;look_down(&apos;_tag&apos;, &apos;img&apos;)
         }

       means &quot;return true only if this element has no &apos;img&apos; element as
       descendants (and isn&apos;t an &apos;img&apos; element itself).&quot;

       This correctly filters out the first &quot;h1&quot; that contains the ad, but it
       also incorrectly filters out the second &quot;h1&quot; that contains a non-
       advertisement photo besides the headline text you want.

       There clearly are detectable differences between the first and second
       &quot;h1&quot; elements -- the only second one contains the string &quot;Schreck&quot;, and
       we could just test for that:

         my $real_h1 = $tree-&gt;look_down(
           &apos;_tag&apos;, &apos;h1&apos;,
           sub {
             $_[0]-&gt;as_text =~ m{Schreck}
           }
         );

       And that works fine for this one example, but unless all thousand of your
       press releases have &quot;Schreck&quot; in the headline, that&apos;s just not a general
       solution.  However, if all the ads-in-&quot;h1&quot;s that you want to exclude
       involve a link whose URL involves &quot;/dyna/&quot;, then you can use that:

         my $real_h1 = $tree-&gt;look_down(
           &apos;_tag&apos;, &apos;h1&apos;,
           sub {
             my $link = $_[0]-&gt;look_down(&apos;_tag&apos;,&apos;a&apos;);
             return 1 unless $link;
               # no link means it&apos;s fine
             return 0 if $link-&gt;attr(&apos;href&apos;) =~ m{/dyna/};
               # a link to there is bad
             return 1; # otherwise okay
           }
         );

       Or you can look at it another way and say that you want the first &quot;h1&quot;
       element that either contains no images, or else whose image has a &quot;src&quot;
       attribute whose value contains &quot;/photos/&quot;:

         my $real_h1 = $tree-&gt;look_down(
           &apos;_tag&apos;, &apos;h1&apos;,
           sub {
             my $img = $_[0]-&gt;look_down(&apos;_tag&apos;,&apos;img&apos;);
             return 1 unless $img;
               # no image means it&apos;s fine
             return 1 if $img-&gt;attr(&apos;src&apos;) =~ m{/photos/};
               # good if a photo
             return 0; # otherwise bad
           }
         );
&#12;       Recall that this use of &quot;look_down&quot; in a scalar context means to return
       the first element at or under $tree that matches all the criteria.  But
       if you notice that you can formulate criteria that&apos;ll match several
       possible &quot;h1&quot; elements, some of which may be bogus but the last one of
       which is always the one you want, then you can use &quot;look_down&quot; in a list
       context, and just use the last element of that list:

         my @h1s = $tree-&gt;look_down(
           &apos;_tag&apos;, &apos;h1&apos;,
           ...maybe more criteria...
         );
         die &quot;What, no h1s here?&quot; unless @h1s;
         my $real_h1 = $h1s[-1]; # last or only

   A Case Study: Scanning Yahoo News&apos;s HTML
       The above (somewhat contrived) case involves extracting data from a bunch
       of pre-existing HTML files.  In that sort of situation, if your code
       works for all the files, then you know that the code works -- since the
       data it&apos;s meant to handle won&apos;t go changing or growing; and, typically,
       once you&apos;ve used the program, you&apos;ll never need to use it again.

       The other kind of situation faced in many data extraction tasks is where
       the program is used recurringly to handle new data -- such as from ever-
       changing Web pages.  As a real-world example of this, consider a program
       that you could use (suppose it&apos;s crontabbed) to extract headline-links
       from subsections of Yahoo News (&quot;http://dailynews.yahoo.com/&quot;).

       Yahoo News has several subsections:

       http://dailynews.yahoo.com/h/tc/ for technology news
       http://dailynews.yahoo.com/h/sc/ for science news
       http://dailynews.yahoo.com/h/hl/ for health news
       http://dailynews.yahoo.com/h/wl/ for world news
       http://dailynews.yahoo.com/h/en/ for entertainment news

       and others.  All of them are built on the same basic HTML template -- and
       a scarily complicated template it is, especially when you look at it with
       an eye toward making up rules that will select where the real headline-
       links are, while screening out all the links to other parts of Yahoo,
       other news services, etc.  You will need to puzzle over the HTML source,
       and scrutinize the output of &quot;$tree-&gt;dump&quot; on the parse tree of that
       HTML.

       Sometimes the only way to pin down what you&apos;re after is by position in
       the tree. For example, headlines of interest may be in the third column
       of the second row of the second table element in a page:

         my $table = ( $tree-&gt;look_down(&apos;_tag&apos;,&apos;table&apos;) )[1];
         my $row2  = ( $table-&gt;look_down(&apos;_tag&apos;, &apos;tr&apos; ) )[1];
         my $col3  = ( $row2-&gt;look-down(&apos;_tag&apos;, &apos;td&apos;)   )[2];
         ...then do things with $col3...

       Or they may be all the links in a &quot;p&quot; element that has at least three
       &quot;br&quot; elements as children:

         my $p = $tree-&gt;look_down(
           &apos;_tag&apos;, &apos;p&apos;,
           sub {
             2 &lt; grep { ref($_) and $_-&gt;tag eq &apos;br&apos; }
                      $_[0]-&gt;content_list
           }
         );
         @links = $p-&gt;look_down(&apos;_tag&apos;, &apos;a&apos;);

       But almost always, you can get away with looking for properties of the of
       the thing itself, rather than just looking for contexts.  Now, if you&apos;re
       lucky, the document you&apos;re looking through has clear semantic tagging,
       such is as useful in CSS -- note the class=&quot;headlinelink&quot; bit here:

         &lt;a href=&quot;...long_news_url...&quot; class=&quot;headlinelink&quot;&gt;Elvis
         seen in tortilla&lt;/a&gt;

       If you find anything like that, you could leap right in and select links
       with:

         @links = $tree-&gt;look_down(&apos;class&apos;,&apos;headlinelink&apos;);

       Regrettably, your chances of seeing any sort of semantic markup
       principles really being followed with actual HTML are pretty thin.

           Footnote: In fact, your chances of finding a page that is simply free
           of HTML errors are even thinner.  And surprisingly, sites like Amazon
           or Yahoo are typically worse as far as quality of code than personal
           sites whose entire production cycle involves simply being saved and
           uploaded from Netscape Composer.

       The code may be sort of &quot;accidentally semantic&quot;, however -- for example,
       in a set of pages I was scanning recently, I found that looking for &quot;td&quot;
       elements with a &quot;width&quot; attribute value of &quot;375&quot; got me exactly what I
       wanted.  No-one designing that page ever conceived of &quot;width=375&quot; as
       meaning &quot;this is a headline&quot;, but if you impute it to mean that, it
       works.

       An approach like this happens to work for the Yahoo News code, because
       the headline-links are distinguished by the fact that they (and they
       alone) contain a &quot;b&quot; element:

         &lt;a href=&quot;...long_news_url...&quot;&gt;&lt;b&gt;Elvis seen in tortilla&lt;/b&gt;&lt;/a&gt;

       or, diagrammed as a part of the parse tree:

         . a  [href=&quot;...long_news_url...&quot;]
           . b
             . &quot;Elvis seen in tortilla&quot;

       A rule that matches these can be formalized as &quot;look for any &apos;a&apos; element
       that has only one daughter node, which must be a &apos;b&apos; element&quot;.  And this
       is what it looks like when cooked up as a &quot;look_down&quot; expression and
       prefaced with a bit of code that retrieves the text of the given Yahoo
       News page and feeds it to TreeBuilder:

         use strict;
         use HTML::TreeBuilder 2.97;
         use LWP::UserAgent;
         sub get_headlines {
           my $url = $_[0] || die &quot;What URL?&quot;;

           my $response = LWP::UserAgent-&gt;new-&gt;request(
             HTTP::Request-&gt;new( GET =&gt; $url )
           );
           unless($response-&gt;is_success) {
             warn &quot;Couldn&apos;t get $url: &quot;, $response-&gt;status_line, &quot;\n&quot;;
             return;
           }

           my $tree = HTML::TreeBuilder-&gt;new();
           $tree-&gt;parse($response-&gt;content);
           $tree-&gt;eof;

           my @out;
           foreach my $link (
             $tree-&gt;look_down(   # !
               &apos;_tag&apos;, &apos;a&apos;,
               sub {
                 return unless $_[0]-&gt;attr(&apos;href&apos;);
                 my @c = $_[0]-&gt;content_list;
                 @c == 1 and ref $c[0] and $c[0]-&gt;tag eq &apos;b&apos;;
               }
             )
           ) {
             push @out, [ $link-&gt;attr(&apos;href&apos;), $link-&gt;as_text ];
           }

           warn &quot;Odd, fewer than 6 stories in $url!&quot; if @out &lt; 6;
           $tree-&gt;delete;
           return @out;
         }

       ...and add a bit of code to actually call that routine and display the
       results...

         foreach my $section (qw[tc sc hl wl en]) {
           my @links = get_headlines(
             &quot;http://dailynews.yahoo.com/h/$section/&quot;
           );
           print
             $section, &quot;: &quot;, scalar(@links), &quot; stories\n&quot;,
             map((&quot;  &quot;, $_-&gt;[0], &quot; : &quot;, $_-&gt;[1], &quot;\n&quot;), @links),
             &quot;\n&quot;;
         }
&#12;       And we&apos;ve got our own headline-extractor service!  This in and of itself
       isn&apos;t no amazingly useful (since if you want to see the headlines, you
       can just look at the Yahoo News pages), but it could easily be the basis
       for quite useful features like filtering the headlines for matching
       certain keywords of interest to you.

       Now, one of these days, Yahoo News will decide to change its HTML
       template.  When this happens, this will appear to the above program as
       there being no links that meet the given criteria; or, less likely,
       dozens of erroneous links will meet the criteria.  In either case, the
       criteria will have to be changed for the new template; they may just need
       adjustment, or you may need to scrap them and start over.

   Regardez, duvet!
       It&apos;s often quite a challenge to write criteria to match the desired parts
       of an HTML parse tree.  Very often you can pull it off with a simple
       &quot;$tree-&gt;look_down(&apos;_tag&apos;, &apos;h1&apos;)&quot;, but sometimes you do have to keep
       adding and refining criteria, until you might end up with complex filters
       like what I&apos;ve shown in this article.  The benefit to learning how to
       deal with HTML parse trees is that one main search tool, the &quot;look_down&quot;
       method, can do most of the work, making simple things easy, while still
       making hard things possible.

       [end body of article]

   [Author Credit]
       Sean M. Burke (&quot;sburke@cpan.org&quot;) is the current maintainer of
       &quot;HTML::TreeBuilder&quot; and &quot;HTML::Element&quot;, both originally by Gisle Aas.

       Sean adds: &quot;I&apos;d like to thank the folks who listened to me ramble
       incessantly about HTML::TreeBuilder and HTML::Element at this year&apos;s Yet
       Another Perl Conference and O&apos;Reilly Open Source Software Convention.&quot;

BACK
       Return to the HTML::Tree docs.



perl v5.14.2                       2012-06-30          HTML::Tree::Scanning(3pm)

</pre></body></html>
