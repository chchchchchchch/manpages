<html>
<head><meta charset=utf-8/>
<title>HTTP::Daemon - a simple http server class</title></head>
<body><pre>

HTTP::Daemon(3pm)      User Contributed Perl Documentation     HTTP::Daemon(3pm)



NAME
       HTTP::Daemon - a simple http server class

SYNOPSIS
         use HTTP::Daemon;
         use HTTP::Status;

         my $d = HTTP::Daemon-&gt;new || die;
         print &quot;Please contact me at: &lt;URL:&quot;, $d-&gt;url, &quot;&gt;\n&quot;;
         while (my $c = $d-&gt;accept) {
             while (my $r = $c-&gt;get_request) {
                 if ($r-&gt;method eq &apos;GET&apos; and $r-&gt;uri-&gt;path eq &quot;/xyzzy&quot;) {
                     # remember, this is *not* recommended practice :-)
                     $c-&gt;send_file_response(&quot;/etc/passwd&quot;);
                 }
                 else {
                     $c-&gt;send_error(RC_FORBIDDEN)
                 }
             }
             $c-&gt;close;
             undef($c);
         }

DESCRIPTION
       Instances of the &quot;HTTP::Daemon&quot; class are HTTP/1.1 servers that listen on
       a socket for incoming requests. The &quot;HTTP::Daemon&quot; is a subclass of
       &quot;IO::Socket::INET&quot;, so you can perform socket operations directly on it
       too.

       The accept() method will return when a connection from a client is
       available.  The returned value will be an &quot;HTTP::Daemon::ClientConn&quot;
       object which is another &quot;IO::Socket::INET&quot; subclass.  Calling the
       get_request() method on this object will read data from the client and
       return an &quot;HTTP::Request&quot; object.  The ClientConn object also provide
       methods to send back various responses.

       This HTTP daemon does not fork(2) for you.  Your application, i.e. the
       user of the &quot;HTTP::Daemon&quot; is responsible for forking if that is
       desirable.  Also note that the user is responsible for generating
       responses that conform to the HTTP/1.1 protocol.

       The following methods of &quot;HTTP::Daemon&quot; are new (or enhanced) relative to
       the &quot;IO::Socket::INET&quot; base class:

       $d = HTTP::Daemon-&gt;new
       $d = HTTP::Daemon-&gt;new( %opts )
           The constructor method takes the same arguments as the
           &quot;IO::Socket::INET&quot; constructor, but unlike its base class it can also
           be called without any arguments.  The daemon will then set up a
           listen queue of 5 connections and allocate some random port number.

           A server that wants to bind to some specific address on the standard
           HTTP port will be constructed like this:

             $d = HTTP::Daemon-&gt;new(
                      LocalAddr =&gt; &apos;www.thisplace.com&apos;,
                      LocalPort =&gt; 80,
                  );

           See IO::Socket::INET for a description of other arguments that can be
           used configure the daemon during construction.
&#12;       $c = $d-&gt;accept
       $c = $d-&gt;accept( $pkg )
       ($c, $peer_addr) = $d-&gt;accept
           This method works the same the one provided by the base class, but it
           returns an &quot;HTTP::Daemon::ClientConn&quot; reference by default.  If a
           package name is provided as argument, then the returned object will
           be blessed into the given class.  It is probably a good idea to make
           that class a subclass of &quot;HTTP::Daemon::ClientConn&quot;.

           The accept method will return &quot;undef&quot; if timeouts have been enabled
           and no connection is made within the given time.  The timeout()
           method is described in IO::Socket.

           In list context both the client object and the peer address will be
           returned; see the description of the accept method IO::Socket for
           details.

       $d-&gt;url
           Returns a URL string that can be used to access the server root.

       $d-&gt;product_tokens
           Returns the name that this server will use to identify itself.  This
           is the string that is sent with the &quot;Server&quot; response header.  The
           main reason to have this method is that subclasses can override it if
           they want to use another product name.

           The default is the string &quot;libwww-perl-daemon/#.##&quot; where &quot;#.##&quot; is
           replaced with the version number of this module.

       The &quot;HTTP::Daemon::ClientConn&quot; is a &quot;IO::Socket::INET&quot; subclass.
       Instances of this class are returned by the accept() method of
       &quot;HTTP::Daemon&quot;.  The following methods are provided:

       $c-&gt;get_request
       $c-&gt;get_request( $headers_only )
           This method reads data from the client and turns it into an
           &quot;HTTP::Request&quot; object which is returned.  It returns &quot;undef&quot; if
           reading fails.  If it fails, then the &quot;HTTP::Daemon::ClientConn&quot;
           object ($c) should be discarded, and you should not try call this
           method again on it.  The $c-&gt;reason method might give you some
           information about why $c-&gt;get_request failed.

           The get_request() method will normally not return until the whole
           request has been received from the client.  This might not be what
           you want if the request is an upload of a large file (and with
           chunked transfer encoding HTTP can even support infinite request
           messages - uploading live audio for instance).  If you pass a TRUE
           value as the $headers_only argument, then get_request() will return
           immediately after parsing the request headers and you are responsible
           for reading the rest of the request content.  If you are going to
           call $c-&gt;get_request again on the same connection you better read the
           correct number of bytes.

       $c-&gt;read_buffer
       $c-&gt;read_buffer( $new_value )
           Bytes read by $c-&gt;get_request, but not used are placed in the read
           buffer.  The next time $c-&gt;get_request is called it will consume the
           bytes in this buffer before reading more data from the network
           connection itself.  The read buffer is invalid after $c-&gt;get_request
           has failed.

           If you handle the reading of the request content yourself you need to
           empty this buffer before you read more and you need to place
           unconsumed bytes here.  You also need this buffer if you implement
           services like 101 Switching Protocols.
&#12;           This method always returns the old buffer content and can optionally
           replace the buffer content if you pass it an argument.

       $c-&gt;reason
           When $c-&gt;get_request returns &quot;undef&quot; you can obtain a short string
           describing why it happened by calling $c-&gt;reason.

       $c-&gt;proto_ge( $proto )
           Return TRUE if the client announced a protocol with version number
           greater or equal to the given argument.  The $proto argument can be a
           string like &quot;HTTP/1.1&quot; or just &quot;1.1&quot;.

       $c-&gt;antique_client
           Return TRUE if the client speaks the HTTP/0.9 protocol.  No status
           code and no headers should be returned to such a client.  This should
           be the same as !$c-&gt;proto_ge(&quot;HTTP/1.0&quot;).

       $c-&gt;head_request
           Return TRUE if the last request was a &quot;HEAD&quot; request.  No content
           body must be generated for these requests.

       $c-&gt;force_last_request
           Make sure that $c-&gt;get_request will not try to read more requests off
           this connection.  If you generate a response that is not self
           delimiting, then you should signal this fact by calling this method.

           This attribute is turned on automatically if the client announces
           protocol HTTP/1.0 or worse and does not include a &quot;Connection: Keep-
           Alive&quot; header.  It is also turned on automatically when HTTP/1.1 or
           better clients send the &quot;Connection: close&quot; request header.

       $c-&gt;send_status_line
       $c-&gt;send_status_line( $code )
       $c-&gt;send_status_line( $code, $mess )
       $c-&gt;send_status_line( $code, $mess, $proto )
           Send the status line back to the client.  If $code is omitted 200 is
           assumed.  If $mess is omitted, then a message corresponding to $code
           is inserted.  If $proto is missing the content of the
           $HTTP::Daemon::PROTO variable is used.

       $c-&gt;send_crlf
           Send the CRLF sequence to the client.

       $c-&gt;send_basic_header
       $c-&gt;send_basic_header( $code )
       $c-&gt;send_basic_header( $code, $mess )
       $c-&gt;send_basic_header( $code, $mess, $proto )
           Send the status line and the &quot;Date:&quot; and &quot;Server:&quot; headers back to
           the client.  This header is assumed to be continued and does not end
           with an empty CRLF line.

           See the description of send_status_line() for the description of the
           accepted arguments.

       $c-&gt;send_header( $field, $value )
       $c-&gt;send_header( $field1, $value1, $field2, $value2, ... )
           Send one or more header lines.

       $c-&gt;send_response( $res )
           Write a &quot;HTTP::Response&quot; object to the client as a response.  We try
           hard to make sure that the response is self delimiting so that the
           connection can stay persistent for further request/response
           exchanges.

           The content attribute of the &quot;HTTP::Response&quot; object can be a normal
           string or a subroutine reference.  If it is a subroutine, then
           whatever this callback routine returns is written back to the client
           as the response content.  The routine will be called until it return
           an undefined or empty value.  If the client is HTTP/1.1 aware then we
           will use chunked transfer encoding for the response.

       $c-&gt;send_redirect( $loc )
       $c-&gt;send_redirect( $loc, $code )
       $c-&gt;send_redirect( $loc, $code, $entity_body )
           Send a redirect response back to the client.  The location ($loc) can
           be an absolute or relative URL. The $code must be one the redirect
           status codes, and defaults to &quot;301 Moved Permanently&quot;

       $c-&gt;send_error
       $c-&gt;send_error( $code )
       $c-&gt;send_error( $code, $error_message )
           Send an error response back to the client.  If the $code is missing a
           &quot;Bad Request&quot; error is reported.  The $error_message is a string that
           is incorporated in the body of the HTML entity body.

       $c-&gt;send_file_response( $filename )
           Send back a response with the specified $filename as content.  If the
           file is a directory we try to generate an HTML index of it.

       $c-&gt;send_file( $filename )
       $c-&gt;send_file( $fd )
           Copy the file to the client.  The file can be a string (which will be
           interpreted as a filename) or a reference to an &quot;IO::Handle&quot; or glob.

       $c-&gt;daemon
           Return a reference to the corresponding &quot;HTTP::Daemon&quot; object.

SEE ALSO
       RFC 2616

       IO::Socket::INET, IO::Socket

COPYRIGHT
       Copyright 1996-2003, Gisle Aas

       This library is free software; you can redistribute it and/or modify it
       under the same terms as Perl itself.



perl v5.14.2                       2012-02-18                  HTTP::Daemon(3pm)

</pre></body></html>
