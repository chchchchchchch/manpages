<html>
<head><meta charset=utf-8/>
<title>IO::Compress::Bzip2 - Write bzip2 files/buffers</title></head>
<body><pre>

IO::Compress::Bzip2(3pmUser Contributed Perl DocumentatiIO::Compress::Bzip2(3pm)



NAME
       IO::Compress::Bzip2 - Write bzip2 files/buffers

SYNOPSIS
           use IO::Compress::Bzip2 qw(bzip2 $Bzip2Error) ;

           my $status = bzip2 $input =&gt; $output [,OPTS]
               or die &quot;bzip2 failed: $Bzip2Error\n&quot;;

           my $z = new IO::Compress::Bzip2 $output [,OPTS]
               or die &quot;bzip2 failed: $Bzip2Error\n&quot;;

           $z-&gt;print($string);
           $z-&gt;printf($format, $string);
           $z-&gt;write($string);
           $z-&gt;syswrite($string [, $length, $offset]);
           $z-&gt;flush();
           $z-&gt;tell();
           $z-&gt;eof();
           $z-&gt;seek($position, $whence);
           $z-&gt;binmode();
           $z-&gt;fileno();
           $z-&gt;opened();
           $z-&gt;autoflush();
           $z-&gt;input_line_number();
           $z-&gt;newStream( [OPTS] );

           $z-&gt;close() ;

           $Bzip2Error ;

           # IO::File mode

           print $z $string;
           printf $z $format, $string;
           tell $z
           eof $z
           seek $z, $position, $whence
           binmode $z
           fileno $z
           close $z ;

DESCRIPTION
       This module provides a Perl interface that allows writing bzip2
       compressed data to files or buffer.

       For reading bzip2 files/buffers, see the companion module
       IO::Uncompress::Bunzip2.

Functional Interface
       A top-level function, &quot;bzip2&quot;, is provided to carry out &quot;one-shot&quot;
       compression between buffers and/or files. For finer control over the
       compression process, see the &quot;OO Interface&quot; section.

           use IO::Compress::Bzip2 qw(bzip2 $Bzip2Error) ;

           bzip2 $input =&gt; $output [,OPTS]
               or die &quot;bzip2 failed: $Bzip2Error\n&quot;;

       The functional interface needs Perl5.005 or better.

   bzip2 $input =&gt; $output [, OPTS]
       &quot;bzip2&quot; expects at least two parameters, $input and $output.
&#12;       The $input parameter

       The parameter, $input, is used to define the source of the uncompressed
       data.

       It can take one of the following forms:

       A filename
            If the $input parameter is a simple scalar, it is assumed to be a
            filename. This file will be opened for reading and the input data
            will be read from it.

       A filehandle
            If the $input parameter is a filehandle, the input data will be read
            from it.  The string &apos;-&apos; can be used as an alias for standard input.

       A scalar reference
            If $input is a scalar reference, the input data will be read from
            $$input.

       An array reference
            If $input is an array reference, each element in the array must be a
            filename.

            The input data will be read from each file in turn.

            The complete array will be walked to ensure that it only contains
            valid filenames before any data is compressed.

       An Input FileGlob string
            If $input is a string that is delimited by the characters &quot;&lt;&quot; and
            &quot;&gt;&quot; &quot;bzip2&quot; will assume that it is an input fileglob string. The
            input is the list of files that match the fileglob.

            If the fileglob does not match any files ...

            See File::GlobMapper for more details.

       If the $input parameter is any other type, &quot;undef&quot; will be returned.

       The $output parameter

       The parameter $output is used to control the destination of the
       compressed data. This parameter can take one of these forms.

       A filename
            If the $output parameter is a simple scalar, it is assumed to be a
            filename.  This file will be opened for writing and the compressed
            data will be written to it.

       A filehandle
            If the $output parameter is a filehandle, the compressed data will
            be written to it.  The string &apos;-&apos; can be used as an alias for
            standard output.

       A scalar reference
            If $output is a scalar reference, the compressed data will be stored
            in $$output.

       An Array Reference
            If $output is an array reference, the compressed data will be pushed
            onto the array.

       An Output FileGlob
            If $output is a string that is delimited by the characters &quot;&lt;&quot; and
            &quot;&gt;&quot; &quot;bzip2&quot; will assume that it is an output fileglob string. The
            output is the list of files that match the fileglob.
&#12;            When $output is an fileglob string, $input must also be a fileglob
            string. Anything else is an error.

       If the $output parameter is any other type, &quot;undef&quot; will be returned.

   Notes
       When $input maps to multiple files/buffers and $output is a single
       file/buffer the input files/buffers will be stored in $output as a
       concatenated series of compressed data streams.

   Optional Parameters
       Unless specified below, the optional parameters for &quot;bzip2&quot;, &quot;OPTS&quot;, are
       the same as those used with the OO interface defined in the &quot;Constructor
       Options&quot; section below.

       &quot;AutoClose =&gt; 0|1&quot;
            This option applies to any input or output data streams to &quot;bzip2&quot;
            that are filehandles.

            If &quot;AutoClose&quot; is specified, and the value is true, it will result
            in all input and/or output filehandles being closed once &quot;bzip2&quot; has
            completed.

            This parameter defaults to 0.

       &quot;BinModeIn =&gt; 0|1&quot;
            When reading from a file or filehandle, set &quot;binmode&quot; before
            reading.

            Defaults to 0.

       &quot;Append =&gt; 0|1&quot;
            TODO

   Examples
       To read the contents of the file &quot;file1.txt&quot; and write the compressed
       data to the file &quot;file1.txt.bz2&quot;.

           use strict ;
           use warnings ;
           use IO::Compress::Bzip2 qw(bzip2 $Bzip2Error) ;

           my $input = &quot;file1.txt&quot;;
           bzip2 $input =&gt; &quot;$input.bz2&quot;
               or die &quot;bzip2 failed: $Bzip2Error\n&quot;;

       To read from an existing Perl filehandle, $input, and write the
       compressed data to a buffer, $buffer.

           use strict ;
           use warnings ;
           use IO::Compress::Bzip2 qw(bzip2 $Bzip2Error) ;
           use IO::File ;

           my $input = new IO::File &quot;&lt;file1.txt&quot;
               or die &quot;Cannot open &apos;file1.txt&apos;: $!\n&quot; ;
           my $buffer ;
           bzip2 $input =&gt; \$buffer
               or die &quot;bzip2 failed: $Bzip2Error\n&quot;;

       To compress all files in the directory &quot;/my/home&quot; that match &quot;*.txt&quot; and
       store the compressed data in the same directory

           use strict ;
           use warnings ;
           use IO::Compress::Bzip2 qw(bzip2 $Bzip2Error) ;

           bzip2 &apos;&lt;/my/home/*.txt&gt;&apos; =&gt; &apos;&lt;*.bz2&gt;&apos;
               or die &quot;bzip2 failed: $Bzip2Error\n&quot;;

       and if you want to compress each file one at a time, this will do the
       trick

           use strict ;
           use warnings ;
           use IO::Compress::Bzip2 qw(bzip2 $Bzip2Error) ;

           for my $input ( glob &quot;/my/home/*.txt&quot; )
           {
               my $output = &quot;$input.bz2&quot; ;
               bzip2 $input =&gt; $output
                   or die &quot;Error compressing &apos;$input&apos;: $Bzip2Error\n&quot;;
           }

OO Interface
   Constructor
       The format of the constructor for &quot;IO::Compress::Bzip2&quot; is shown below

           my $z = new IO::Compress::Bzip2 $output [,OPTS]
               or die &quot;IO::Compress::Bzip2 failed: $Bzip2Error\n&quot;;

       It returns an &quot;IO::Compress::Bzip2&quot; object on success and undef on
       failure.  The variable $Bzip2Error will contain an error message on
       failure.

       If you are running Perl 5.005 or better the object, $z, returned from
       IO::Compress::Bzip2 can be used exactly like an IO::File filehandle.
       This means that all normal output file operations can be carried out with
       $z.  For example, to write to a compressed file/buffer you can use either
       of these forms

           $z-&gt;print(&quot;hello world\n&quot;);
           print $z &quot;hello world\n&quot;;

       The mandatory parameter $output is used to control the destination of the
       compressed data. This parameter can take one of these forms.

       A filename
            If the $output parameter is a simple scalar, it is assumed to be a
            filename. This file will be opened for writing and the compressed
            data will be written to it.

       A filehandle
            If the $output parameter is a filehandle, the compressed data will
            be written to it.  The string &apos;-&apos; can be used as an alias for
            standard output.

       A scalar reference
            If $output is a scalar reference, the compressed data will be stored
            in $$output.

       If the $output parameter is any other type, &quot;IO::Compress::Bzip2&quot;::new
       will return undef.

   Constructor Options
       &quot;OPTS&quot; is any combination of the following options:

       &quot;AutoClose =&gt; 0|1&quot;
            This option is only valid when the $output parameter is a
            filehandle. If specified, and the value is true, it will result in
            the $output being closed once either the &quot;close&quot; method is called or
            the &quot;IO::Compress::Bzip2&quot; object is destroyed.

            This parameter defaults to 0.

       &quot;Append =&gt; 0|1&quot;
            Opens $output in append mode.
&#12;            The behaviour of this option is dependent on the type of $output.

            &#183;    A Buffer

                 If $output is a buffer and &quot;Append&quot; is enabled, all compressed
                 data will be append to the end if $output. Otherwise $output
                 will be cleared before any data is written to it.

            &#183;    A Filename

                 If $output is a filename and &quot;Append&quot; is enabled, the file will
                 be opened in append mode. Otherwise the contents of the file,
                 if any, will be truncated before any compressed data is written
                 to it.

            &#183;    A Filehandle

                 If $output is a filehandle, the file pointer will be positioned
                 to the end of the file via a call to &quot;seek&quot; before any
                 compressed data is written to it.  Otherwise the file pointer
                 will not be moved.

            This parameter defaults to 0.

       &quot;BlockSize100K =&gt; number&quot;
            Specify the number of 100K blocks bzip2 uses during compression.

            Valid values are from 1 to 9, where 9 is best compression.

            The default is 1.

       &quot;WorkFactor =&gt; number&quot;
            Specifies how much effort bzip2 should take before resorting to a
            slower fallback compression algorithm.

            Valid values range from 0 to 250, where 0 means use the default
            value 30.

            The default is 0.

       &quot;Strict =&gt; 0|1&quot;
            This is a placeholder option.

   Examples
       TODO

Methods
   print
       Usage is

           $z-&gt;print($data)
           print $z $data

       Compresses and outputs the contents of the $data parameter. This has the
       same behaviour as the &quot;print&quot; built-in.

       Returns true if successful.

   printf
       Usage is

           $z-&gt;printf($format, $data)
           printf $z $format, $data

       Compresses and outputs the contents of the $data parameter.

       Returns true if successful.

   syswrite
       Usage is
&#12;           $z-&gt;syswrite $data
           $z-&gt;syswrite $data, $length
           $z-&gt;syswrite $data, $length, $offset

       Compresses and outputs the contents of the $data parameter.

       Returns the number of uncompressed bytes written, or &quot;undef&quot; if
       unsuccessful.

   write
       Usage is

           $z-&gt;write $data
           $z-&gt;write $data, $length
           $z-&gt;write $data, $length, $offset

       Compresses and outputs the contents of the $data parameter.

       Returns the number of uncompressed bytes written, or &quot;undef&quot; if
       unsuccessful.

   flush
       Usage is

           $z-&gt;flush;

       Flushes any pending compressed data to the output file/buffer.

       TODO

       Returns true on success.

   tell
       Usage is

           $z-&gt;tell()
           tell $z

       Returns the uncompressed file offset.

   eof
       Usage is

           $z-&gt;eof();
           eof($z);

       Returns true if the &quot;close&quot; method has been called.

   seek
           $z-&gt;seek($position, $whence);
           seek($z, $position, $whence);

       Provides a sub-set of the &quot;seek&quot; functionality, with the restriction that
       it is only legal to seek forward in the output file/buffer.  It is a
       fatal error to attempt to seek backward.

       Empty parts of the file/buffer will have NULL (0x00) bytes written to
       them.

       The $whence parameter takes one the usual values, namely SEEK_SET,
       SEEK_CUR or SEEK_END.

       Returns 1 on success, 0 on failure.

   binmode
       Usage is

           $z-&gt;binmode
           binmode $z ;

       This is a noop provided for completeness.

   opened
           $z-&gt;opened()

       Returns true if the object currently refers to a opened file/buffer.

   autoflush
           my $prev = $z-&gt;autoflush()
           my $prev = $z-&gt;autoflush(EXPR)

       If the $z object is associated with a file or a filehandle, this method
       returns the current autoflush setting for the underlying filehandle. If
       &quot;EXPR&quot; is present, and is non-zero, it will enable flushing after every
       write/print operation.

       If $z is associated with a buffer, this method has no effect and always
       returns &quot;undef&quot;.

       Note that the special variable $| cannot be used to set or retrieve the
       autoflush setting.

   input_line_number
           $z-&gt;input_line_number()
           $z-&gt;input_line_number(EXPR)

       This method always returns &quot;undef&quot; when compressing.

   fileno
           $z-&gt;fileno()
           fileno($z)

       If the $z object is associated with a file or a filehandle, &quot;fileno&quot; will
       return the underlying file descriptor. Once the &quot;close&quot; method is called
       &quot;fileno&quot; will return &quot;undef&quot;.

       If the $z object is is associated with a buffer, this method will return
       &quot;undef&quot;.

   close
           $z-&gt;close() ;
           close $z ;

       Flushes any pending compressed data and then closes the output
       file/buffer.

       For most versions of Perl this method will be automatically invoked if
       the IO::Compress::Bzip2 object is destroyed (either explicitly or by the
       variable with the reference to the object going out of scope). The
       exceptions are Perl versions 5.005 through 5.00504 and 5.8.0. In these
       cases, the &quot;close&quot; method will be called automatically, but not until
       global destruction of all live objects when the program is terminating.

       Therefore, if you want your scripts to be able to run on all versions of
       Perl, you should call &quot;close&quot; explicitly and not rely on automatic
       closing.

       Returns true on success, otherwise 0.

       If the &quot;AutoClose&quot; option has been enabled when the IO::Compress::Bzip2
       object was created, and the object is associated with a file, the
       underlying file will also be closed.

   newStream([OPTS])
       Usage is

           $z-&gt;newStream( [OPTS] )

       Closes the current compressed data stream and starts a new one.

       OPTS consists of any of the the options that are available when creating
       the $z object.

       See the &quot;Constructor Options&quot; section for more details.

Importing
       No symbolic constants are required by this IO::Compress::Bzip2 at
       present.

       :all Imports &quot;bzip2&quot; and $Bzip2Error.  Same as doing this

                use IO::Compress::Bzip2 qw(bzip2 $Bzip2Error) ;

EXAMPLES
   Apache::GZip Revisited
       See IO::Compress::Bzip2::FAQ

   Working with Net::FTP
       See IO::Compress::Bzip2::FAQ

SEE ALSO
       Compress::Zlib, IO::Compress::Gzip, IO::Uncompress::Gunzip,
       IO::Compress::Deflate, IO::Uncompress::Inflate, IO::Compress::RawDeflate,
       IO::Uncompress::RawInflate, IO::Uncompress::Bunzip2, IO::Compress::Lzop,
       IO::Uncompress::UnLzop, IO::Compress::Lzf, IO::Uncompress::UnLzf,
       IO::Uncompress::AnyInflate, IO::Uncompress::AnyUncompress

       Compress::Zlib::FAQ

       File::GlobMapper, Archive::Zip, Archive::Tar, IO::Zlib

       The primary site for the bzip2 program is http://www.bzip.org.

       See the module Compress::Bzip2

AUTHOR
       This module was written by Paul Marquess, pmqs@cpan.org.

MODIFICATION HISTORY
       See the Changes file.

COPYRIGHT AND LICENSE
       Copyright (c) 2005-2008 Paul Marquess. All rights reserved.

       This program is free software; you can redistribute it and/or modify it
       under the same terms as Perl itself.



perl v5.14.2                       2012-04-29           IO::Compress::Bzip2(3pm)

</pre></body></html>
