<html>
<head><meta charset=utf-8/>
<title>IO::Compress::Zip - Write zip files/buffers</title></head>
<body><pre>

IO::Compress::Zip(3pm) User Contributed Perl DocumentationIO::Compress::Zip(3pm)



NAME
       IO::Compress::Zip - Write zip files/buffers

SYNOPSIS
           use IO::Compress::Zip qw(zip $ZipError) ;

           my $status = zip $input =&gt; $output [,OPTS]
               or die &quot;zip failed: $ZipError\n&quot;;

           my $z = new IO::Compress::Zip $output [,OPTS]
               or die &quot;zip failed: $ZipError\n&quot;;

           $z-&gt;print($string);
           $z-&gt;printf($format, $string);
           $z-&gt;write($string);
           $z-&gt;syswrite($string [, $length, $offset]);
           $z-&gt;flush();
           $z-&gt;tell();
           $z-&gt;eof();
           $z-&gt;seek($position, $whence);
           $z-&gt;binmode();
           $z-&gt;fileno();
           $z-&gt;opened();
           $z-&gt;autoflush();
           $z-&gt;input_line_number();
           $z-&gt;newStream( [OPTS] );

           $z-&gt;deflateParams();

           $z-&gt;close() ;

           $ZipError ;

           # IO::File mode

           print $z $string;
           printf $z $format, $string;
           tell $z
           eof $z
           seek $z, $position, $whence
           binmode $z
           fileno $z
           close $z ;

DESCRIPTION
       This module provides a Perl interface that allows writing zip compressed
       data to files or buffer.

       The primary purpose of this module is to provide streaming write access
       to zip files and buffers. It is not a general-purpose file archiver. If
       that is what you want, check out &quot;Archive::Zip&quot;.

       At present three compression methods are supported by IO::Compress::Zip,
       namely Store (no compression at all), Deflate, Bzip2 and LZMA.

       Note that to create Bzip2 content, the module &quot;IO::Compress::Bzip2&quot; must
       be installed.

       Note that to create LZMA content, the module &quot;IO::Compress::Lzma&quot; must be
       installed.

       For reading zip files/buffers, see the companion module
       IO::Uncompress::Unzip.

Functional Interface
       A top-level function, &quot;zip&quot;, is provided to carry out &quot;one-shot&quot;
       compression between buffers and/or files. For finer control over the
       compression process, see the &quot;OO Interface&quot; section.

           use IO::Compress::Zip qw(zip $ZipError) ;

           zip $input =&gt; $output [,OPTS]
               or die &quot;zip failed: $ZipError\n&quot;;

       The functional interface needs Perl5.005 or better.

   zip $input =&gt; $output [, OPTS]
       &quot;zip&quot; expects at least two parameters, $input and $output.

       The $input parameter

       The parameter, $input, is used to define the source of the uncompressed
       data.

       It can take one of the following forms:

       A filename
            If the $input parameter is a simple scalar, it is assumed to be a
            filename. This file will be opened for reading and the input data
            will be read from it.

       A filehandle
            If the $input parameter is a filehandle, the input data will be read
            from it.  The string &apos;-&apos; can be used as an alias for standard input.

       A scalar reference
            If $input is a scalar reference, the input data will be read from
            $$input.

       An array reference
            If $input is an array reference, each element in the array must be a
            filename.

            The input data will be read from each file in turn.

            The complete array will be walked to ensure that it only contains
            valid filenames before any data is compressed.

       An Input FileGlob string
            If $input is a string that is delimited by the characters &quot;&lt;&quot; and
            &quot;&gt;&quot; &quot;zip&quot; will assume that it is an input fileglob string. The input
            is the list of files that match the fileglob.

            See File::GlobMapper for more details.

       If the $input parameter is any other type, &quot;undef&quot; will be returned.

       In addition, if $input is a simple filename, the default values for the
       &quot;Name&quot;, &quot;Time&quot;, &quot;TextFlag&quot;, &quot;ExtAttr&quot;, &quot;exUnixN&quot; and &quot;exTime&quot; options
       will be sourced from that file.

       If you do not want to use these defaults they can be overridden by
       explicitly setting the &quot;Name&quot;, &quot;Time&quot;, &quot;TextFlag&quot;, &quot;ExtAttr&quot;, &quot;exUnixN&quot;
       and &quot;exTime&quot; options or by setting the &quot;Minimal&quot; parameter.

       The $output parameter

       The parameter $output is used to control the destination of the
       compressed data. This parameter can take one of these forms.

       A filename
            If the $output parameter is a simple scalar, it is assumed to be a
            filename.  This file will be opened for writing and the compressed
            data will be written to it.

       A filehandle
            If the $output parameter is a filehandle, the compressed data will
            be written to it.  The string &apos;-&apos; can be used as an alias for
            standard output.

       A scalar reference
            If $output is a scalar reference, the compressed data will be stored
            in $$output.

       An Array Reference
            If $output is an array reference, the compressed data will be pushed
            onto the array.

       An Output FileGlob
            If $output is a string that is delimited by the characters &quot;&lt;&quot; and
            &quot;&gt;&quot; &quot;zip&quot; will assume that it is an output fileglob string. The
            output is the list of files that match the fileglob.

            When $output is an fileglob string, $input must also be a fileglob
            string. Anything else is an error.

            See File::GlobMapper for more details.

       If the $output parameter is any other type, &quot;undef&quot; will be returned.

   Notes
       When $input maps to multiple files/buffers and $output is a single
       file/buffer the input files/buffers will each be stored in $output as a
       distinct entry.

   Optional Parameters
       Unless specified below, the optional parameters for &quot;zip&quot;, &quot;OPTS&quot;, are
       the same as those used with the OO interface defined in the &quot;Constructor
       Options&quot; section below.

       &quot;AutoClose =&gt; 0|1&quot;
            This option applies to any input or output data streams to &quot;zip&quot;
            that are filehandles.

            If &quot;AutoClose&quot; is specified, and the value is true, it will result
            in all input and/or output filehandles being closed once &quot;zip&quot; has
            completed.

            This parameter defaults to 0.

       &quot;BinModeIn =&gt; 0|1&quot;
            When reading from a file or filehandle, set &quot;binmode&quot; before
            reading.

            Defaults to 0.

       &quot;Append =&gt; 0|1&quot;
            The behaviour of this option is dependent on the type of output data
            stream.

            &#183;    A Buffer

                 If &quot;Append&quot; is enabled, all compressed data will be append to
                 the end of the output buffer. Otherwise the output buffer will
                 be cleared before any compressed data is written to it.

            &#183;    A Filename

                 If &quot;Append&quot; is enabled, the file will be opened in append mode.
                 Otherwise the contents of the file, if any, will be truncated
                 before any compressed data is written to it.

            &#183;    A Filehandle

                 If &quot;Append&quot; is enabled, the filehandle will be positioned to
                 the end of the file via a call to &quot;seek&quot; before any compressed
                 data is written to it.  Otherwise the file pointer will not be
                 moved.

            When &quot;Append&quot; is specified, and set to true, it will append all
            compressed data to the output data stream.

            So when the output is a filehandle it will carry out a seek to the
            eof before writing any compressed data. If the output is a filename,
            it will be opened for appending. If the output is a buffer, all
            compressed data will be appended to the existing buffer.

            Conversely when &quot;Append&quot; is not specified, or it is present and is
            set to false, it will operate as follows.

            When the output is a filename, it will truncate the contents of the
            file before writing any compressed data. If the output is a
            filehandle its position will not be changed. If the output is a
            buffer, it will be wiped before any compressed data is output.

            Defaults to 0.

   Examples
       To read the contents of the file &quot;file1.txt&quot; and write the compressed
       data to the file &quot;file1.txt.zip&quot;.

           use strict ;
           use warnings ;
           use IO::Compress::Zip qw(zip $ZipError) ;

           my $input = &quot;file1.txt&quot;;
           zip $input =&gt; &quot;$input.zip&quot;
               or die &quot;zip failed: $ZipError\n&quot;;

       To read from an existing Perl filehandle, $input, and write the
       compressed data to a buffer, $buffer.

           use strict ;
           use warnings ;
           use IO::Compress::Zip qw(zip $ZipError) ;
           use IO::File ;

           my $input = new IO::File &quot;&lt;file1.txt&quot;
               or die &quot;Cannot open &apos;file1.txt&apos;: $!\n&quot; ;
           my $buffer ;
           zip $input =&gt; \$buffer
               or die &quot;zip failed: $ZipError\n&quot;;

       To create a zip file, &quot;output.zip&quot;, that contains the compressed contents
       of the files &quot;alpha.txt&quot; and &quot;beta.txt&quot;

           use strict ;
           use warnings ;
           use IO::Compress::Zip qw(zip $ZipError) ;
&#12;           zip [ &apos;alpha.txt&apos;, &apos;beta.txt&apos; ] =&gt; &apos;output.zip&apos;
               or die &quot;zip failed: $ZipError\n&quot;;

       Alternatively, rather than having to explicitly name each of the files
       that you want to compress, you could use a fileglob to select all the
       &quot;txt&quot; files in the current directory, as follows

           use strict ;
           use warnings ;
           use IO::Compress::Zip qw(zip $ZipError) ;

           my @files = &lt;*.txt&gt;;
           zip \@files =&gt; &apos;output.zip&apos;
               or die &quot;zip failed: $ZipError\n&quot;;

       or more succinctly

           zip [ &lt;*.txt&gt; ] =&gt; &apos;output.zip&apos;
               or die &quot;zip failed: $ZipError\n&quot;;

OO Interface
   Constructor
       The format of the constructor for &quot;IO::Compress::Zip&quot; is shown below

           my $z = new IO::Compress::Zip $output [,OPTS]
               or die &quot;IO::Compress::Zip failed: $ZipError\n&quot;;

       It returns an &quot;IO::Compress::Zip&quot; object on success and undef on failure.
       The variable $ZipError will contain an error message on failure.

       If you are running Perl 5.005 or better the object, $z, returned from
       IO::Compress::Zip can be used exactly like an IO::File filehandle.  This
       means that all normal output file operations can be carried out with $z.
       For example, to write to a compressed file/buffer you can use either of
       these forms

           $z-&gt;print(&quot;hello world\n&quot;);
           print $z &quot;hello world\n&quot;;

       The mandatory parameter $output is used to control the destination of the
       compressed data. This parameter can take one of these forms.

       A filename
            If the $output parameter is a simple scalar, it is assumed to be a
            filename. This file will be opened for writing and the compressed
            data will be written to it.

       A filehandle
            If the $output parameter is a filehandle, the compressed data will
            be written to it.  The string &apos;-&apos; can be used as an alias for
            standard output.

       A scalar reference
            If $output is a scalar reference, the compressed data will be stored
            in $$output.

       If the $output parameter is any other type, &quot;IO::Compress::Zip&quot;::new will
       return undef.

   Constructor Options
       &quot;OPTS&quot; is any combination of the following options:

       &quot;AutoClose =&gt; 0|1&quot;
            This option is only valid when the $output parameter is a
            filehandle. If specified, and the value is true, it will result in
            the $output being closed once either the &quot;close&quot; method is called or
            the &quot;IO::Compress::Zip&quot; object is destroyed.

            This parameter defaults to 0.

       &quot;Append =&gt; 0|1&quot;
            Opens $output in append mode.

            The behaviour of this option is dependent on the type of $output.

            &#183;    A Buffer

                 If $output is a buffer and &quot;Append&quot; is enabled, all compressed
                 data will be append to the end of $output. Otherwise $output
                 will be cleared before any data is written to it.

            &#183;    A Filename

                 If $output is a filename and &quot;Append&quot; is enabled, the file will
                 be opened in append mode. Otherwise the contents of the file,
                 if any, will be truncated before any compressed data is written
                 to it.

            &#183;    A Filehandle

                 If $output is a filehandle, the file pointer will be positioned
                 to the end of the file via a call to &quot;seek&quot; before any
                 compressed data is written to it.  Otherwise the file pointer
                 will not be moved.

            This parameter defaults to 0.

       &quot;Name =&gt; $string&quot;
            Stores the contents of $string in the zip filename header field.

            If &quot;Name&quot; is not specified and the $input parameter is a filename,
            the value of $input will be used for the zip filename header field.

            If &quot;Name&quot; is not specified and the $input parameter is not a
            filename, no zip filename field will be created.

            Note that both the &quot;CanonicalName&quot; and &quot;FilterName&quot; options can
            modify the value used for the zip filename header field.

       &quot;CanonicalName =&gt; 0|1&quot;
            This option controls whether the filename field in the zip header is
            normalized into Unix format before being written to the zip file.

            It is recommended that you enable this option unless you really need
            to create a non-standard Zip file.

            This is what APPNOTE.TXT has to say on what should be stored in the
            zip filename header field.

                The name of the file, with optional relative path.
                The path stored should not contain a drive or
                device letter, or a leading slash.  All slashes
                should be forward slashes &apos;/&apos; as opposed to
                backwards slashes &apos;\&apos; for compatibility with Amiga
                and UNIX file systems etc.

            This option defaults to false.

       &quot;FilterName =&gt; sub { ... }&quot;
            This option allow the filename field in the zip header to be
            modified before it is written to the zip file.
&#12;            This option takes a parameter that must be a reference to a sub.  On
            entry to the sub the $_ variable will contain the name to be
            filtered. If no filename is available $_ will contain an empty
            string.

            The value of $_ when the sub returns will be  stored in the filename
            header field.

            Note that if &quot;CanonicalName&quot; is enabled, a normalized filename will
            be passed to the sub.

            If you use &quot;FilterName&quot; to modify the filename, it is your
            responsibility to keep the filename in Unix format.

            Although this option can be used with the OO ointerface, it is of
            most use with the one-shot interface. For example, the code below
            shows how &quot;FilterName&quot; can be used to remove the path component from
            a series of filenames before they are stored in $zipfile.

                sub compressTxtFiles
                {
                    my $zipfile = shift ;
                    my $dir     = shift ;

                    zip [ &lt;$dir/*.txt&gt; ] =&gt; $zipfile,
                        FilterName =&gt; sub { s[^$dir/][] } ;
                }

       &quot;Time =&gt; $number&quot;
            Sets the last modified time field in the zip header to $number.

            This field defaults to the time the &quot;IO::Compress::Zip&quot; object was
            created if this option is not specified and the $input parameter is
            not a filename.

       &quot;ExtAttr =&gt; $attr&quot;
            This option controls the &quot;external file attributes&quot; field in the
            central header of the zip file. This is a 4 byte field.

            If you are running a Unix derivative this value defaults to

                0100644 &lt;&lt; 16

            This should allow read/write access to any files that are extracted
            from the zip file/buffer`.

            For all other systems it defaults to 0.

       &quot;exTime =&gt; [$atime, $mtime, $ctime]&quot;
            This option expects an array reference with exactly three elements:
            $atime, &quot;mtime&quot; and $ctime. These correspond to the last access
            time, last modification time and creation time respectively.

            It uses these values to set the extended timestamp field (ID is
            &quot;UT&quot;) in the local zip header using the three values, $atime,
            $mtime, $ctime. In addition it sets the extended timestamp field in
            the central zip header using $mtime.

            If any of the three values is &quot;undef&quot; that time value will not be
            used.  So, for example, to set only the $mtime you would use this

                exTime =&gt; [undef, $mtime, undef]

            If the &quot;Minimal&quot; option is set to true, this option will be ignored.

            By default no extended time field is created.

       &quot;exUnix2 =&gt; [$uid, $gid]&quot;
            This option expects an array reference with exactly two elements:
            $uid and $gid. These values correspond to the numeric User ID (UID)
            and Group ID (GID) of the owner of the files respectively.

            When the &quot;exUnix2&quot; option is present it will trigger the creation of
            a Unix2 extra field (ID is &quot;Ux&quot;) in the local zip header. This will
            be populated with $uid and $gid. An empty Unix2 extra field will
            also be created in the central zip header.

            Note - The UID &amp; GID are stored as 16-bit integers in the &quot;Ux&quot;
            field. Use &quot;exUnixN&quot; if your UID or GID are 32-bit.

            If the &quot;Minimal&quot; option is set to true, this option will be ignored.

            By default no Unix2 extra field is created.

       &quot;exUnixN =&gt; [$uid, $gid]&quot;
            This option expects an array reference with exactly two elements:
            $uid and $gid. These values correspond to the numeric User ID (UID)
            and Group ID (GID) of the owner of the files respectively.

            When the &quot;exUnixN&quot; option is present it will trigger the creation of
            a UnixN extra field (ID is &quot;ux&quot;) in bothe the local and central zip
            headers.  This will be populated with $uid and $gid.  The UID &amp; GID
            are stored as 32-bit integers.

            If the &quot;Minimal&quot; option is set to true, this option will be ignored.

            By default no UnixN extra field is created.

       &quot;Comment =&gt; $comment&quot;
            Stores the contents of $comment in the Central File Header of the
            zip file.

            By default, no comment field is written to the zip file.

       &quot;ZipComment =&gt; $comment&quot;
            Stores the contents of $comment in the End of Central Directory
            record of the zip file.

            By default, no comment field is written to the zip file.

       &quot;Method =&gt; $method&quot;
            Controls which compression method is used. At present four
            compression methods are supported, namely Store (no compression at
            all), Deflate, Bzip2 and Lzma.

            The symbols, ZIP_CM_STORE, ZIP_CM_DEFLATE, ZIP_CM_BZIP2 and
            ZIP_CM_LZMA are used to select the compression method.

            These constants are not imported by &quot;IO::Compress::Zip&quot; by default.

                use IO::Compress::Zip qw(:zip_method);
                use IO::Compress::Zip qw(:constants);
                use IO::Compress::Zip qw(:all);

            Note that to create Bzip2 content, the module &quot;IO::Compress::Bzip2&quot;
            must be installed. A fatal error will be thrown if you attempt to
            create Bzip2 content when &quot;IO::Compress::Bzip2&quot; is not available.

            Note that to create Lzma content, the module &quot;IO::Compress::Lzma&quot;
            must be installed. A fatal error will be thrown if you attempt to
            create Lzma content when &quot;IO::Compress::Lzma&quot; is not available.
&#12;            The default method is ZIP_CM_DEFLATE.

       &quot;Stream =&gt; 0|1&quot;
            This option controls whether the zip file/buffer output is created
            in streaming mode.

            Note that when outputting to a file with streaming mode disabled
            (&quot;Stream&quot; is 0), the output file must be seekable.

            The default is 1.

       &quot;Zip64 =&gt; 0|1&quot;
            Create a Zip64 zip file/buffer. This option is used if you want to
            store files larger than 4 Gig or store more than 64K files in a
            single zip archive..

            &quot;Zip64&quot; will be automatically set, as needed, if working with the
            one-shot interface when the input is either a filename or a scalar
            reference.

            If you intend to manipulate the Zip64 zip files created with this
            module using an external zip/unzip, make sure that it supports
            Zip64.

            In particular, if you are using Info-Zip you need to have zip
            version 3.x or better to update a Zip64 archive and unzip version
            6.x to read a zip64 archive.

            The default is 0.

       &quot;TextFlag =&gt; 0|1&quot;
            This parameter controls the setting of a bit in the zip central
            header. It is used to signal that the data stored in the zip
            file/buffer is probably text.

            In one-shot mode this flag will be set to true if the Perl &quot;-T&quot;
            operator thinks the file contains text.

            The default is 0.

       &quot;ExtraFieldLocal =&gt; $data&quot; =item &quot;ExtraFieldCentral =&gt; $data&quot;
            The &quot;ExtraFieldLocal&quot; option is used to store additional metadata in
            the local header for the zip file/buffer. The &quot;ExtraFieldCentral&quot;
            does the same for the matching central header.

            An extra field consists of zero or more subfields. Each subfield
            consists of a two byte header followed by the subfield data.

            The list of subfields can be supplied in any of the following
            formats

                ExtraFieldLocal =&gt; [$id1, $data1,
                                    $id2, $data2,
                                     ...
                                   ]

                ExtraFieldLocal =&gt; [ [$id1 =&gt; $data1],
                                     [$id2 =&gt; $data2],
                                     ...
                                   ]

                ExtraFieldLocal =&gt; { $id1 =&gt; $data1,
                                     $id2 =&gt; $data2,
                                     ...
                                   }
&#12;            Where $id1, $id2 are two byte subfield ID&apos;s.

            If you use the hash syntax, you have no control over the order in
            which the ExtraSubFields are stored, plus you cannot have SubFields
            with duplicate ID.

            Alternatively the list of subfields can by supplied as a scalar,
            thus

                ExtraField =&gt; $rawdata

            In this case &quot;IO::Compress::Zip&quot; will check that $rawdata consists
            of zero or more conformant sub-fields.

            The Extended Time field (ID &quot;UT&quot;), set using the &quot;exTime&quot; option,
            and the Unix2 extra field (ID &quot;Ux), set using the &quot;exUnix2&quot; option,
            are examples of extra fields.

            If the &quot;Minimal&quot; option is set to true, this option will be ignored.

            The maximum size of an extra field 65535 bytes.

       &quot;Minimal =&gt; 1|0&quot;
            If specified, this option will disable the creation of all extra
            fields in the zip local and central headers. So the &quot;exTime&quot;,
            &quot;exUnix2&quot;, &quot;exUnixN&quot;, &quot;ExtraFieldLocal&quot; and &quot;ExtraFieldCentral&quot;
            options will be ignored.

            This parameter defaults to 0.

       &quot;BlockSize100K =&gt; number&quot;
            Specify the number of 100K blocks bzip2 uses during compression.

            Valid values are from 1 to 9, where 9 is best compression.

            This option is only valid if the &quot;Method&quot; is ZIP_CM_BZIP2. It is
            ignored otherwise.

            The default is 1.

       &quot;WorkFactor =&gt; number&quot;
            Specifies how much effort bzip2 should take before resorting to a
            slower fallback compression algorithm.

            Valid values range from 0 to 250, where 0 means use the default
            value 30.

            This option is only valid if the &quot;Method&quot; is ZIP_CM_BZIP2. It is
            ignored otherwise.

            The default is 0.

       &quot;Preset =&gt; number&quot;
            Used to choose the LZMA compression preset.

            Valid values are 0-9 and &quot;LZMA_PRESET_DEFAULT&quot;.

            0 is the fastest compression with the lowest memory usage and the
            lowest compression.

            9 is the slowest compession with the highest memory usage but with
            the best compression.

            This option is only valid if the &quot;Method&quot; is ZIP_CM_LZMA. It is
            ignored otherwise.
&#12;            Defaults to &quot;LZMA_PRESET_DEFAULT&quot; (6).

       &quot;Extreme =&gt; 0|1&quot;
            Makes LZMA compression a lot slower, but a small compression gain.

            This option is only valid if the &quot;Method&quot; is ZIP_CM_LZMA. It is
            ignored otherwise.

            Defaults to 0.

       -Level
            Defines the compression level used by zlib. The value should either
            be a number between 0 and 9 (0 means no compression and 9 is maximum
            compression), or one of the symbolic constants defined below.

               Z_NO_COMPRESSION
               Z_BEST_SPEED
               Z_BEST_COMPRESSION
               Z_DEFAULT_COMPRESSION

            The default is Z_DEFAULT_COMPRESSION.

            Note, these constants are not imported by &quot;IO::Compress::Zip&quot; by
            default.

                use IO::Compress::Zip qw(:strategy);
                use IO::Compress::Zip qw(:constants);
                use IO::Compress::Zip qw(:all);

       -Strategy
            Defines the strategy used to tune the compression. Use one of the
            symbolic constants defined below.

               Z_FILTERED
               Z_HUFFMAN_ONLY
               Z_RLE
               Z_FIXED
               Z_DEFAULT_STRATEGY

            The default is Z_DEFAULT_STRATEGY.

       &quot;Strict =&gt; 0|1&quot;
            This is a placeholder option.

   Examples
       TODO

Methods
   print
       Usage is

           $z-&gt;print($data)
           print $z $data

       Compresses and outputs the contents of the $data parameter. This has the
       same behaviour as the &quot;print&quot; built-in.

       Returns true if successful.

   printf
       Usage is

           $z-&gt;printf($format, $data)
           printf $z $format, $data

       Compresses and outputs the contents of the $data parameter.

       Returns true if successful.

   syswrite
       Usage is

           $z-&gt;syswrite $data
           $z-&gt;syswrite $data, $length
           $z-&gt;syswrite $data, $length, $offset

       Compresses and outputs the contents of the $data parameter.

       Returns the number of uncompressed bytes written, or &quot;undef&quot; if
       unsuccessful.

   write
       Usage is

           $z-&gt;write $data
           $z-&gt;write $data, $length
           $z-&gt;write $data, $length, $offset

       Compresses and outputs the contents of the $data parameter.

       Returns the number of uncompressed bytes written, or &quot;undef&quot; if
       unsuccessful.

   flush
       Usage is

           $z-&gt;flush;
           $z-&gt;flush($flush_type);

       Flushes any pending compressed data to the output file/buffer.

       This method takes an optional parameter, $flush_type, that controls how
       the flushing will be carried out. By default the $flush_type used is
       &quot;Z_FINISH&quot;. Other valid values for $flush_type are &quot;Z_NO_FLUSH&quot;,
       &quot;Z_SYNC_FLUSH&quot;, &quot;Z_FULL_FLUSH&quot; and &quot;Z_BLOCK&quot;. It is strongly recommended
       that you only set the &quot;flush_type&quot; parameter if you fully understand the
       implications of what it does - overuse of &quot;flush&quot; can seriously degrade
       the level of compression achieved. See the &quot;zlib&quot; documentation for
       details.

       Returns true on success.

   tell
       Usage is

           $z-&gt;tell()
           tell $z

       Returns the uncompressed file offset.

   eof
       Usage is

           $z-&gt;eof();
           eof($z);

       Returns true if the &quot;close&quot; method has been called.

   seek
           $z-&gt;seek($position, $whence);
           seek($z, $position, $whence);
&#12;       Provides a sub-set of the &quot;seek&quot; functionality, with the restriction that
       it is only legal to seek forward in the output file/buffer.  It is a
       fatal error to attempt to seek backward.

       Empty parts of the file/buffer will have NULL (0x00) bytes written to
       them.

       The $whence parameter takes one the usual values, namely SEEK_SET,
       SEEK_CUR or SEEK_END.

       Returns 1 on success, 0 on failure.

   binmode
       Usage is

           $z-&gt;binmode
           binmode $z ;

       This is a noop provided for completeness.

   opened
           $z-&gt;opened()

       Returns true if the object currently refers to a opened file/buffer.

   autoflush
           my $prev = $z-&gt;autoflush()
           my $prev = $z-&gt;autoflush(EXPR)

       If the $z object is associated with a file or a filehandle, this method
       returns the current autoflush setting for the underlying filehandle. If
       &quot;EXPR&quot; is present, and is non-zero, it will enable flushing after every
       write/print operation.

       If $z is associated with a buffer, this method has no effect and always
       returns &quot;undef&quot;.

       Note that the special variable $| cannot be used to set or retrieve the
       autoflush setting.

   input_line_number
           $z-&gt;input_line_number()
           $z-&gt;input_line_number(EXPR)

       This method always returns &quot;undef&quot; when compressing.

   fileno
           $z-&gt;fileno()
           fileno($z)

       If the $z object is associated with a file or a filehandle, &quot;fileno&quot; will
       return the underlying file descriptor. Once the &quot;close&quot; method is called
       &quot;fileno&quot; will return &quot;undef&quot;.

       If the $z object is associated with a buffer, this method will return
       &quot;undef&quot;.

   close
           $z-&gt;close() ;
           close $z ;

       Flushes any pending compressed data and then closes the output
       file/buffer.

       For most versions of Perl this method will be automatically invoked if
       the IO::Compress::Zip object is destroyed (either explicitly or by the
       variable with the reference to the object going out of scope). The
       exceptions are Perl versions 5.005 through 5.00504 and 5.8.0. In these
       cases, the &quot;close&quot; method will be called automatically, but not until
       global destruction of all live objects when the program is terminating.

       Therefore, if you want your scripts to be able to run on all versions of
       Perl, you should call &quot;close&quot; explicitly and not rely on automatic
       closing.

       Returns true on success, otherwise 0.

       If the &quot;AutoClose&quot; option has been enabled when the IO::Compress::Zip
       object was created, and the object is associated with a file, the
       underlying file will also be closed.

   newStream([OPTS])
       Usage is

           $z-&gt;newStream( [OPTS] )

       Closes the current compressed data stream and starts a new one.

       OPTS consists of any of the the options that are available when creating
       the $z object.

       See the &quot;Constructor Options&quot; section for more details.

   deflateParams
       Usage is

           $z-&gt;deflateParams

       TODO

Importing
       A number of symbolic constants are required by some methods in
       &quot;IO::Compress::Zip&quot;. None are imported by default.

       :all Imports &quot;zip&quot;, $ZipError and all symbolic constants that can be used
            by &quot;IO::Compress::Zip&quot;. Same as doing this

                use IO::Compress::Zip qw(zip $ZipError :constants) ;

       :constants
            Import all symbolic constants. Same as doing this

                use IO::Compress::Zip qw(:flush :level :strategy :zip_method) ;

       :flush
            These symbolic constants are used by the &quot;flush&quot; method.

                Z_NO_FLUSH
                Z_PARTIAL_FLUSH
                Z_SYNC_FLUSH
                Z_FULL_FLUSH
                Z_FINISH
                Z_BLOCK

       :level
            These symbolic constants are used by the &quot;Level&quot; option in the
            constructor.

                Z_NO_COMPRESSION
                Z_BEST_SPEED
                Z_BEST_COMPRESSION
                Z_DEFAULT_COMPRESSION
&#12;       :strategy
            These symbolic constants are used by the &quot;Strategy&quot; option in the
            constructor.

                Z_FILTERED
                Z_HUFFMAN_ONLY
                Z_RLE
                Z_FIXED
                Z_DEFAULT_STRATEGY

       :zip_method
            These symbolic constants are used by the &quot;Method&quot; option in the
            constructor.

                ZIP_CM_STORE
                ZIP_CM_DEFLATE
                ZIP_CM_BZIP2

EXAMPLES
   Apache::GZip Revisited
       See IO::Compress::FAQ

   Working with Net::FTP
       See IO::Compress::FAQ

SEE ALSO
       Compress::Zlib, IO::Compress::Gzip, IO::Uncompress::Gunzip,
       IO::Compress::Deflate, IO::Uncompress::Inflate, IO::Compress::RawDeflate,
       IO::Uncompress::RawInflate, IO::Compress::Bzip2, IO::Uncompress::Bunzip2,
       IO::Compress::Lzma, IO::Uncompress::UnLzma, IO::Compress::Xz,
       IO::Uncompress::UnXz, IO::Compress::Lzop, IO::Uncompress::UnLzop,
       IO::Compress::Lzf, IO::Uncompress::UnLzf, IO::Uncompress::AnyInflate,
       IO::Uncompress::AnyUncompress

       IO::Compress::FAQ

       File::GlobMapper, Archive::Zip, Archive::Tar, IO::Zlib

       For RFC 1950, 1951 and 1952 see http://www.faqs.org/rfcs/rfc1950.html,
       http://www.faqs.org/rfcs/rfc1951.html and
       http://www.faqs.org/rfcs/rfc1952.html

       The zlib compression library was written by Jean-loup Gailly
       gzip@prep.ai.mit.edu and Mark Adler madler@alumni.caltech.edu.

       The primary site for the zlib compression library is http://www.zlib.org.

       The primary site for gzip is http://www.gzip.org.

AUTHOR
       This module was written by Paul Marquess, pmqs@cpan.org.

MODIFICATION HISTORY
       See the Changes file.

COPYRIGHT AND LICENSE
       Copyright (c) 2005-2012 Paul Marquess. All rights reserved.

       This program is free software; you can redistribute it and/or modify it
       under the same terms as Perl itself.



perl v5.14.2                       2012-04-29             IO::Compress::Zip(3pm)

</pre></body></html>
