<html>
<head><meta charset=utf-8/>
<title>IO::Uncompress::Gunzip - Read RFC 1952 files/buffers</title></head>
<body><pre>

IO::Uncompress::Gunzip(User Contributed Perl DocumentIO::Uncompress::Gunzip(3pm)



NAME
       IO::Uncompress::Gunzip - Read RFC 1952 files/buffers

SYNOPSIS
           use IO::Uncompress::Gunzip qw(gunzip $GunzipError) ;

           my $status = gunzip $input =&gt; $output [,OPTS]
               or die &quot;gunzip failed: $GunzipError\n&quot;;

           my $z = new IO::Uncompress::Gunzip $input [OPTS]
               or die &quot;gunzip failed: $GunzipError\n&quot;;

           $status = $z-&gt;read($buffer)
           $status = $z-&gt;read($buffer, $length)
           $status = $z-&gt;read($buffer, $length, $offset)
           $line = $z-&gt;getline()
           $char = $z-&gt;getc()
           $char = $z-&gt;ungetc()
           $char = $z-&gt;opened()

           $status = $z-&gt;inflateSync()

           $data = $z-&gt;trailingData()
           $status = $z-&gt;nextStream()
           $data = $z-&gt;getHeaderInfo()
           $z-&gt;tell()
           $z-&gt;seek($position, $whence)
           $z-&gt;binmode()
           $z-&gt;fileno()
           $z-&gt;eof()
           $z-&gt;close()

           $GunzipError ;

           # IO::File mode

           &lt;$z&gt;
           read($z, $buffer);
           read($z, $buffer, $length);
           read($z, $buffer, $length, $offset);
           tell($z)
           seek($z, $position, $whence)
           binmode($z)
           fileno($z)
           eof($z)
           close($z)

DESCRIPTION
       This module provides a Perl interface that allows the reading of
       files/buffers that conform to RFC 1952.

       For writing RFC 1952 files/buffers, see the companion module
       IO::Compress::Gzip.

Functional Interface
       A top-level function, &quot;gunzip&quot;, is provided to carry out &quot;one-shot&quot;
       uncompression between buffers and/or files. For finer control over the
       uncompression process, see the &quot;OO Interface&quot; section.

           use IO::Uncompress::Gunzip qw(gunzip $GunzipError) ;

           gunzip $input =&gt; $output [,OPTS]
               or die &quot;gunzip failed: $GunzipError\n&quot;;

       The functional interface needs Perl5.005 or better.

   gunzip $input =&gt; $output [, OPTS]
       &quot;gunzip&quot; expects at least two parameters, $input and $output.

       The $input parameter

       The parameter, $input, is used to define the source of the compressed
       data.

       It can take one of the following forms:

       A filename
            If the $input parameter is a simple scalar, it is assumed to be a
            filename. This file will be opened for reading and the input data
            will be read from it.

       A filehandle
            If the $input parameter is a filehandle, the input data will be read
            from it.  The string &apos;-&apos; can be used as an alias for standard input.

       A scalar reference
            If $input is a scalar reference, the input data will be read from
            $$input.

       An array reference
            If $input is an array reference, each element in the array must be a
            filename.

            The input data will be read from each file in turn.

            The complete array will be walked to ensure that it only contains
            valid filenames before any data is uncompressed.

       An Input FileGlob string
            If $input is a string that is delimited by the characters &quot;&lt;&quot; and
            &quot;&gt;&quot; &quot;gunzip&quot; will assume that it is an input fileglob string. The
            input is the list of files that match the fileglob.

            See File::GlobMapper for more details.

       If the $input parameter is any other type, &quot;undef&quot; will be returned.

       The $output parameter

       The parameter $output is used to control the destination of the
       uncompressed data. This parameter can take one of these forms.

       A filename
            If the $output parameter is a simple scalar, it is assumed to be a
            filename.  This file will be opened for writing and the uncompressed
            data will be written to it.

       A filehandle
            If the $output parameter is a filehandle, the uncompressed data will
            be written to it.  The string &apos;-&apos; can be used as an alias for
            standard output.

       A scalar reference
            If $output is a scalar reference, the uncompressed data will be
            stored in $$output.

       An Array Reference
            If $output is an array reference, the uncompressed data will be
            pushed onto the array.

       An Output FileGlob
            If $output is a string that is delimited by the characters &quot;&lt;&quot; and
            &quot;&gt;&quot; &quot;gunzip&quot; will assume that it is an output fileglob string. The
            output is the list of files that match the fileglob.

            When $output is an fileglob string, $input must also be a fileglob
            string. Anything else is an error.

            See File::GlobMapper for more details.

       If the $output parameter is any other type, &quot;undef&quot; will be returned.

   Notes
       When $input maps to multiple compressed files/buffers and $output is a
       single file/buffer, after uncompression $output will contain a
       concatenation of all the uncompressed data from each of the input
       files/buffers.

   Optional Parameters
       Unless specified below, the optional parameters for &quot;gunzip&quot;, &quot;OPTS&quot;, are
       the same as those used with the OO interface defined in the &quot;Constructor
       Options&quot; section below.

       &quot;AutoClose =&gt; 0|1&quot;
            This option applies to any input or output data streams to &quot;gunzip&quot;
            that are filehandles.

            If &quot;AutoClose&quot; is specified, and the value is true, it will result
            in all input and/or output filehandles being closed once &quot;gunzip&quot;
            has completed.

            This parameter defaults to 0.

       &quot;BinModeOut =&gt; 0|1&quot;
            When writing to a file or filehandle, set &quot;binmode&quot; before writing
            to the file.

            Defaults to 0.

       &quot;Append =&gt; 0|1&quot;
            The behaviour of this option is dependent on the type of output data
            stream.

            &#183;    A Buffer

                 If &quot;Append&quot; is enabled, all uncompressed data will be append to
                 the end of the output buffer. Otherwise the output buffer will
                 be cleared before any uncompressed data is written to it.

            &#183;    A Filename

                 If &quot;Append&quot; is enabled, the file will be opened in append mode.
                 Otherwise the contents of the file, if any, will be truncated
                 before any uncompressed data is written to it.

            &#183;    A Filehandle

                 If &quot;Append&quot; is enabled, the filehandle will be positioned to
                 the end of the file via a call to &quot;seek&quot; before any
                 uncompressed data is written to it.  Otherwise the file pointer
                 will not be moved.

            When &quot;Append&quot; is specified, and set to true, it will append all
            uncompressed data to the output data stream.

            So when the output is a filehandle it will carry out a seek to the
            eof before writing any uncompressed data. If the output is a
            filename, it will be opened for appending. If the output is a
            buffer, all uncompressed data will be appended to the existing
            buffer.

            Conversely when &quot;Append&quot; is not specified, or it is present and is
            set to false, it will operate as follows.

            When the output is a filename, it will truncate the contents of the
            file before writing any uncompressed data. If the output is a
            filehandle its position will not be changed. If the output is a
            buffer, it will be wiped before any uncompressed data is output.

            Defaults to 0.

       &quot;MultiStream =&gt; 0|1&quot;
            If the input file/buffer contains multiple compressed data streams,
            this option will uncompress the whole lot as a single data stream.

            Defaults to 0.

       &quot;TrailingData =&gt; $scalar&quot;
            Returns the data, if any, that is present immediately after the
            compressed data stream once uncompression is complete.

            This option can be used when there is useful information immediately
            following the compressed data stream, and you don&apos;t know the length
            of the compressed data stream.

            If the input is a buffer, &quot;trailingData&quot; will return everything from
            the end of the compressed data stream to the end of the buffer.

            If the input is a filehandle, &quot;trailingData&quot; will return the data
            that is left in the filehandle input buffer once the end of the
            compressed data stream has been reached. You can then use the
            filehandle to read the rest of the input file.

            Don&apos;t bother using &quot;trailingData&quot; if the input is a filename.

            If you know the length of the compressed data stream before you
            start uncompressing, you can avoid having to use &quot;trailingData&quot; by
            setting the &quot;InputLength&quot; option.

   Examples
       To read the contents of the file &quot;file1.txt.gz&quot; and write the
       uncompressed data to the file &quot;file1.txt&quot;.

           use strict ;
           use warnings ;
           use IO::Uncompress::Gunzip qw(gunzip $GunzipError) ;

           my $input = &quot;file1.txt.gz&quot;;
           my $output = &quot;file1.txt&quot;;
           gunzip $input =&gt; $output
               or die &quot;gunzip failed: $GunzipError\n&quot;;

       To read from an existing Perl filehandle, $input, and write the
       uncompressed data to a buffer, $buffer.

           use strict ;
           use warnings ;
           use IO::Uncompress::Gunzip qw(gunzip $GunzipError) ;
           use IO::File ;

           my $input = new IO::File &quot;&lt;file1.txt.gz&quot;
               or die &quot;Cannot open &apos;file1.txt.gz&apos;: $!\n&quot; ;
           my $buffer ;
           gunzip $input =&gt; \$buffer
               or die &quot;gunzip failed: $GunzipError\n&quot;;

       To uncompress all files in the directory &quot;/my/home&quot; that match &quot;*.txt.gz&quot;
       and store the compressed data in the same directory

           use strict ;
           use warnings ;
           use IO::Uncompress::Gunzip qw(gunzip $GunzipError) ;

           gunzip &apos;&lt;/my/home/*.txt.gz&gt;&apos; =&gt; &apos;&lt;/my/home/#1.txt&gt;&apos;
               or die &quot;gunzip failed: $GunzipError\n&quot;;

       and if you want to compress each file one at a time, this will do the
       trick

           use strict ;
           use warnings ;
           use IO::Uncompress::Gunzip qw(gunzip $GunzipError) ;

           for my $input ( glob &quot;/my/home/*.txt.gz&quot; )
           {
               my $output = $input;
               $output =~ s/.gz// ;
               gunzip $input =&gt; $output
                   or die &quot;Error compressing &apos;$input&apos;: $GunzipError\n&quot;;
           }

OO Interface
   Constructor
       The format of the constructor for IO::Uncompress::Gunzip is shown below

           my $z = new IO::Uncompress::Gunzip $input [OPTS]
               or die &quot;IO::Uncompress::Gunzip failed: $GunzipError\n&quot;;

       Returns an &quot;IO::Uncompress::Gunzip&quot; object on success and undef on
       failure.  The variable $GunzipError will contain an error message on
       failure.

       If you are running Perl 5.005 or better the object, $z, returned from
       IO::Uncompress::Gunzip can be used exactly like an IO::File filehandle.
       This means that all normal input file operations can be carried out with
       $z.  For example, to read a line from a compressed file/buffer you can
       use either of these forms

           $line = $z-&gt;getline();
           $line = &lt;$z&gt;;

       The mandatory parameter $input is used to determine the source of the
       compressed data. This parameter can take one of three forms.

       A filename
            If the $input parameter is a scalar, it is assumed to be a filename.
            This file will be opened for reading and the compressed data will be
            read from it.

       A filehandle
            If the $input parameter is a filehandle, the compressed data will be
            read from it.  The string &apos;-&apos; can be used as an alias for standard
            input.

       A scalar reference
            If $input is a scalar reference, the compressed data will be read
            from $$input.

   Constructor Options
       The option names defined below are case insensitive and can be optionally
       prefixed by a &apos;-&apos;.  So all of the following are valid

           -AutoClose
           -autoclose
           AUTOCLOSE
           autoclose

       OPTS is a combination of the following options:

       &quot;AutoClose =&gt; 0|1&quot;
            This option is only valid when the $input parameter is a filehandle.
            If specified, and the value is true, it will result in the file
            being closed once either the &quot;close&quot; method is called or the
            IO::Uncompress::Gunzip object is destroyed.

            This parameter defaults to 0.

       &quot;MultiStream =&gt; 0|1&quot;
            Allows multiple concatenated compressed streams to be treated as a
            single compressed stream. Decompression will stop once either the
            end of the file/buffer is reached, an error is encountered
            (premature eof, corrupt compressed data) or the end of a stream is
            not immediately followed by the start of another stream.

            This parameter defaults to 0.

       &quot;Prime =&gt; $string&quot;
            This option will uncompress the contents of $string before
            processing the input file/buffer.

            This option can be useful when the compressed data is embedded in
            another file/data structure and it is not possible to work out where
            the compressed data begins without having to read the first few
            bytes. If this is the case, the uncompression can be primed with
            these bytes using this option.

       &quot;Transparent =&gt; 0|1&quot;
            If this option is set and the input file/buffer is not compressed
            data, the module will allow reading of it anyway.

            In addition, if the input file/buffer does contain compressed data
            and there is non-compressed data immediately following it, setting
            this option will make this module treat the whole file/buffer as a
            single data stream.

            This option defaults to 1.

       &quot;BlockSize =&gt; $num&quot;
            When reading the compressed input data, IO::Uncompress::Gunzip will
            read it in blocks of $num bytes.

            This option defaults to 4096.

       &quot;InputLength =&gt; $size&quot;
            When present this option will limit the number of compressed bytes
            read from the input file/buffer to $size. This option can be used in
            the situation where there is useful data directly after the
            compressed data stream and you know beforehand the exact length of
            the compressed data stream.

            This option is mostly used when reading from a filehandle, in which
            case the file pointer will be left pointing to the first byte
            directly after the compressed data stream.

            This option defaults to off.

       &quot;Append =&gt; 0|1&quot;
            This option controls what the &quot;read&quot; method does with uncompressed
            data.

            If set to 1, all uncompressed data will be appended to the output
            parameter of the &quot;read&quot; method.

            If set to 0, the contents of the output parameter of the &quot;read&quot;
            method will be overwritten by the uncompressed data.

            Defaults to 0.

       &quot;Strict =&gt; 0|1&quot;
            This option controls whether the extra checks defined below are used
            when carrying out the decompression. When Strict is on, the extra
            tests are carried out, when Strict is off they are not.

            The default for this option is off.

            1.   If the FHCRC bit is set in the gzip FLG header byte, the CRC16
                 bytes in the header must match the crc16 value of the gzip
                 header actually read.

            2.   If the gzip header contains a name field (FNAME) it consists
                 solely of ISO 8859-1 characters.

            3.   If the gzip header contains a comment field (FCOMMENT) it
                 consists solely of ISO 8859-1 characters plus line-feed.

            4.   If the gzip FEXTRA header field is present it must conform to
                 the sub-field structure as defined in RFC 1952.

            5.   The CRC32 and ISIZE trailer fields must be present.

            6.   The value of the CRC32 field read must match the crc32 value of
                 the uncompressed data actually contained in the gzip file.

            7.   The value of the ISIZE fields read must match the length of the
                 uncompressed data actually read from the file.

       &quot;ParseExtra =&gt; 0|1&quot; If the gzip FEXTRA header field is present and this
       option is set, it will force the module to check that it conforms to the
       sub-field structure as defined in RFC 1952.
            If the &quot;Strict&quot; is on it will automatically enable this option.

            Defaults to 0.

   Examples
       TODO

Methods
   read
       Usage is

           $status = $z-&gt;read($buffer)

       Reads a block of compressed data (the size the the compressed block is
       determined by the &quot;Buffer&quot; option in the constructor), uncompresses it
       and writes any uncompressed data into $buffer. If the &quot;Append&quot; parameter
       is set in the constructor, the uncompressed data will be appended to the
       $buffer parameter. Otherwise $buffer will be overwritten.

       Returns the number of uncompressed bytes written to $buffer, zero if eof
       or a negative number on error.

   read
       Usage is

           $status = $z-&gt;read($buffer, $length)
           $status = $z-&gt;read($buffer, $length, $offset)
&#12;           $status = read($z, $buffer, $length)
           $status = read($z, $buffer, $length, $offset)

       Attempt to read $length bytes of uncompressed data into $buffer.

       The main difference between this form of the &quot;read&quot; method and the
       previous one, is that this one will attempt to return exactly $length
       bytes. The only circumstances that this function will not is if end-of-
       file or an IO error is encountered.

       Returns the number of uncompressed bytes written to $buffer, zero if eof
       or a negative number on error.

   getline
       Usage is

           $line = $z-&gt;getline()
           $line = &lt;$z&gt;

       Reads a single line.

       This method fully supports the use of of the variable $/ (or
       $INPUT_RECORD_SEPARATOR or $RS when &quot;English&quot; is in use) to determine
       what constitutes an end of line. Paragraph mode, record mode and file
       slurp mode are all supported.

   getc
       Usage is

           $char = $z-&gt;getc()

       Read a single character.

   ungetc
       Usage is

           $char = $z-&gt;ungetc($string)

   inflateSync
       Usage is

           $status = $z-&gt;inflateSync()

       TODO

   getHeaderInfo
       Usage is

           $hdr  = $z-&gt;getHeaderInfo();
           @hdrs = $z-&gt;getHeaderInfo();

       This method returns either a hash reference (in scalar context) or a list
       or hash references (in array context) that contains information about
       each of the header fields in the compressed data stream(s).

       Name The contents of the Name header field, if present. If no name is
            present, the value will be undef. Note this is different from a zero
            length name, which will return an empty string.

       Comment
            The contents of the Comment header field, if present. If no comment
            is present, the value will be undef. Note this is different from a
            zero length comment, which will return an empty string.

   tell
       Usage is

           $z-&gt;tell()
           tell $z
&#12;       Returns the uncompressed file offset.

   eof
       Usage is

           $z-&gt;eof();
           eof($z);

       Returns true if the end of the compressed input stream has been reached.

   seek
           $z-&gt;seek($position, $whence);
           seek($z, $position, $whence);

       Provides a sub-set of the &quot;seek&quot; functionality, with the restriction that
       it is only legal to seek forward in the input file/buffer.  It is a fatal
       error to attempt to seek backward.

       The $whence parameter takes one the usual values, namely SEEK_SET,
       SEEK_CUR or SEEK_END.

       Returns 1 on success, 0 on failure.

   binmode
       Usage is

           $z-&gt;binmode
           binmode $z ;

       This is a noop provided for completeness.

   opened
           $z-&gt;opened()

       Returns true if the object currently refers to a opened file/buffer.

   autoflush
           my $prev = $z-&gt;autoflush()
           my $prev = $z-&gt;autoflush(EXPR)

       If the $z object is associated with a file or a filehandle, this method
       returns the current autoflush setting for the underlying filehandle. If
       &quot;EXPR&quot; is present, and is non-zero, it will enable flushing after every
       write/print operation.

       If $z is associated with a buffer, this method has no effect and always
       returns &quot;undef&quot;.

       Note that the special variable $| cannot be used to set or retrieve the
       autoflush setting.

   input_line_number
           $z-&gt;input_line_number()
           $z-&gt;input_line_number(EXPR)

       Returns the current uncompressed line number. If &quot;EXPR&quot; is present it has
       the effect of setting the line number. Note that setting the line number
       does not change the current position within the file/buffer being read.

       The contents of $/ are used to to determine what constitutes a line
       terminator.

   fileno
           $z-&gt;fileno()
           fileno($z)

       If the $z object is associated with a file or a filehandle, &quot;fileno&quot; will
       return the underlying file descriptor. Once the &quot;close&quot; method is called
       &quot;fileno&quot; will return &quot;undef&quot;.
&#12;       If the $z object is associated with a buffer, this method will return
       &quot;undef&quot;.

   close
           $z-&gt;close() ;
           close $z ;

       Closes the output file/buffer.

       For most versions of Perl this method will be automatically invoked if
       the IO::Uncompress::Gunzip object is destroyed (either explicitly or by
       the variable with the reference to the object going out of scope). The
       exceptions are Perl versions 5.005 through 5.00504 and 5.8.0. In these
       cases, the &quot;close&quot; method will be called automatically, but not until
       global destruction of all live objects when the program is terminating.

       Therefore, if you want your scripts to be able to run on all versions of
       Perl, you should call &quot;close&quot; explicitly and not rely on automatic
       closing.

       Returns true on success, otherwise 0.

       If the &quot;AutoClose&quot; option has been enabled when the
       IO::Uncompress::Gunzip object was created, and the object is associated
       with a file, the underlying file will also be closed.

   nextStream
       Usage is

           my $status = $z-&gt;nextStream();

       Skips to the next compressed data stream in the input file/buffer. If a
       new compressed data stream is found, the eof marker will be cleared and
       $.  will be reset to 0.

       Returns 1 if a new stream was found, 0 if none was found, and -1 if an
       error was encountered.

   trailingData
       Usage is

           my $data = $z-&gt;trailingData();

       Returns the data, if any, that is present immediately after the
       compressed data stream once uncompression is complete. It only makes
       sense to call this method once the end of the compressed data stream has
       been encountered.

       This option can be used when there is useful information immediately
       following the compressed data stream, and you don&apos;t know the length of
       the compressed data stream.

       If the input is a buffer, &quot;trailingData&quot; will return everything from the
       end of the compressed data stream to the end of the buffer.

       If the input is a filehandle, &quot;trailingData&quot; will return the data that is
       left in the filehandle input buffer once the end of the compressed data
       stream has been reached. You can then use the filehandle to read the rest
       of the input file.

       Don&apos;t bother using &quot;trailingData&quot; if the input is a filename.

       If you know the length of the compressed data stream before you start
       uncompressing, you can avoid having to use &quot;trailingData&quot; by setting the
       &quot;InputLength&quot; option in the constructor.

Importing
       No symbolic constants are required by this IO::Uncompress::Gunzip at
       present.
&#12;       :all Imports &quot;gunzip&quot; and $GunzipError.  Same as doing this

                use IO::Uncompress::Gunzip qw(gunzip $GunzipError) ;

EXAMPLES
   Working with Net::FTP
       See IO::Compress::FAQ

SEE ALSO
       Compress::Zlib, IO::Compress::Gzip, IO::Compress::Deflate,
       IO::Uncompress::Inflate, IO::Compress::RawDeflate,
       IO::Uncompress::RawInflate, IO::Compress::Bzip2, IO::Uncompress::Bunzip2,
       IO::Compress::Lzma, IO::Uncompress::UnLzma, IO::Compress::Xz,
       IO::Uncompress::UnXz, IO::Compress::Lzop, IO::Uncompress::UnLzop,
       IO::Compress::Lzf, IO::Uncompress::UnLzf, IO::Uncompress::AnyInflate,
       IO::Uncompress::AnyUncompress

       IO::Compress::FAQ

       File::GlobMapper, Archive::Zip, Archive::Tar, IO::Zlib

       For RFC 1950, 1951 and 1952 see http://www.faqs.org/rfcs/rfc1950.html,
       http://www.faqs.org/rfcs/rfc1951.html and
       http://www.faqs.org/rfcs/rfc1952.html

       The zlib compression library was written by Jean-loup Gailly
       gzip@prep.ai.mit.edu and Mark Adler madler@alumni.caltech.edu.

       The primary site for the zlib compression library is http://www.zlib.org.

       The primary site for gzip is http://www.gzip.org.

AUTHOR
       This module was written by Paul Marquess, pmqs@cpan.org.

MODIFICATION HISTORY
       See the Changes file.

COPYRIGHT AND LICENSE
       Copyright (c) 2005-2012 Paul Marquess. All rights reserved.

       This program is free software; you can redistribute it and/or modify it
       under the same terms as Perl itself.



perl v5.14.2                       2012-04-29        IO::Uncompress::Gunzip(3pm)

</pre></body></html>
