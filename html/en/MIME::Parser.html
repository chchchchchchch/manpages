<html>
<head><meta charset=utf-8/>
<title>MIME::Parser - experimental class for parsing MIME streams</title></head>
<body><pre>

MIME::Parser(3pm)      User Contributed Perl Documentation     MIME::Parser(3pm)



NAME
       MIME::Parser - experimental class for parsing MIME streams

SYNOPSIS
       Before reading further, you should see MIME::Tools to make sure that you
       understand where this module fits into the grand scheme of things.  Go
       on, do it now.  I&apos;ll wait.

       Ready?  Ok...

   Basic usage examples
           ### Create a new parser object:
           my $parser = new MIME::Parser;

           ### Tell it where to put things:
           $parser-&gt;output_under(&quot;/tmp&quot;);

           ### Parse an input filehandle:
           $entity = $parser-&gt;parse(\*STDIN);

           ### Congratulations: you now have a (possibly multipart) MIME entity!
           $entity-&gt;dump_skeleton;          # for debugging

   Examples of input
           ### Parse from filehandles:
           $entity = $parser-&gt;parse(\*STDIN);
           $entity = $parser-&gt;parse(IO::File-&gt;new(&quot;some command|&quot;);

           ### Parse from any object that supports getline() and read():
           $entity = $parser-&gt;parse($myHandle);

           ### Parse an in-core MIME message:
           $entity = $parser-&gt;parse_data($message);

           ### Parse an MIME message in a file:
           $entity = $parser-&gt;parse_open(&quot;/some/file.msg&quot;);

           ### Parse an MIME message out of a pipeline:
           $entity = $parser-&gt;parse_open(&quot;gunzip - &lt; file.msg.gz |&quot;);

           ### Parse already-split input (as &quot;deliver&quot; would give it to you):
           $entity = $parser-&gt;parse_two(&quot;msg.head&quot;, &quot;msg.body&quot;);

   Examples of output control
           ### Keep parsed message bodies in core (default outputs to disk):
           $parser-&gt;output_to_core(1);

           ### Output each message body to a one-per-message directory:
           $parser-&gt;output_under(&quot;/tmp&quot;);

           ### Output each message body to the same directory:
           $parser-&gt;output_dir(&quot;/tmp&quot;);

           ### Change how nameless message-component files are named:
           $parser-&gt;output_prefix(&quot;msg&quot;);

           ### Put temporary files somewhere else
           $parser-&gt;tmp_dir(&quot;/var/tmp/mytmpdir&quot;);

   Examples of error recovery
           ### Normal mechanism:
           eval { $entity = $parser-&gt;parse(\*STDIN) };
           if ($@) {
               $results  = $parser-&gt;results;
               $decapitated = $parser-&gt;last_head;  ### get last top-level head
           }
&#12;           ### Ultra-tolerant mechanism:
           $parser-&gt;ignore_errors(1);
           $entity = eval { $parser-&gt;parse(\*STDIN) };
           $error = ($@ || $parser-&gt;last_error);

           ### Cleanup all files created by the parse:
           eval { $entity = $parser-&gt;parse(\*STDIN) };
           ...
           $parser-&gt;filer-&gt;purge;

   Examples of parser options
           ### Automatically attempt to RFC 2047-decode the MIME headers?
           $parser-&gt;decode_headers(1);             ### default is false

           ### Parse contained &quot;message/rfc822&quot; objects as nested MIME streams?
           $parser-&gt;extract_nested_messages(0);    ### default is true

           ### Look for uuencode in &quot;text&quot; messages, and extract it?
           $parser-&gt;extract_uuencode(1);           ### default is false

           ### Should we forgive normally-fatal errors?
           $parser-&gt;ignore_errors(0);              ### default is true

   Miscellaneous examples
           ### Convert a Mail::Internet object to a MIME::Entity:
           my $data = join(&apos;&apos;, (@{$mail-&gt;header}, &quot;\n&quot;, @{$mail-&gt;body}));
           $entity = $parser-&gt;parse_data(\$data);

DESCRIPTION
       You can inherit from this class to create your own subclasses that parse
       MIME streams into MIME::Entity objects.

PUBLIC INTERFACE
   Construction
       new ARGS...
           Class method.  Create a new parser object.  Once you do this, you can
           then set up various parameters before doing the actual parsing.  For
           example:

               my $parser = new MIME::Parser;
               $parser-&gt;output_dir(&quot;/tmp&quot;);
               $parser-&gt;output_prefix(&quot;msg1&quot;);
               my $entity = $parser-&gt;parse(\*STDIN);

           Any arguments are passed into &quot;init()&quot;.  Don&apos;t override this in your
           subclasses; override init() instead.

       init ARGS...
           Instance method.  Initiallize a new MIME::Parser object.  This is
           automatically sent to a new object; you may want to override it.  If
           you override this, be sure to invoke the inherited method.

       init_parse
           Instance method.  Invoked automatically whenever one of the top-level
           parse() methods is called, to reset the parser to a &quot;ready&quot; state.

   Altering how messages are parsed
       decode_headers [YESNO]
           Instance method.  Controls whether the parser will attempt to decode
           all the MIME headers (as per RFC 2047) the moment it sees them.  This
           is not advisable for two very important reasons:

           &#183;   It screws up the extraction of information from MIME fields.  If
               you fully decode the headers into bytes, you can inadvertently
               transform a parseable MIME header like this:

                   Content-type: text/plain; filename=&quot;=?ISO-8859-1?Q?Hi=22Ho?=&quot;

               into unparseable gobbledygook; in this case:

                   Content-type: text/plain; filename=&quot;Hi&quot;Ho&quot;
&#12;           &#183;   It is information-lossy.  An encoded string which contains both
               Latin-1 and Cyrillic characters will be turned into a binary
               mishmosh which simply can&apos;t be rendered.

           History.  This method was once the only out-of-the-box way to deal
           with attachments whose filenames had non-ASCII characters.  However,
           since MIME-tools 5.4xx this is no longer necessary.

           Parameters.  If YESNO is true, decoding is done.  However, you will
           get a warning unless you use one of the special &quot;true&quot; values:

              &quot;I_NEED_TO_FIX_THIS&quot;
                     Just shut up and do it.  Not recommended.
                     Provided only for those who need to keep old scripts functioning.

              &quot;I_KNOW_WHAT_I_AM_DOING&quot;
                     Just shut up and do it.  Not recommended.
                     Provided for those who REALLY know what they are doing.

           If YESNO is false (the default), no attempt at decoding will be done.
           With no argument, just returns the current setting.  Remember: you
           can always decode the headers after the parsing has completed (see
           MIME::Head::decode()), or decode the words on demand (see
           MIME::Words).

       extract_nested_messages OPTION
           Instance method.  Some MIME messages will contain a part of type
           &quot;message/rfc822&quot; ,&quot;message/partial&quot; or &quot;message/external-body&quot;:
           literally, the text of an embedded mail/news/whatever message.  This
           option controls whether (and how) we parse that embedded message.

           If the OPTION is false, we treat such a message just as if it were a
           &quot;text/plain&quot; document, without attempting to decode its contents.

           If the OPTION is true (the default), the body of the &quot;message/rfc822&quot;
           or &quot;message/partial&quot; part is parsed by this parser, creating an
           entity object.  What happens then is determined by the actual OPTION:

           NEST or 1
               The default setting.  The contained message becomes the sole
               &quot;part&quot; of the &quot;message/rfc822&quot; entity (as if the containing
               message were a special kind of &quot;multipart&quot; message).  You can
               recover the sub-entity by invoking the parts() method on the
               &quot;message/rfc822&quot; entity.

           REPLACE
               The contained message replaces the &quot;message/rfc822&quot; entity, as
               though the &quot;message/rfc822&quot; &quot;container&quot; never existed.

               Warning: notice that, with this option, all the header
               information in the &quot;message/rfc822&quot; header is lost.  This might
               seriously bother you if you&apos;re dealing with a top-level message,
               and you&apos;ve just lost the sender&apos;s address and the subject line.
               &quot;:-/&quot;.

           Thanks to Andreas Koenig for suggesting this method.

       extract_uuencode [YESNO]
           Instance method.  If set true, then whenever we are confronted with a
           message whose effective content-type is &quot;text/plain&quot; and whose
           encoding is 7bit/8bit/binary, we scan the encoded body to see if it
           contains uuencoded data (generally given away by a &quot;begin XXX&quot; line).

           If it does, we explode the uuencoded message into a multipart, where
           the text before the first &quot;begin XXX&quot; becomes the first part, and all
           &quot;begin...end&quot; sections following become the subsequent parts.  The
           filename (if given) is accessible through the normal means.

       ignore_errors [YESNO]
           Instance method.  Controls whether the parser will attempt to ignore
           normally-fatal errors, treating them as warnings and continuing with
           the parse.

           If YESNO is true (the default), many syntax errors are tolerated.  If
           YESNO is false, fatal errors throw exceptions.  With no argument,
           just returns the current setting.

       decode_bodies [YESNO]
           Instance method.  Controls whether the parser should decode entity
           bodies or not.  If this is set to a false value (default is true),
           all entity bodies will be kept as-is in the original content-transfer
           encoding.

           To prevent double encoding on the output side MIME::Body-&gt;is_encoded
           is set, which tells MIME::Body not to encode the data again, if
           encoded data was requested. This is in particular useful, when it&apos;s
           important that the content must not be modified, e.g. if you want to
           calculate OpenPGP signatures from it.

           WARNING: the semantics change significantly if you parse MIME
           messages with this option set, because MIME::Entity resp. MIME::Body
           *always* see encoded data now, while the default behaviour is working
           with *decoded* data (and encoding it only if you request it).  You
           need to decode the data yourself, if you want to have it decoded.

           So use this option only if you exactly know, what you&apos;re doing, and
           that you&apos;re sure, that you really need it.

   Parsing an input source
       parse_data DATA
           Instance method.  Parse a MIME message that&apos;s already in core.  This
           internally creates an &quot;in memory&quot; filehandle on a Perl scalar value
           using PerlIO

           You may supply the DATA in any of a number of ways...

           &#183;   A scalar which holds the message.  A reference to this scalar
               will be used internally.

           &#183;   A ref to a scalar which holds the message.  This reference will
               be used internally.

           &#183;   DEPRECATED

               A ref to an array of scalars.  The array is internally
               concatenated into a temporary string, and a reference to the new
               string is used internally.

               It is much more efficient to pass in a scalar reference, so
               please consider refactoring your code to use that interface
               instead.  If you absolutely MUST pass an array, you may be better
               off using IO::ScalarArray in the calling code to generate a
               filehandle, and passing that filehandle to parse()

           Returns the parsed MIME::Entity on success.

       parse INSTREAM
           Instance method.  Takes a MIME-stream and splits it into its
           component entities.

           The INSTREAM can be given as an IO::File, a globref filehandle (like
           &quot;\*STDIN&quot;), or as any blessed object conforming to the IO:: interface
           (which minimally implements getline() and read()).

           Returns the parsed MIME::Entity on success.  Throws exception on
           failure.  If the message contained too many parts (as set by
           max_parts), returns undef.

       parse_open EXPR
           Instance method.  Convenience front-end onto &quot;parse()&quot;.  Simply give
           this method any expression that may be sent as the second argument to
           open() to open a filehandle for reading.

           Returns the parsed MIME::Entity on success.  Throws exception on
           failure.

       parse_two HEADFILE, BODYFILE
           Instance method.  Convenience front-end onto &quot;parse_open()&quot;, intended
           for programs running under mail-handlers like deliver, which splits
           the incoming mail message into a header file and a body file.  Simply
           give this method the paths to the respective files.

           Warning: it is assumed that, once the files are cat&apos;ed together,
           there will be a blank line separating the head part and the body
           part.

           Warning: new implementation slurps files into line array for
           portability, instead of using &apos;cat&apos;.  May be an issue if your
           messages are large.

           Returns the parsed MIME::Entity on success.  Throws exception on
           failure.

   Specifying output destination
       Warning: in 5.212 and before, this was done by methods of MIME::Parser.
       However, since many users have requested fine-tuned control over how this
       is done, the logic has been split off from the parser into its own class,
       MIME::Parser::Filer Every MIME::Parser maintains an instance of a
       MIME::Parser::Filer subclass to manage disk output (see
       MIME::Parser::Filer for details.)

       The benefit to this is that the MIME::Parser code won&apos;t be confounded
       with a lot of garbage related to disk output.  The drawback is that the
       way you override the default behavior will change.

       For now, all the normal public-interface methods are still provided, but
       many are only stubs which create or delegate to the underlying
       MIME::Parser::Filer object.

       filer [FILER]
           Instance method.  Get/set the FILER object used to manage the output
           of files to disk.  This will be some subclass of MIME::Parser::Filer.

       output_dir DIRECTORY
           Instance method.  Causes messages to be filed directly into the given
           DIRECTORY.  It does this by setting the underlying filer() to a new
           instance of MIME::Parser::FileInto, and passing the arguments into
           that class&apos; new() method.

           Note: Since this method replaces the underlying filer, you must
           invoke it before doing changing any attributes of the filer, like the
           output prefix; otherwise those changes will be lost.

       output_under BASEDIR, OPTS...
           Instance method.  Causes messages to be filed directly into
           subdirectories of the given BASEDIR, one subdirectory per message.
           It does this by setting the underlying filer() to a new instance of
           MIME::Parser::FileUnder, and passing the arguments into that class&apos;
           new() method.

           Note: Since this method replaces the underlying filer, you must
           invoke it before doing changing any attributes of the filer, like the
           output prefix; otherwise those changes will be lost.

       output_path HEAD
           Instance method, DEPRECATED.  Given a MIME head for a file to be
           extracted, come up with a good output pathname for the extracted
           file.  Identical to the preferred form:

                $parser-&gt;filer-&gt;output_path(...args...);

           We just delegate this to the underlying filer() object.

       output_prefix [PREFIX]
           Instance method, DEPRECATED.  Get/set the short string that all
           filenames for extracted body-parts will begin with (assuming that
           there is no better &quot;recommended filename&quot;).  Identical to the
           preferred form:

                $parser-&gt;filer-&gt;output_prefix(...args...);

           We just delegate this to the underlying filer() object.

       evil_filename NAME
           Instance method, DEPRECATED.  Identical to the preferred form:

                $parser-&gt;filer-&gt;evil_filename(...args...);

           We just delegate this to the underlying filer() object.

       max_parts NUM
           Instance method.  Limits the number of MIME parts we will parse.

           Normally, instances of this class parse a message to the bitter end.
           Messages with many MIME parts can cause excessive memory consumption.
           If you invoke this method, parsing will abort with a die() if a
           message contains more than NUM parts.

           If NUM is set to -1 (the default), then no maximum limit is enforced.

           With no argument, returns the current setting as an integer

       output_to_core YESNO
           Instance method.  Normally, instances of this class output all their
           decoded body data to disk files (via MIME::Body::File).  However, you
           can change this behaviour by invoking this method before parsing:

           If YESNO is false (the default), then all body data goes to disk
           files.

           If YESNO is true, then all body data goes to in-core data structures
           This is a little risky (what if someone emails you an MPEG or a tar
           file, hmmm?) but people seem to want this bit of noose-shaped rope,
           so I&apos;m providing it.  Note that setting this attribute true does not
           mean that parser-internal temporary files are avoided!  Use
           tmp_to_core() for that.

           With no argument, returns the current setting as a boolean.

       tmp_recycling
           Instance method, DEPRECATED.

           This method is a no-op to preserve the pre-5.421 API.

           The tmp_recycling() feature was removed in 5.421 because it had never
           actually worked.  Please update your code to stop using it.

       tmp_to_core [YESNO]
           Instance method.  Should new_tmpfile() create real temp files, or use
           fake in-core ones?  Normally we allow the creation of temporary disk
           files, since this allows us to handle huge attachments even when core
           is limited.

           If YESNO is true, we implement new_tmpfile() via in-core handles.  If
           YESNO is false (the default), we use real tmpfiles.  With no
           argument, just returns the current setting.

       use_inner_files [YESNO]
           REMOVED.

           Instance method.

           MIME::Parser no longer supports IO::InnerFile, but this method is
           retained for backwards compatibility.  It does nothing.

           The original reasoning for IO::InnerFile was that inner files were
           faster than &quot;in-core&quot; temp files.  At the time, the &quot;in-core&quot;
           tempfile support was implemented with IO::Scalar from the IO-Stringy
           distribution, which used the tie() interface to wrap a scalar with
           the appropriate IO::Handle operations.  The penalty for this was
           fairly hefty, and IO::InnerFile actually was faster.

           Nowadays, MIME::Parser uses Perl&apos;s built in ability to open a
           filehandle on an in-memory scalar variable via PerlIO.  Benchmarking
           shows that IO::InnerFile is slightly slower than using in-memory
           temporary files, and is slightly faster than on-disk temporary files.
           Both measurements are within a few percent of each other.  Since
           there&apos;s no real benefit, and since the IO::InnerFile abuse was fairly
           hairy and evil (&quot;writes&quot; to it were faked by extending the size of
           the inner file with the assumption that the only data you&apos;d ever
           -&gt;print() to it would be the line from the &quot;outer&quot; file, for example)
           it&apos;s been removed.

   Specifying classes to be instantiated
       interface ROLE,[VALUE]
           Instance method.  During parsing, the parser normally creates
           instances of certain classes, like MIME::Entity.  However, you may
           want to create a parser subclass that uses your own experimental
           head, entity, etc. classes (for example, your &quot;head&quot; class may
           provide some additional MIME-field-oriented methods).

           If so, then this is the method that your subclass should invoke
           during init.  Use it like this:

               package MyParser;
               @ISA = qw(MIME::Parser);
               ...
               sub init {
                   my $self = shift;
                   $self-&gt;SUPER::init(@_);        ### do my parent&apos;s init
                   $self-&gt;interface(ENTITY_CLASS =&gt; &apos;MIME::MyEntity&apos;);
                   $self-&gt;interface(HEAD_CLASS   =&gt; &apos;MIME::MyHead&apos;);
                   $self;                         ### return
               }

           With no VALUE, returns the VALUE currently associated with that ROLE.

       new_body_for HEAD
           Instance method.  Based on the HEAD of a part we are parsing, return
           a new body object (any desirable subclass of MIME::Body) for
           receiving that part&apos;s data.

           If you set the &quot;output_to_core&quot; option to false before parsing (the
           default), then we call &quot;output_path()&quot; and create a new
           MIME::Body::File on that filename.

           If you set the &quot;output_to_core&quot; option to true before parsing, then
           you get a MIME::Body::InCore instead.

           If you want the parser to do something else entirely, you can
           override this method in a subclass.

   Temporary File Creation
       tmp_dir DIRECTORY
           Instance method.  Causes any temporary files created by this parser
           to be created in the given DIRECTORY.

           If called without arguments, returns current value.

           The default value is undef, which will cause new_tmpfile() to use the
           system default temporary directory.

       new_tmpfile
           Instance method.  Return an IO handle to be used to hold temporary
           data during a parse.

           The default uses MIME::Tools::tmpopen() to create a new temporary
           file, unless tmp_to_core() dictates otherwise, but you can override
           this.  You shouldn&apos;t need to.
&#12;           The location for temporary files can be changed on a per-parser basis
           with tmp_dir().

           If you do override this, make certain that the object you return is
           set for binmode(), and is able to handle the following methods:

               read(BUF, NBYTES)
               getline()
               getlines()
               print(@ARGS)
               flush()
               seek(0, 0)

           Fatal exception if the stream could not be established.

   Parse results and error recovery
       last_error
           Instance method.  Return the error (if any) that we ignored in the
           last parse.

       last_head
           Instance method.  Return the top-level MIME header of the last stream
           we attempted to parse.  This is useful for replying to people who
           sent us bad MIME messages.

               ### Parse an input stream:
               eval { $entity = $parser-&gt;parse(\*STDIN) };
               if (!$entity) {    ### parse failed!
                   my $decapitated = $parser-&gt;last_head;
                   ...
               }

       results
           Instance method.  Return an object containing lots of info from the
           last entity parsed.  This will be an instance of class
           MIME::Parser::Results.

OPTIMIZING YOUR PARSER
   Maximizing speed
       Optimum input mechanisms:

           parse()                    YES (if you give it a globref or a
                                           subclass of IO::File)
           parse_open()               YES
           parse_data()               NO  (see below)
           parse_two()                NO  (see below)

       Optimum settings:

           decode_headers()           *** (no real difference; 0 is slightly faster)
           extract_nested_messages()  0   (may be slightly faster, but in
                                           general you want it set to 1)
           output_to_core()           0   (will be MUCH faster)
           tmp_to_core()              0   (will be MUCH faster)

       Native I/O is much faster than object-oriented I/O.  It&apos;s much faster to
       use &lt;$foo&gt; than $foo-&gt;getline.  For backwards compatibility, this module
       must continue to use object-oriented I/O in most places, but if you use
       parse() with a &quot;real&quot; filehandle (string, globref, or subclass of
       IO::File) then MIME::Parser is able to perform some crucial
       optimizations.

       The parse_two() call is very inefficient.  Currently this is just a
       front-end onto parse_data().  If your OS supports it, you&apos;re far better
       off doing something like:

           $parser-&gt;parse_open(&quot;/bin/cat msg.head msg.body |&quot;);

   Minimizing memory
       Optimum input mechanisms:

           parse()                    YES
           parse_open()               YES
           parse_data()               NO  (in-core I/O will burn core)
           parse_two()                NO  (in-core I/O will burn core)
&#12;       Optimum settings:

           decode_headers()           *** (no real difference)
           extract_nested_messages()  *** (no real difference)
           output_to_core()           0   (will use MUCH less memory)
                                           tmp_to_core is 1)
           tmp_to_core()              0   (will use MUCH less memory)

   Maximizing tolerance of bad MIME
       Optimum input mechanisms:

           parse()                    *** (doesn&apos;t matter)
           parse_open()               *** (doesn&apos;t matter)
           parse_data()               *** (doesn&apos;t matter)
           parse_two()                *** (doesn&apos;t matter)

       Optimum settings:

           decode_headers()           0   (sidesteps problem of bad hdr encodings)
           extract_nested_messages()  0   (sidesteps problems of bad nested messages,
                                           but often you want it set to 1 anyway).
           output_to_core()           *** (doesn&apos;t matter)
           tmp_to_core()              *** (doesn&apos;t matter)

   Avoiding disk-based temporary files
       Optimum input mechanisms:

           parse()                    YES (if you give it a seekable handle)
           parse_open()               YES (becomes a seekable handle)
           parse_data()               NO  (unless you set tmp_to_core(1))
           parse_two()                NO  (unless you set tmp_to_core(1))

       Optimum settings:

           decode_headers()           *** (doesn&apos;t matter)
           extract_nested_messages()  *** (doesn&apos;t matter)
           output_to_core()           *** (doesn&apos;t matter)
           tmp_to_core()              1

       You can veto tmpfiles entirely.  You can set tmp_to_core() true: this
       will always use in-core I/O for the buffering (warning: this will slow
       down the parsing of messages with large attachments).

       Final resort.  You can always override new_tmpfile() in a subclass.

WARNINGS
       Multipart messages are always read line-by-line
           Multipart document parts are read line-by-line, so that the
           encapsulation boundaries may easily be detected.  However, bad MIME
           composition agents (for example, naive CGI scripts) might return
           multipart documents where the parts are, say, unencoded bitmap
           files... and, consequently, where such &quot;lines&quot; might be veeeeeeeeery
           long indeed.

           A better solution for this case would be to set up some form of state
           machine for input processing.  This will be left for future versions.

       Multipart parts read into temp files before decoding
           In my original implementation, the MIME::Decoder classes had to be
           aware of encapsulation boundaries in multipart MIME documents.  While
           this decode-while-parsing approach obviated the need for temporary
           files, it resulted in inflexible and complex decoder implementations.

           The revised implementation uses a temporary file (a la &quot;tmpfile()&quot;)
           during parsing to hold the encoded portion of the current MIME
           document or part.  This file is deleted automatically after the
           current part is decoded and the data is written to the &quot;body stream&quot;
           object; you&apos;ll never see it, and should never need to worry about it.

           Some folks have asked for the ability to bypass this temp-file
           mechanism, I suppose because they assume it would slow down their
           application.  I considered accomodating this wish, but the temp-file
           approach solves a lot of thorny problems in parsing, and it also
           protects against hidden bugs in user applications (what if you&apos;ve
           directed the encoded part into a scalar, and someone unexpectedly
           sends you a 6 MB tar file?).  Finally, I&apos;m just not conviced that the
           temp-file use adds significant overhead.

       Fuzzing of CRLF and newline on input
           RFC 2045 dictates that MIME streams have lines terminated by CRLF
           (&quot;\r\n&quot;).  However, it is extremely likely that folks will want to
           parse MIME streams where each line ends in the local newline
           character &quot;\n&quot; instead.

           An attempt has been made to allow the parser to handle both CRLF and
           newline-terminated input.

       Fuzzing of CRLF and newline on output
           The &quot;7bit&quot; and &quot;8bit&quot; decoders will decode both a &quot;\n&quot; and a &quot;\r\n&quot;
           end-of-line sequence into a &quot;\n&quot;.

           The &quot;binary&quot; decoder (default if no encoding specified) still outputs
           stuff verbatim... so a MIME message with CRLFs and no explicit
           encoding will be output as a text file that, on many systems, will
           have an annoying ^M at the end of each line... but this is as it
           should be.

       Inability to handle multipart boundaries that contain newlines
           First, let&apos;s get something straight: this is an evil, EVIL practice,
           and is incompatible with RFC 2046... hence, it&apos;s not valid MIME.

           If your mailer creates multipart boundary strings that contain
           newlines when they appear in the message body, give it two weeks
           notice and find another one.  If your mail robot receives MIME mail
           like this, regard it as syntactically incorrect MIME, which it is.

           Why do I say that?  Well, in RFC 2046, the syntax of a boundary is
           given quite clearly:

                 boundary := 0*69&lt;bchars&gt; bcharsnospace

                 bchars := bcharsnospace / &quot; &quot;

                 bcharsnospace :=    DIGIT / ALPHA / &quot;&apos;&quot; / &quot;(&quot; / &quot;)&quot; / &quot;+&quot; /&quot;_&quot;
                              / &quot;,&quot; / &quot;-&quot; / &quot;.&quot; / &quot;/&quot; / &quot;:&quot; / &quot;=&quot; / &quot;?&quot;

           All of which means that a valid boundary string cannot have newlines
           in it, and any newlines in such a string in the message header are
           expected to be solely the result of folding the string (i.e.,
           inserting to-be-removed newlines for readability and line-shortening
           only).

           Yet, there is at least one brain-damaged user agent out there that
           composes mail like this:

                 MIME-Version: 1.0
                 Content-type: multipart/mixed; boundary=&quot;----ABC-
                  123----&quot;
                 Subject: Hi... I&apos;m a dork!

                 This is a multipart MIME message (yeah, right...)

                 ----ABC-
                  123----

                 Hi there!

           We have got to discourage practices like this (and the recent file
           upload idiocy where binary files that are part of a multipart MIME
           message aren&apos;t base64-encoded) if we want MIME to stay relatively
           simple, and MIME parsers to be relatively robust.

           Thanks to Andreas Koenig for bringing a baaaaaaaaad user agent to my
           attention.

SEE ALSO
       MIME::Tools, MIME::Head, MIME::Body, MIME::Entity, MIME::Decoder

AUTHOR
       Eryq (eryq@zeegee.com), ZeeGee Software Inc (http://www.zeegee.com).
       David F. Skoll (dfs@roaringpenguin.com) http://www.roaringpenguin.com
&#12;       All rights reserved.  This program is free software; you can redistribute
       it and/or modify it under the same terms as Perl itself.



perl v5.14.2                       2012-06-08                  MIME::Parser(3pm)

</pre></body></html>
