<html>
<head><meta charset=utf-8/>
<title>Net::DNS::Update - Create a DNS update packet</title></head>
<body><pre>

Net::DNS::Update(3pm)  User Contributed Perl Documentation Net::DNS::Update(3pm)



NAME
       Net::DNS::Update - Create a DNS update packet

SYNOPSIS
       &quot;use Net::DNS::Update;&quot;

DESCRIPTION
       &quot;Net::DNS::Update&quot; is a subclass of &quot;Net::DNS::Packet&quot;, to be used for
       making DNS dynamic updates.  Programmers should refer to RFC 2136 for the
       semantics of dynamic updates.

       WARNING:  This code is still under development.  Please use with caution
       on production nameservers.

METHODS
   new
           $packet = Net::DNS::Update-&gt;new;
           $packet = Net::DNS::Update-&gt;new(&apos;example.com&apos;);
           $packet = Net::DNS::Update-&gt;new(&apos;example.com&apos;, &apos;HS&apos;);

       Returns a &quot;Net::DNS::Update&quot; object suitable for performing a DNS dynamic
       update.  Specifically, it creates a packet with the header opcode set to
       UPDATE and the zone record type to SOA (per RFC 2136, Section 2.3).

       Programs must use the &quot;push&quot; method to add RRs to the prerequisite,
       update, and additional sections before performing the update.

       Arguments are the zone name and the class.  If the zone is omitted, the
       default domain will be taken from the resolver configuration.  If the
       class is omitted, it defaults to IN.

       Future versions of &quot;Net::DNS&quot; may provide a simpler interface for making
       dynamic updates.

EXAMPLES
       The first example below shows a complete program; subsequent examples
       show only the creation of the update packet.

   Add a new host
        #!/usr/bin/perl -w

        use Net::DNS;
        use strict;

        # Create the update packet.
        my $update = Net::DNS::Update-&gt;new(&apos;example.com&apos;);

        # Prerequisite is that no A records exist for the name.
        $update-&gt;push(pre =&gt; nxrrset(&apos;foo.example.com. A&apos;));

        # Add two A records for the name.
        $update-&gt;push(update =&gt; rr_add(&apos;foo.example.com. 86400 A 192.168.1.2&apos;));
        $update-&gt;push(update =&gt; rr_add(&apos;foo.example.com. 86400 A 172.16.3.4&apos;));

        # Send the update to the zone&apos;s primary master.
        my $res = Net::DNS::Resolver-&gt;new;
        $res-&gt;nameservers(&apos;primary-master.example.com&apos;);

        my $reply = $res-&gt;send($update);

        # Did it work?
        if ($reply) {
            if ($reply-&gt;header-&gt;rcode eq &apos;NOERROR&apos;) {
                print &quot;Update succeeded\n&quot;;
            } else {
                print &apos;Update failed: &apos;, $reply-&gt;header-&gt;rcode, &quot;\n&quot;;
            }
        } else {
            print &apos;Update failed: &apos;, $res-&gt;errorstring, &quot;\n&quot;;
        }

   Add an MX record for a name that already exists
           my $update = Net::DNS::Update-&gt;new(&apos;example.com&apos;);
           $update-&gt;push(pre    =&gt; yxdomain(&apos;example.com&apos;));
           $update-&gt;push(update =&gt; rr_add(&apos;example.com MX 10 mailhost.example.com&apos;));

   Add a TXT record for a name that doesn&apos;t exist
           my $update = Net::DNS::Update-&gt;new(&apos;example.com&apos;);
           $update-&gt;push(pre    =&gt; nxdomain(&apos;info.example.com&apos;));
           $update-&gt;push(update =&gt; rr_add(&apos;info.example.com TXT &quot;yabba dabba doo&quot;&apos;));

   Delete all A records for a name
           my $update = Net::DNS::Update-&gt;new(&apos;example.com&apos;);
           $update-&gt;push(pre    =&gt; yxrrset(&apos;foo.example.com A&apos;));
           $update-&gt;push(update =&gt; rr_del(&apos;foo.example.com A&apos;));

   Delete all RRs for a name
           my $update = Net::DNS::Update-&gt;new(&apos;example.com&apos;);
           $update-&gt;push(pre    =&gt; yxdomain(&apos;byebye.example.com&apos;));
           $update-&gt;push(update =&gt; rr_del(&apos;byebye.example.com&apos;));

   Perform a signed update
           my $key_name = &apos;tsig-key&apos;;
           my $key      = &apos;awwLOtRfpGE+rRKF2+DEiw==&apos;;

           my $update = Net::DNS::Update-&gt;new(&apos;example.com&apos;);
           $update-&gt;push(update =&gt; rr_add(&apos;foo.example.com A 10.1.2.3&apos;));
           $update-&gt;push(update =&gt; rr_add(&apos;bar.example.com A 10.4.5.6&apos;));
           $update-&gt;sign_tsig($key_name, $key);

   Another way to perform a signed update
           my $key_name = &apos;tsig-key&apos;;
           my $key      = &apos;awwLOtRfpGE+rRKF2+DEiw==&apos;;

           my $update = Net::DNS::Update-&gt;new(&apos;example.com&apos;);
           $update-&gt;push(update     =&gt; rr_add(&apos;foo.example.com A 10.1.2.3&apos;));
           $update-&gt;push(update     =&gt; rr_add(&apos;bar.example.com A 10.4.5.6&apos;));
           $update-&gt;push(additional =&gt; Net::DNS::RR-&gt;new(&quot;$key_name TSIG $key&quot;));

   Perform a signed update with a customized TSIG record
           my $key_name = &apos;tsig-key&apos;;
           my $key      = &apos;awwLOtRfpGE+rRKF2+DEiw==&apos;;

           my $tsig = Net::DNS::RR-&gt;new(&quot;$key_name TSIG $key&quot;);
           $tsig-&gt;fudge(60);

           my $update = Net::DNS::Update-&gt;new(&apos;example.com&apos;);
           $update-&gt;push(update     =&gt; rr_add(&apos;foo.example.com A 10.1.2.3&apos;));
           $update-&gt;push(update     =&gt; rr_add(&apos;bar.example.com A 10.4.5.6&apos;));
           $update-&gt;push(additional =&gt; $tsig);

BUGS
       This code is still under development.  Please use with caution on
       production nameservers.

COPYRIGHT
       Copyright (c) 1997-2002 Michael Fuhr.
&#12;       Portions Copyright (c) 2002-2004 Chris Reinhardt.

       All rights reserved.  This program is free software; you may redistribute
       it and/or modify it under the same terms as Perl itself.

SEE ALSO
       perl(1), Net::DNS, Net::DNS::Resolver, Net::DNS::Header,
       Net::DNS::Packet, Net::DNS::Question, Net::DNS::RR, RFC 2136, RFC 2845



perl v5.14.2                       2009-12-30              Net::DNS::Update(3pm)

</pre></body></html>
