<html>
<head><meta charset=utf-8/>
<title>Switch - A switch statement for Perl</title></head>
<body><pre>

Switch(3pm)            User Contributed Perl Documentation           Switch(3pm)



NAME
       Switch - A switch statement for Perl

SYNOPSIS
           use Switch;

           switch ($val) {
               case 1          { print &quot;number 1&quot; }
               case &quot;a&quot;        { print &quot;string a&quot; }
               case [1..10,42] { print &quot;number in list&quot; }
               case (\@array)  { print &quot;number in list&quot; }
               case /\w+/      { print &quot;pattern&quot; }
               case qr/\w+/    { print &quot;pattern&quot; }
               case (\%hash)   { print &quot;entry in hash&quot; }
               case (\&amp;sub)    { print &quot;arg to subroutine&quot; }
               else            { print &quot;previous case not true&quot; }
           }

BACKGROUND
       [Skip ahead to &quot;DESCRIPTION&quot; if you don&apos;t care about the whys and
       wherefores of this control structure]

       In seeking to devise a &quot;Swiss Army&quot; case mechanism suitable for Perl, it
       is useful to generalize this notion of distributed conditional testing as
       far as possible. Specifically, the concept of &quot;matching&quot; between the
       switch value and the various case values need not be restricted to
       numeric (or string or referential) equality, as it is in other languages.
       Indeed, as Table 1 illustrates, Perl offers at least eighteen different
       ways in which two values could generate a match.

               Table 1: Matching a switch value ($s) with a case value ($c)

               Switch  Case    Type of Match Implied   Matching Code
               Value   Value
               ======  =====   =====================   =============

               number  same    numeric or referential  match if $s == $c;
               or ref          equality

               object  method  result of method call   match if $s-&gt;$c();
               ref     name                            match if defined $s-&gt;$c();
                       or ref

               other   other   string equality         match if $s eq $c;
               non-ref non-ref
               scalar  scalar

               string  regexp  pattern match           match if $s =~ /$c/;

               array   scalar  array entry existence   match if 0&lt;=$c &amp;&amp; $c&lt;@$s;
               ref             array entry definition  match if defined $s-&gt;[$c];
                               array entry truth       match if $s-&gt;[$c];

               array   array   array intersection      match if intersects(@$s, @$c);
               ref     ref     (apply this table to
                                all pairs of elements
                                $s-&gt;[$i] and
                                $c-&gt;[$j])

               array   regexp  array grep              match if grep /$c/, @$s;
               ref
&#12;               hash    scalar  hash entry existence    match if exists $s-&gt;{$c};
               ref             hash entry definition   match if defined $s-&gt;{$c};
                               hash entry truth        match if $s-&gt;{$c};

               hash    regexp  hash grep               match if grep /$c/, keys %$s;
               ref

               sub     scalar  return value defn       match if defined $s-&gt;($c);
               ref             return value truth      match if $s-&gt;($c);

               sub     array   return value defn       match if defined $s-&gt;(@$c);
               ref     ref     return value truth      match if $s-&gt;(@$c);

       In reality, Table 1 covers 31 alternatives, because only the equality and
       intersection tests are commutative; in all other cases, the roles of the
       $s and $c variables could be reversed to produce a different test. For
       example, instead of testing a single hash for the existence of a series
       of keys (&quot;match if exists $s-&gt;{$c}&quot;), one could test for the existence of
       a single key in a series of hashes (&quot;match if exists $c-&gt;{$s}&quot;).

DESCRIPTION
       The Switch.pm module implements a generalized case mechanism that covers
       most (but not all) of the numerous possible combinations of switch and
       case values described above.

       The module augments the standard Perl syntax with two new control
       statements: &quot;switch&quot; and &quot;case&quot;. The &quot;switch&quot; statement takes a single
       scalar argument of any type, specified in parentheses.  &quot;switch&quot; stores
       this value as the current switch value in a (localized) control variable.
       The value is followed by a block which may contain one or more Perl
       statements (including the &quot;case&quot; statement described below).  The block
       is unconditionally executed once the switch value has been cached.

       A &quot;case&quot; statement takes a single scalar argument (in mandatory
       parentheses if it&apos;s a variable; otherwise the parens are optional) and
       selects the appropriate type of matching between that argument and the
       current switch value. The type of matching used is determined by the
       respective types of the switch value and the &quot;case&quot; argument, as
       specified in Table 1. If the match is successful, the mandatory block
       associated with the &quot;case&quot; statement is executed.

       In most other respects, the &quot;case&quot; statement is semantically identical to
       an &quot;if&quot; statement. For example, it can be followed by an &quot;else&quot; clause,
       and can be used as a postfix statement qualifier.

       However, when a &quot;case&quot; block has been executed control is automatically
       transferred to the statement after the immediately enclosing &quot;switch&quot;
       block, rather than to the next statement within the block. In other
       words, the success of any &quot;case&quot; statement prevents other cases in the
       same scope from executing. But see &quot;Allowing fall-through&quot; below.

       Together these two new statements provide a fully generalized case
       mechanism:

               use Switch;

               # AND LATER...

               %special = ( woohoo =&gt; 1,  d&apos;oh =&gt; 1 );

               while (&lt;&gt;) {
                   chomp;
                   switch ($_) {
                       case (%special) { print &quot;homer\n&quot;; }      # if $special{$_}
                       case /[a-z]/i   { print &quot;alpha\n&quot;; }      # if $_ =~ /a-z/i
                       case [1..9]     { print &quot;small num\n&quot;; }  # if $_ in [1..9]
                       case { $_[0] &gt;= 10 } { print &quot;big num\n&quot;; } # if $_ &gt;= 10
                       print &quot;must be punctuation\n&quot; case /\W/;  # if $_ ~= /\W/
                   }
               }

       Note that &quot;switch&quot;es can be nested within &quot;case&quot; (or any other) blocks,
       and a series of &quot;case&quot; statements can try different types of matches --
       hash membership, pattern match, array intersection, simple equality, etc.
       -- against the same switch value.

       The use of intersection tests against an array reference is particularly
       useful for aggregating integral cases:

               sub classify_digit
               {
                       switch ($_[0]) { case 0            { return &apos;zero&apos; }
                                        case [2,4,6,8]    { return &apos;even&apos; }
                                        case [1,3,5,7,9]  { return &apos;odd&apos; }
                                        case /[A-F]/i     { return &apos;hex&apos; }
                                      }
               }

   Allowing fall-through
       Fall-though (trying another case after one has already succeeded) is
       usually a Bad Idea in a switch statement. However, this is Perl, not a
       police state, so there is a way to do it, if you must.

       If a &quot;case&quot; block executes an untargeted &quot;next&quot;, control is immediately
       transferred to the statement after the &quot;case&quot; statement (i.e. usually
       another case), rather than out of the surrounding &quot;switch&quot; block.

       For example:

               switch ($val) {
                       case 1      { handle_num_1(); next }    # and try next case...
                       case &quot;1&quot;    { handle_str_1(); next }    # and try next case...
                       case [0..9] { handle_num_any(); }       # and we&apos;re done
                       case /\d/   { handle_dig_any(); next }  # and try next case...
                       case /.*/   { handle_str_any(); next }  # and try next case...
               }

       If $val held the number 1, the above &quot;switch&quot; block would call the first
       three &quot;handle_...&quot; subroutines, jumping to the next case test each time
       it encountered a &quot;next&quot;. After the third &quot;case&quot; block was executed,
       control would jump to the end of the enclosing &quot;switch&quot; block.

       On the other hand, if $val held 10, then only the last two &quot;handle_...&quot;
       subroutines would be called.

       Note that this mechanism allows the notion of conditional fall-through.
       For example:

               switch ($val) {
                       case [0..9] { handle_num_any(); next if $val &lt; 7; }
                       case /\d/   { handle_dig_any(); }
               }

       If an untargeted &quot;last&quot; statement is executed in a case block, this
       immediately transfers control out of the enclosing &quot;switch&quot; block (in
       other words, there is an implicit &quot;last&quot; at the end of each normal &quot;case&quot;
       block). Thus the previous example could also have been written:

               switch ($val) {
                       case [0..9] { handle_num_any(); last if $val &gt;= 7; next; }
                       case /\d/   { handle_dig_any(); }
               }
&#12;   Automating fall-through
       In situations where case fall-through should be the norm, rather than an
       exception, an endless succession of terminal &quot;next&quot;s is tedious and ugly.
       Hence, it is possible to reverse the default behaviour by specifying the
       string &quot;fallthrough&quot; when importing the module. For example, the
       following code is equivalent to the first example in &quot;Allowing fall-
       through&quot;:

               use Switch &apos;fallthrough&apos;;

               switch ($val) {
                       case 1      { handle_num_1(); }
                       case &quot;1&quot;    { handle_str_1(); }
                       case [0..9] { handle_num_any(); last }
                       case /\d/   { handle_dig_any(); }
                       case /.*/   { handle_str_any(); }
               }

       Note the explicit use of a &quot;last&quot; to preserve the non-fall-through
       behaviour of the third case.

   Alternative syntax
       Perl 6 will provide a built-in switch statement with essentially the same
       semantics as those offered by Switch.pm, but with a different pair of
       keywords. In Perl 6 &quot;switch&quot; will be spelled &quot;given&quot;, and &quot;case&quot; will be
       pronounced &quot;when&quot;. In addition, the &quot;when&quot; statement will not require
       switch or case values to be parenthesized.

       This future syntax is also (largely) available via the Switch.pm module,
       by importing it with the argument &quot;Perl6&quot;.  For example:

               use Switch &apos;Perl6&apos;;

               given ($val) {
                       when 1       { handle_num_1(); }
                       when ($str1) { handle_str_1(); }
                       when [0..9]  { handle_num_any(); last }
                       when /\d/    { handle_dig_any(); }
                       when /.*/    { handle_str_any(); }
                       default      { handle anything else; }
               }

       Note that scalars still need to be parenthesized, since they would be
       ambiguous in Perl 5.

       Note too that you can mix and match both syntaxes by importing the module
       with:

               use Switch &apos;Perl5&apos;, &apos;Perl6&apos;;

   Higher-order Operations
       One situation in which &quot;switch&quot; and &quot;case&quot; do not provide a good
       substitute for a cascaded &quot;if&quot;, is where a switch value needs to be
       tested against a series of conditions. For example:

               sub beverage {
                   switch (shift) {
                       case { $_[0] &lt; 10 } { return &apos;milk&apos; }
                       case { $_[0] &lt; 20 } { return &apos;coke&apos; }
                       case { $_[0] &lt; 30 } { return &apos;beer&apos; }
                       case { $_[0] &lt; 40 } { return &apos;wine&apos; }
                       case { $_[0] &lt; 50 } { return &apos;malt&apos; }
                       case { $_[0] &lt; 60 } { return &apos;Moet&apos; }
                       else                { return &apos;milk&apos; }
                   }
               }
&#12;       (This is equivalent to writing &quot;case (sub { $_[0] &lt; 10 })&quot;, etc.; $_[0]
       is the argument to the anonymous subroutine.)

       The need to specify each condition as a subroutine block is tiresome. To
       overcome this, when importing Switch.pm, a special &quot;placeholder&quot;
       subroutine named &quot;__&quot; [sic] may also be imported. This subroutine
       converts (almost) any expression in which it appears to a reference to a
       higher-order function. That is, the expression:

               use Switch &apos;__&apos;;

               __ &lt; 2

       is equivalent to:

               sub { $_[0] &lt; 2 }

       With &quot;__&quot;, the previous ugly case statements can be rewritten:

               case  __ &lt; 10  { return &apos;milk&apos; }
               case  __ &lt; 20  { return &apos;coke&apos; }
               case  __ &lt; 30  { return &apos;beer&apos; }
               case  __ &lt; 40  { return &apos;wine&apos; }
               case  __ &lt; 50  { return &apos;malt&apos; }
               case  __ &lt; 60  { return &apos;Moet&apos; }
               else           { return &apos;milk&apos; }

       The &quot;__&quot; subroutine makes extensive use of operator overloading to
       perform its magic. All operations involving __ are overloaded to produce
       an anonymous subroutine that implements a lazy version of the original
       operation.

       The only problem is that operator overloading does not allow the boolean
       operators &quot;&amp;&amp;&quot; and &quot;||&quot; to be overloaded. So a case statement like this:

               case  0 &lt;= __ &amp;&amp; __ &lt; 10  { return &apos;digit&apos; }

       doesn&apos;t act as expected, because when it is executed, it constructs two
       higher order subroutines and then treats the two resulting references as
       arguments to &quot;&amp;&amp;&quot;:

               sub { 0 &lt;= $_[0] } &amp;&amp; sub { $_[0] &lt; 10 }

       This boolean expression is inevitably true, since both references are
       non-false. Fortunately, the overloaded &apos;bool&apos; operator catches this
       situation and flags it as an error.

DEPENDENCIES
       The module is implemented using Filter::Util::Call and Text::Balanced and
       requires both these modules to be installed.

AUTHOR
       Damian Conway (damian@conway.org). This module is now maintained by
       Rafael Garcia-Suarez (rgarciasuarez@gmail.com) and more generally by the
       Perl 5 Porters (perl5-porters@perl.org), as part of the Perl core.

BUGS
       There are undoubtedly serious bugs lurking somewhere in code this funky
       :-) Bug reports and other feedback are most welcome.

LIMITATIONS
       Due to the heuristic nature of Switch.pm&apos;s source parsing, the presence
       of regexes with embedded newlines that are specified with raw &quot;/.../&quot;
       delimiters and don&apos;t have a modifier &quot;//x&quot; are indistinguishable from
       code chunks beginning with the division operator &quot;/&quot;. As a workaround you
       must use &quot;m/.../&quot; or &quot;m?...?&quot; for such patterns. Also, the presence of
       regexes specified with raw &quot;?...?&quot; delimiters may cause mysterious
       errors. The workaround is to use &quot;m?...?&quot; instead.

       Due to the way source filters work in Perl, you can&apos;t use Switch inside
       an string &quot;eval&quot;.

       If your source file is longer then 1 million characters and you have a
       switch statement that crosses the 1 million (or 2 million, etc.)
       character boundary you will get mysterious errors. The workaround is to
       use smaller source files.

COPYRIGHT
           Copyright (c) 1997-2008, Damian Conway. All Rights Reserved.
           This module is free software. It may be used, redistributed
               and/or modified under the same terms as Perl itself.



perl v5.12.4                       2011-07-31                        Switch(3pm)

</pre></body></html>
