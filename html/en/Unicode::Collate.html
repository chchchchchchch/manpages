<html>
<head><meta charset=utf-8/>
<title>Unicode::Collate - Unicode Collation Algorithm</title></head>
<body><pre>

Collate(3pm)           User Contributed Perl Documentation          Collate(3pm)



NAME
       Unicode::Collate - Unicode Collation Algorithm

SYNOPSIS
         use Unicode::Collate;

         #construct
         $Collator = Unicode::Collate-&gt;new(%tailoring);

         #sort
         @sorted = $Collator-&gt;sort(@not_sorted);

         #compare
         $result = $Collator-&gt;cmp($a, $b); # returns 1, 0, or -1.

       Note: Strings in @not_sorted, $a and $b are interpreted according to
       Perl&apos;s Unicode support. See perlunicode, perluniintro, perlunitut,
       perlunifaq, utf8.  Otherwise you can use &quot;preprocess&quot; or should decode
       them before.

DESCRIPTION
       This module is an implementation of Unicode Technical Standard #10
       (a.k.a. UTS #10) - Unicode Collation Algorithm (a.k.a. UCA).

   Constructor and Tailoring
       The &quot;new&quot; method returns a collator object. If new() is called with no
       parameters, the collator should do the default collation.

          $Collator = Unicode::Collate-&gt;new(
             UCA_Version =&gt; $UCA_Version,
             alternate =&gt; $alternate, # alias for &apos;variable&apos;
             backwards =&gt; $levelNumber, # or \@levelNumbers
             entry =&gt; $element,
             hangul_terminator =&gt; $term_primary_weight,
             ignoreName =&gt; qr/$ignoreName/,
             ignoreChar =&gt; qr/$ignoreChar/,
             ignore_level2 =&gt; $bool,
             katakana_before_hiragana =&gt; $bool,
             level =&gt; $collationLevel,
             normalization  =&gt; $normalization_form,
             overrideCJK =&gt; \&amp;overrideCJK,
             overrideHangul =&gt; \&amp;overrideHangul,
             preprocess =&gt; \&amp;preprocess,
             rearrange =&gt; \@charList,
             rewrite =&gt; \&amp;rewrite,
             suppress =&gt; \@charList,
             table =&gt; $filename,
             undefName =&gt; qr/$undefName/,
             undefChar =&gt; qr/$undefChar/,
             upper_before_lower =&gt; $bool,
             variable =&gt; $variable,
          );

       UCA_Version
           If the revision (previously &quot;tracking version&quot;) number of UCA is
           given, behavior of that revision is emulated on collating.  If
           omitted, the return value of &quot;UCA_Version()&quot; is used.

           The following revisions are supported.  The default is 24.

                UCA       Unicode Standard         DUCET (@version)
              -------------------------------------------------------
                 8              3.1                3.0.1 (3.0.1d9)
                 9     3.1 with Corrigendum 3      3.1.1 (3.1.1)
                11              4.0                4.0.0 (4.0.0)
                14             4.1.0               4.1.0 (4.1.0)
                16              5.0                5.0.0 (5.0.0)
                18             5.1.0               5.1.0 (5.1.0)
                20             5.2.0               5.2.0 (5.2.0)
                22             6.0.0               6.0.0 (6.0.0)
                24             6.1.0               6.1.0 (6.1.0)
&#12;           * Noncharacters (e.g. U+FFFF) are not ignored, and can be overridden
           since &quot;UCA_Version&quot; 22.

           * Fully ignorable characters were ignored, and would not interrupt
           contractions with &quot;UCA_Version&quot; 9 and 11.

           * Treatment of ignorables after variables and some behaviors were
           changed at &quot;UCA_Version&quot; 9.

           * Characters regarded as CJK unified ideographs (cf. &quot;overrideCJK&quot;)
           depend on &quot;UCA_Version&quot;.

           * Many hangul jamo are assigned at &quot;UCA_Version&quot; 20, that will affect
           &quot;hangul_terminator&quot;.

       alternate
           -- see 3.2.2 Alternate Weighting, version 8 of UTS #10

           For backward compatibility, &quot;alternate&quot; (old name) can be used as an
           alias for &quot;variable&quot;.

       backwards
           -- see 3.1.2 French Accents, UTS #10.

                backwards =&gt; $levelNumber or \@levelNumbers

           Weights in reverse order; ex. level 2 (diacritic ordering) in French.
           If omitted (or $levelNumber is &quot;undef&quot; or &quot;\@levelNumbers&quot; is &quot;[]&quot;),
           forwards at all the levels.

       entry
           -- see 3.1 Linguistic Features; 3.2.1 File Format, UTS #10.

           If the same character (or a sequence of characters) exists in the
           collation element table through &quot;table&quot;, mapping to collation
           elements is overridden.  If it does not exist, the mapping is defined
           additionally.

               entry =&gt; &lt;&lt;&apos;ENTRY&apos;, # for DUCET v4.0.0 (allkeys-4.0.0.txt)
           0063 0068 ; [.0E6A.0020.0002.0063] # ch
           0043 0068 ; [.0E6A.0020.0007.0043] # Ch
           0043 0048 ; [.0E6A.0020.0008.0043] # CH
           006C 006C ; [.0F4C.0020.0002.006C] # ll
           004C 006C ; [.0F4C.0020.0007.004C] # Ll
           004C 004C ; [.0F4C.0020.0008.004C] # LL
           00F1      ; [.0F7B.0020.0002.00F1] # n-tilde
           006E 0303 ; [.0F7B.0020.0002.00F1] # n-tilde
           00D1      ; [.0F7B.0020.0008.00D1] # N-tilde
           004E 0303 ; [.0F7B.0020.0008.00D1] # N-tilde
           ENTRY

               entry =&gt; &lt;&lt;&apos;ENTRY&apos;, # for DUCET v4.0.0 (allkeys-4.0.0.txt)
           00E6 ; [.0E33.0020.0002.00E6][.0E8B.0020.0002.00E6] # ae ligature as &lt;a&gt;&lt;e&gt;
           00C6 ; [.0E33.0020.0008.00C6][.0E8B.0020.0008.00C6] # AE ligature as &lt;A&gt;&lt;E&gt;
           ENTRY

           NOTE: The code point in the UCA file format (before &apos;;&apos;) must be a
           Unicode code point (defined as hexadecimal), but not a native code
           point.  So 0063 must always denote &quot;U+0063&quot;, but not a character of
           &quot;\x63&quot;.

           Weighting may vary depending on collation element table.  So ensure
           the weights defined in &quot;entry&quot; will be consistent with those in the
           collation element table loaded via &quot;table&quot;.

           In DUCET v4.0.0, primary weight of &quot;C&quot; is 0E60 and that of &quot;D&quot; is
           &quot;0E6D&quot;. So setting primary weight of &quot;CH&quot; to &quot;0E6A&quot; (as a value
           between 0E60 and &quot;0E6D&quot;) makes ordering as &quot;C &lt; CH &lt; D&quot;.  Exactly
           speaking DUCET already has some characters between &quot;C&quot; and &quot;D&quot;:
           &quot;small capital C&quot; (&quot;U+1D04&quot;) with primary weight 0E64,
           &quot;c-hook/C-hook&quot; (&quot;U+0188/U+0187&quot;) with 0E65, and &quot;c-curl&quot; (&quot;U+0255&quot;)
           with 0E69.  Then primary weight &quot;0E6A&quot; for &quot;CH&quot; makes &quot;CH&quot; ordered
           between &quot;c-curl&quot; and &quot;D&quot;.

       hangul_terminator
           -- see 7.1.4 Trailing Weights, UTS #10.
&#12;           If a true value is given (non-zero but should be positive), it will
           be added as a terminator primary weight to the end of every standard
           Hangul syllable. Secondary and any higher weights for terminator are
           set to zero.  If the value is false or &quot;hangul_terminator&quot; key does
           not exist, insertion of terminator weights will not be performed.

           Boundaries of Hangul syllables are determined according to conjoining
           Jamo behavior in the Unicode Standard and HangulSyllableType.txt.

           Implementation Note: (1) For expansion mapping (Unicode character
           mapped to a sequence of collation elements), a terminator will not be
           added between collation elements, even if Hangul syllable boundary
           exists there.  Addition of terminator is restricted to the next
           position to the last collation element.

           (2) Non-conjoining Hangul letters (Compatibility Jamo, halfwidth
           Jamo, and enclosed letters) are not automatically terminated with a
           terminator primary weight.  These characters may need terminator
           included in a collation element table beforehand.

       ignoreChar
       ignoreName
           -- see 3.2.2 Variable Weighting, UTS #10.

           Makes the entry in the table completely ignorable; i.e. as if the
           weights were zero at all level.

           Through &quot;ignoreChar&quot;, any character matching &quot;qr/$ignoreChar/&quot; will
           be ignored. Through &quot;ignoreName&quot;, any character whose name (given in
           the &quot;table&quot; file as a comment) matches &quot;qr/$ignoreName/&quot; will be
           ignored.

           E.g. when &apos;a&apos; and &apos;e&apos; are ignorable, &apos;element&apos; is equal to &apos;lament&apos;
           (or &apos;lmnt&apos;).

       ignore_level2
           -- see 5.1 Parametric Tailoring, UTS #10.

           By default, case-sensitive comparison (that is level 3 difference)
           won&apos;t ignore accents (that is level 2 difference).

           If the parameter is made true, accents (and other primary ignorable
           characters) are ignored, even though cases are taken into account.

           NOTE: &quot;level&quot; should be 3 or greater.

       katakana_before_hiragana
           -- see 7.3.1 Tertiary Weight Table, UTS #10.

           By default, hiragana is before katakana.  If the parameter is made
           true, this is reversed.

           NOTE: This parameter simplemindedly assumes that any
           hiragana/katakana distinctions must occur in level 3, and their
           weights at level 3 must be same as those mentioned in 7.3.1, UTS #10.
           If you define your collation elements which violate this requirement,
           this parameter does not work validly.

       level
           -- see 4.3 Form Sort Key, UTS #10.

           Set the maximum level.  Any higher levels than the specified one are
           ignored.

             Level 1: alphabetic ordering
             Level 2: diacritic ordering
             Level 3: case ordering
             Level 4: tie-breaking (e.g. in the case when variable is &apos;shifted&apos;)

             ex.level =&gt; 2,

           If omitted, the maximum is the 4th.

       normalization
           -- see 4.1 Normalize, UTS #10.

           If specified, strings are normalized before preparation of sort keys
           (the normalization is executed after preprocess).

           A form name &quot;Unicode::Normalize::normalize()&quot; accepts will be applied
           as $normalization_form.  Acceptable names include &apos;NFD&apos;, &apos;NFC&apos;,
           &apos;NFKD&apos;, and &apos;NFKC&apos;.  See &quot;Unicode::Normalize::normalize()&quot; for
           detail.  If omitted, &apos;NFD&apos; is used.

           &quot;normalization&quot; is performed after &quot;preprocess&quot; (if defined).

           Furthermore, special values, &quot;undef&quot; and &quot;prenormalized&quot;, can be
           used, though they are not concerned with
           &quot;Unicode::Normalize::normalize()&quot;.

           If &quot;undef&quot; (not a string &quot;undef&quot;) is passed explicitly as the value
           for this key, any normalization is not carried out (this may make
           tailoring easier if any normalization is not desired). Under
           &quot;(normalization =&gt; undef)&quot;, only contiguous contractions are
           resolved; e.g. even if &quot;A-ring&quot; (and &quot;A-ring-cedilla&quot;) is ordered
           after &quot;Z&quot;, &quot;A-cedilla-ring&quot; would be primary equal to &quot;A&quot;.  In this
           point, &quot;(normalization =&gt; undef, preprocess =&gt; sub { NFD(shift) })&quot;
           is not equivalent to &quot;(normalization =&gt; &apos;NFD&apos;)&quot;.

           In the case of &quot;(normalization =&gt; &quot;prenormalized&quot;)&quot;, any
           normalization is not performed, but discontiguous contractions with
           combining characters are performed.  Therefore &quot;(normalization =&gt;
           &apos;prenormalized&apos;, preprocess =&gt; sub { NFD(shift) })&quot; is equivalent to
           &quot;(normalization =&gt; &apos;NFD&apos;)&quot;.  If source strings are finely
           prenormalized, &quot;(normalization =&gt; &apos;prenormalized&apos;)&quot; may save time for
           normalization.

           Except &quot;(normalization =&gt; undef)&quot;, Unicode::Normalize is required
           (see also CAVEAT).

       overrideCJK
           -- see 7.1 Derived Collation Elements, UTS #10.

           By default, CJK unified ideographs are ordered in Unicode codepoint
           order, but those in the CJK Unified Ideographs block are lesser than
           those in the CJK Unified Ideographs Extension A etc.

               In the CJK Unified Ideographs block:
               U+4E00..U+9FA5 if UCA_Version is 8, 9 or 11.
               U+4E00..U+9FBB if UCA_Version is 14 or 16.
               U+4E00..U+9FC3 if UCA_Version is 18.
               U+4E00..U+9FCB if UCA_Version is 20 or 22.
               U+4E00..U+9FCC if UCA_Version is 24.

               In the CJK Unified Ideographs Extension blocks:
               Ext.A (U+3400..U+4DB5) and Ext.B (U+20000..U+2A6D6) in any UCA_Version.
               Ext.C (U+2A700..U+2B734) if UCA_Version is 20 or greater.
               Ext.D (U+2B740..U+2B81D) if UCA_Version is 22 or greater.

           Through &quot;overrideCJK&quot;, ordering of CJK unified ideographs (including
           extensions) can be overridden.

           ex. CJK unified ideographs in the JIS code point order.

             overrideCJK =&gt; sub {
                 my $u = shift;             # get a Unicode codepoint
                 my $b = pack(&apos;n&apos;, $u);     # to UTF-16BE
                 my $s = your_unicode_to_sjis_converter($b); # convert
                 my $n = unpack(&apos;n&apos;, $s);   # convert sjis to short
                 [ $n, 0x20, 0x2, $u ];     # return the collation element
             },

           The return value may be an arrayref of 1st to 4th weights as shown
           above. The return value may be an integer as the primary weight as
           shown below.  If &quot;undef&quot; is returned, the default derived collation
           element will be used.

             overrideCJK =&gt; sub {
                 my $u = shift;             # get a Unicode codepoint
                 my $b = pack(&apos;n&apos;, $u);     # to UTF-16BE
                 my $s = your_unicode_to_sjis_converter($b); # convert
                 my $n = unpack(&apos;n&apos;, $s);   # convert sjis to short
                 return $n;                 # return the primary weight
             },
&#12;           The return value may be a list containing zero or more of an
           arrayref, an integer, or &quot;undef&quot;.

           ex. ignores all CJK unified ideographs.

             overrideCJK =&gt; sub {()}, # CODEREF returning empty list

              # where -&gt;eq(&quot;Pe\x{4E00}rl&quot;, &quot;Perl&quot;) is true
              # as U+4E00 is a CJK unified ideograph and to be ignorable.

           If &quot;undef&quot; is passed explicitly as the value for this key, weights
           for CJK unified ideographs are treated as undefined.  But assignment
           of weight for CJK unified ideographs in &quot;table&quot; or &quot;entry&quot; is still
           valid.

           Note: In addition to them, 12 CJK compatibility ideographs (&quot;U+FA0E&quot;,
           &quot;U+FA0F&quot;, &quot;U+FA11&quot;, &quot;U+FA13&quot;, &quot;U+FA14&quot;, &quot;U+FA1F&quot;, &quot;U+FA21&quot;, &quot;U+FA23&quot;,
           &quot;U+FA24&quot;, &quot;U+FA27&quot;, &quot;U+FA28&quot;, &quot;U+FA29&quot;) are also treated as CJK
           unified ideographs. But they can&apos;t be overridden via &quot;overrideCJK&quot;
           when you use DUCET, as the table includes weights for them. &quot;table&quot;
           or &quot;entry&quot; has priority over &quot;overrideCJK&quot;.

       overrideHangul
           -- see 7.1 Derived Collation Elements, UTS #10.

           By default, Hangul syllables are decomposed into Hangul Jamo, even if
           &quot;(normalization =&gt; undef)&quot;.  But the mapping of Hangul syllables may
           be overridden.

           This parameter works like &quot;overrideCJK&quot;, so see there for examples.

           If you want to override the mapping of Hangul syllables, NFD and NFKD
           are not appropriate, since NFD and NFKD will decompose Hangul
           syllables before overriding. FCD may decompose Hangul syllables as
           the case may be.

           If &quot;undef&quot; is passed explicitly as the value for this key, weight for
           Hangul syllables is treated as undefined without decomposition into
           Hangul Jamo.  But definition of weight for Hangul syllables in
           &quot;table&quot; or &quot;entry&quot; is still valid.

       preprocess
           -- see 5.1 Preprocessing, UTS #10.

           If specified, the coderef is used to preprocess each string before
           the formation of sort keys.

           ex. dropping English articles, such as &quot;a&quot; or &quot;the&quot;.  Then, &quot;the pen&quot;
           is before &quot;a pencil&quot;.

                preprocess =&gt; sub {
                      my $str = shift;
                      $str =~ s/\b(?:an?|the)\s+//gi;
                      return $str;
                   },

           &quot;preprocess&quot; is performed before &quot;normalization&quot; (if defined).

           ex. decoding strings in a legacy encoding such as shift-jis:

               $sjis_collator = Unicode::Collate-&gt;new(
                   preprocess =&gt; \&amp;your_shiftjis_to_unicode_decoder,
               );
               @result = $sjis_collator-&gt;sort(@shiftjis_strings);

           Note: Strings returned from the coderef will be interpreted according
           to Perl&apos;s Unicode support. See perlunicode, perluniintro, perlunitut,
           perlunifaq, utf8.

       rearrange
           -- see 3.1.3 Rearrangement, UTS #10.

           Characters that are not coded in logical order and to be rearranged.
           If &quot;UCA_Version&quot; is equal to or lesser than 11, default is:

               rearrange =&gt; [ 0x0E40..0x0E44, 0x0EC0..0x0EC4 ],

           If you want to disallow any rearrangement, pass &quot;undef&quot; or &quot;[]&quot; (a
           reference to empty list) as the value for this key.

           If &quot;UCA_Version&quot; is equal to or greater than 14, default is &quot;[]&quot;
           (i.e. no rearrangement).

           According to the version 9 of UCA, this parameter shall not be used;
           but it is not warned at present.

       rewrite
           If specified, the coderef is used to rewrite lines in &quot;table&quot; or
           &quot;entry&quot;.  The coderef will get each line, and then should return a
           rewritten line according to the UCA file format.  If the coderef
           returns an empty line, the line will be skipped.

           e.g. any primary ignorable characters into tertiary ignorable:

               rewrite =&gt; sub {
                   my $line = shift;
                   $line =~ s/\[\.0000\..{4}\..{4}\./[.0000.0000.0000./g;
                   return $line;
               },

           This example shows rewriting weights. &quot;rewrite&quot; is allowed to affect
           code points, weights, and the name.

           NOTE: &quot;table&quot; is available to use another table file; preparing a
           modified table once would be more efficient than rewriting lines on
           reading an unmodified table every time.

       suppress
           -- see suppress contractions in 5.14.11 Special-Purpose Commands, UTS
           #35 (LDML).

           Contractions beginning with the specified characters are suppressed,
           even if those contractions are defined in &quot;table&quot;.

           An example for Russian and some languages using the Cyrillic script:

               suppress =&gt; [0x0400..0x0417, 0x041A..0x0437, 0x043A..0x045F],

           where 0x0400 stands for &quot;U+0400&quot;, CYRILLIC CAPITAL LETTER IE WITH
           GRAVE.

           NOTE: Contractions via &quot;entry&quot; are not be suppressed.

       table
           -- see 3.2 Default Unicode Collation Element Table, UTS #10.

           You can use another collation element table if desired.

           The table file should locate in the Unicode/Collate directory on
           @INC. Say, if the filename is Foo.txt, the table file is searched as
           Unicode/Collate/Foo.txt in @INC.

           By default, allkeys.txt (as the filename of DUCET) is used.  If you
           will prepare your own table file, any name other than allkeys.txt may
           be better to avoid namespace conflict.

           NOTE: When XSUB is used, the DUCET is compiled on building this
           module, and it may save time at the run time.  Explicit saying &quot;table
           =&gt; &apos;allkeys.txt&apos;&quot; (or using another table), or using &quot;ignoreChar&quot;,
           &quot;ignoreName&quot;, &quot;undefChar&quot;, &quot;undefName&quot; or &quot;rewrite&quot; will prevent this
           module from using the compiled DUCET.

           If &quot;undef&quot; is passed explicitly as the value for this key, no file is
           read (but you can define collation elements via &quot;entry&quot;).

           A typical way to define a collation element table without any file of
           table:

              $onlyABC = Unicode::Collate-&gt;new(
                  table =&gt; undef,
                  entry =&gt; &lt;&lt; &apos;ENTRIES&apos;,
           0061 ; [.0101.0020.0002.0061] # LATIN SMALL LETTER A
           0041 ; [.0101.0020.0008.0041] # LATIN CAPITAL LETTER A
           0062 ; [.0102.0020.0002.0062] # LATIN SMALL LETTER B
           0042 ; [.0102.0020.0008.0042] # LATIN CAPITAL LETTER B
           0063 ; [.0103.0020.0002.0063] # LATIN SMALL LETTER C
           0043 ; [.0103.0020.0008.0043] # LATIN CAPITAL LETTER C
           ENTRIES
               );
&#12;           If &quot;ignoreName&quot; or &quot;undefName&quot; is used, character names should be
           specified as a comment (following &quot;#&quot;) on each line.

       undefChar
       undefName
           -- see 6.3.4 Reducing the Repertoire, UTS #10.

           Undefines the collation element as if it were unassigned in the
           &quot;table&quot;.  This reduces the size of the table.  If an unassigned
           character appears in the string to be collated, the sort key is made
           from its codepoint as a single-character collation element, as it is
           greater than any other assigned collation elements (in the codepoint
           order among the unassigned characters).  But, it&apos;d be better to
           ignore characters unfamiliar to you and maybe never used.

           Through &quot;undefChar&quot;, any character matching &quot;qr/$undefChar/&quot; will be
           undefined. Through &quot;undefName&quot;, any character whose name (given in
           the &quot;table&quot; file as a comment) matches &quot;qr/$undefName/&quot; will be
           undefined.

           ex. Collation weights for beyond-BMP characters are not stored in
           object:

               undefChar =&gt; qr/[^\0-\x{fffd}]/,

       upper_before_lower
           -- see 6.6 Case Comparisons, UTS #10.

           By default, lowercase is before uppercase.  If the parameter is made
           true, this is reversed.

           NOTE: This parameter simplemindedly assumes that any
           lowercase/uppercase distinctions must occur in level 3, and their
           weights at level 3 must be same as those mentioned in 7.3.1, UTS #10.
           If you define your collation elements which differs from this
           requirement, this parameter doesn&apos;t work validly.

       variable
           -- see 3.2.2 Variable Weighting, UTS #10.

           This key allows for variable weighting of variable collation
           elements, which are marked with an ASTERISK in the table (NOTE: Many
           punctuation marks and symbols are variable in allkeys.txt).

              variable =&gt; &apos;blanked&apos;, &apos;non-ignorable&apos;, &apos;shifted&apos;, or &apos;shift-trimmed&apos;.

           These names are case-insensitive.  By default (if specification is
           omitted), &apos;shifted&apos; is adopted.

              &apos;Blanked&apos;        Variable elements are made ignorable at levels 1 through 3;
                               considered at the 4th level.

              &apos;Non-Ignorable&apos;  Variable elements are not reset to ignorable.

              &apos;Shifted&apos;        Variable elements are made ignorable at levels 1 through 3
                               their level 4 weight is replaced by the old level 1 weight.
                               Level 4 weight for Non-Variable elements is 0xFFFF.

              &apos;Shift-Trimmed&apos;  Same as &apos;shifted&apos;, but all FFFF&apos;s at the 4th level
                               are trimmed.

   Methods for Collation
       &quot;@sorted = $Collator-&gt;sort(@not_sorted)&quot;
           Sorts a list of strings.

       &quot;$result = $Collator-&gt;cmp($a, $b)&quot;
           Returns 1 (when $a is greater than $b) or 0 (when $a is equal to $b)
           or -1 (when $a is lesser than $b).

       &quot;$result = $Collator-&gt;eq($a, $b)&quot;
       &quot;$result = $Collator-&gt;ne($a, $b)&quot;
       &quot;$result = $Collator-&gt;lt($a, $b)&quot;
       &quot;$result = $Collator-&gt;le($a, $b)&quot;
       &quot;$result = $Collator-&gt;gt($a, $b)&quot;
       &quot;$result = $Collator-&gt;ge($a, $b)&quot;
           They works like the same name operators as theirs.

              eq : whether $a is equal to $b.
              ne : whether $a is not equal to $b.
              lt : whether $a is lesser than $b.
              le : whether $a is lesser than $b or equal to $b.
              gt : whether $a is greater than $b.
              ge : whether $a is greater than $b or equal to $b.
&#12;       &quot;$sortKey = $Collator-&gt;getSortKey($string)&quot;
           -- see 4.3 Form Sort Key, UTS #10.

           Returns a sort key.

           You compare the sort keys using a binary comparison and get the
           result of the comparison of the strings using UCA.

              $Collator-&gt;getSortKey($a) cmp $Collator-&gt;getSortKey($b)

                 is equivalent to

              $Collator-&gt;cmp($a, $b)

       &quot;$sortKeyForm = $Collator-&gt;viewSortKey($string)&quot;
           Converts a sorting key into its representation form.  If
           &quot;UCA_Version&quot; is 8, the output is slightly different.

              use Unicode::Collate;
              my $c = Unicode::Collate-&gt;new();
              print $c-&gt;viewSortKey(&quot;Perl&quot;),&quot;\n&quot;;

              # output:
              # [0B67 0A65 0B7F 0B03 | 0020 0020 0020 0020 | 0008 0002 0002 0002 | FFFF FFFF FFFF FFFF]
              #  Level 1               Level 2               Level 3               Level 4

   Methods for Searching
       The &quot;match&quot;, &quot;gmatch&quot;, &quot;subst&quot;, &quot;gsubst&quot; methods work like &quot;m//&quot;, &quot;m//g&quot;,
       &quot;s///&quot;, &quot;s///g&quot;, respectively, but they are not aware of any pattern, but
       only a literal substring.

       DISCLAIMER: If &quot;preprocess&quot; or &quot;normalization&quot; parameter is true for
       $Collator, calling these methods (&quot;index&quot;, &quot;match&quot;, &quot;gmatch&quot;, &quot;subst&quot;,
       &quot;gsubst&quot;) is croaked, as the position and the length might differ from
       those on the specified string.

       &quot;rearrange&quot; and &quot;hangul_terminator&quot; parameters are neglected.
       &quot;katakana_before_hiragana&quot; and &quot;upper_before_lower&quot; don&apos;t affect matching
       and searching, as it doesn&apos;t matter whether greater or lesser.

       &quot;$position = $Collator-&gt;index($string, $substring[, $position])&quot;
       &quot;($position, $length) = $Collator-&gt;index($string, $substring[,
       $position])&quot;
           If $substring matches a part of $string, returns the position of the
           first occurrence of the matching part in scalar context; in list
           context, returns a two-element list of the position and the length of
           the matching part.

           If $substring does not match any part of $string, returns &quot;-1&quot; in
           scalar context and an empty list in list context.

           e.g. you say

             my $Collator = Unicode::Collate-&gt;new( normalization =&gt; undef, level =&gt; 1 );
                                                # (normalization =&gt; undef) is REQUIRED.
             my $str = &quot;Ich muss studieren Perl.&quot;;
             my $sub = &quot;MUeSS&quot;;
             my $match;
             if (my($pos,$len) = $Collator-&gt;index($str, $sub)) {
                 $match = substr($str, $pos, $len);
             }

           and get &quot;muss&quot; in $match since &quot;muss&quot; is primary equal to &quot;MUeSS&quot;.

       &quot;$match_ref = $Collator-&gt;match($string, $substring)&quot;
       &quot;($match)   = $Collator-&gt;match($string, $substring)&quot;
           If $substring matches a part of $string, in scalar context, returns a
           reference to the first occurrence of the matching part ($match_ref is
           always true if matches, since every reference is true); in list
           context, returns the first occurrence of the matching part.

           If $substring does not match any part of $string, returns &quot;undef&quot; in
           scalar context and an empty list in list context.

           e.g.

               if ($match_ref = $Collator-&gt;match($str, $sub)) { # scalar context
                   print &quot;matches [$$match_ref].\n&quot;;
               } else {
                   print &quot;doesn&apos;t match.\n&quot;;
               }

                or
&#12;               if (($match) = $Collator-&gt;match($str, $sub)) { # list context
                   print &quot;matches [$match].\n&quot;;
               } else {
                   print &quot;doesn&apos;t match.\n&quot;;
               }

       &quot;@match = $Collator-&gt;gmatch($string, $substring)&quot;
           If $substring matches a part of $string, returns all the matching
           parts (or matching count in scalar context).

           If $substring does not match any part of $string, returns an empty
           list.

       &quot;$count = $Collator-&gt;subst($string, $substring, $replacement)&quot;
           If $substring matches a part of $string, the first occurrence of the
           matching part is replaced by $replacement ($string is modified) and
           $count (always equals to 1) is returned.

           $replacement can be a &quot;CODEREF&quot;, taking the matching part as an
           argument, and returning a string to replace the matching part (a bit
           similar to &quot;s/(..)/$coderef-&gt;($1)/e&quot;).

       &quot;$count = $Collator-&gt;gsubst($string, $substring, $replacement)&quot;
           If $substring matches a part of $string, all the occurrences of the
           matching part are replaced by $replacement ($string is modified) and
           $count is returned.

           $replacement can be a &quot;CODEREF&quot;, taking the matching part as an
           argument, and returning a string to replace the matching part (a bit
           similar to &quot;s/(..)/$coderef-&gt;($1)/eg&quot;).

           e.g.

             my $Collator = Unicode::Collate-&gt;new( normalization =&gt; undef, level =&gt; 1 );
                                                # (normalization =&gt; undef) is REQUIRED.
             my $str = &quot;Camel donkey zebra came\x{301}l CAMEL horse cam\0e\0l...&quot;;
             $Collator-&gt;gsubst($str, &quot;camel&quot;, sub { &quot;&lt;b&gt;$_[0]&lt;/b&gt;&quot; });

             # now $str is &quot;&lt;b&gt;Camel&lt;/b&gt; donkey zebra &lt;b&gt;came\x{301}l&lt;/b&gt; &lt;b&gt;CAMEL&lt;/b&gt; horse &lt;b&gt;cam\0e\0l&lt;/b&gt;...&quot;;
             # i.e., all the camels are made bold-faced.

              Examples: levels and ignore_level2 - what does camel match?
             ---------------------------------------------------------------------------
              level  ignore_level2  |  camel  Camel  came\x{301}l  c-a-m-e-l  cam\0e\0l
             -----------------------|---------------------------------------------------
                1        false      |   yes    yes      yes          yes        yes
                2        false      |   yes    yes      no           yes        yes
                3        false      |   yes    no       no           yes        yes
                4        false      |   yes    no       no           no         yes
             -----------------------|---------------------------------------------------
                1        true       |   yes    yes      yes          yes        yes
                2        true       |   yes    yes      yes          yes        yes
                3        true       |   yes    no       yes          yes        yes
                4        true       |   yes    no       yes          no         yes
             ---------------------------------------------------------------------------
              note: if variable =&gt; non-ignorable, camel doesn&apos;t match c-a-m-e-l
                    at any level.

   Other Methods
       &quot;%old_tailoring = $Collator-&gt;change(%new_tailoring)&quot;
       &quot;$modified_collator = $Collator-&gt;change(%new_tailoring)&quot;
           Changes the value of specified keys and returns the changed part.

               $Collator = Unicode::Collate-&gt;new(level =&gt; 4);

               $Collator-&gt;eq(&quot;perl&quot;, &quot;PERL&quot;); # false

               %old = $Collator-&gt;change(level =&gt; 2); # returns (level =&gt; 4).

               $Collator-&gt;eq(&quot;perl&quot;, &quot;PERL&quot;); # true

               $Collator-&gt;change(%old); # returns (level =&gt; 2).

               $Collator-&gt;eq(&quot;perl&quot;, &quot;PERL&quot;); # false

           Not all &quot;(key,value)&quot;s are allowed to be changed.  See also
           @Unicode::Collate::ChangeOK and @Unicode::Collate::ChangeNG.

           In the scalar context, returns the modified collator (but it is not a
           clone from the original).

               $Collator-&gt;change(level =&gt; 2)-&gt;eq(&quot;perl&quot;, &quot;PERL&quot;); # true

               $Collator-&gt;eq(&quot;perl&quot;, &quot;PERL&quot;); # true; now max level is 2nd.

               $Collator-&gt;change(level =&gt; 4)-&gt;eq(&quot;perl&quot;, &quot;PERL&quot;); # false

       &quot;$version = $Collator-&gt;version()&quot;
           Returns the version number (a string) of the Unicode Standard which
           the &quot;table&quot; file used by the collator object is based on.  If the
           table does not include a version line (starting with @version),
           returns &quot;unknown&quot;.

       &quot;UCA_Version()&quot;
           Returns the revision number of UTS #10 this module consults, that
           should correspond with the DUCET incorporated.

       &quot;Base_Unicode_Version()&quot;
           Returns the version number of UTS #10 this module consults, that
           should correspond with the DUCET incorporated.

EXPORT
       No method will be exported.

INSTALL
       Though this module can be used without any &quot;table&quot; file, to use this
       module easily, it is recommended to install a table file in the UCA
       format, by copying it under the directory &lt;a place in
       @INC&gt;/Unicode/Collate.

       The most preferable one is &quot;The Default Unicode Collation Element Table&quot;
       (aka DUCET), available from the Unicode Consortium&apos;s website:

          http://www.unicode.org/Public/UCA/

          http://www.unicode.org/Public/UCA/latest/allkeys.txt (latest version)

       If DUCET is not installed, it is recommended to copy the file from
       http://www.unicode.org/Public/UCA/latest/allkeys.txt to &lt;a place in
       @INC&gt;/Unicode/Collate/allkeys.txt manually.

CAVEATS
       Normalization
           Use of the &quot;normalization&quot; parameter requires the Unicode::Normalize
           module (see Unicode::Normalize).

           If you need not it (say, in the case when you need not handle any
           combining characters), assign &quot;normalization =&gt; undef&quot; explicitly.

           -- see 6.5 Avoiding Normalization, UTS #10.

       Conformance Test
           The Conformance Test for the UCA is available under
           &lt;http://www.unicode.org/Public/UCA/&gt;.

           For CollationTest_SHIFTED.txt, a collator via &quot;Unicode::Collate-&gt;new(
           )&quot; should be used; for CollationTest_NON_IGNORABLE.txt, a collator
           via &quot;Unicode::Collate-&gt;new(variable =&gt; &quot;non-ignorable&quot;, level =&gt; 3)&quot;.

           Unicode::Normalize is required to try The Conformance Test.

AUTHOR, COPYRIGHT AND LICENSE
       The Unicode::Collate module for perl was written by SADAHIRO Tomoyuki,
       &lt;SADAHIRO@cpan.org&gt;. This module is Copyright(C) 2001-2012, SADAHIRO
       Tomoyuki. Japan. All rights reserved.

       This module is free software; you can redistribute it and/or modify it
       under the same terms as Perl itself.

       The file Unicode/Collate/allkeys.txt was copied verbatim from
       &lt;http://www.unicode.org/Public/UCA/6.1.0/allkeys.txt&gt;.  For this file,
       Copyright (c) 2001-2011 Unicode, Inc.  Distributed under the Terms of Use
       in &lt;http://www.unicode.org/copyright.html&gt;.

SEE ALSO
       Unicode Collation Algorithm - UTS #10
           &lt;http://www.unicode.org/reports/tr10/&gt;

       The Default Unicode Collation Element Table (DUCET)
           &lt;http://www.unicode.org/Public/UCA/latest/allkeys.txt&gt;

       The conformance test for the UCA
           &lt;http://www.unicode.org/Public/UCA/latest/CollationTest.html&gt;

           &lt;http://www.unicode.org/Public/UCA/latest/CollationTest.zip&gt;

       Hangul Syllable Type
           &lt;http://www.unicode.org/Public/UNIDATA/HangulSyllableType.txt&gt;
&#12;       Unicode Normalization Forms - UAX #15
           &lt;http://www.unicode.org/reports/tr15/&gt;

       Unicode Locale Data Markup Language (LDML) - UTS #35
           &lt;http://www.unicode.org/reports/tr35/&gt;



perl v5.14.2                       2012-03-10                       Collate(3pm)

</pre></body></html>
