<html>
<head><meta charset=utf-8/>
<title>lwptut -- An LWP Tutorial</title></head>
<body><pre>

lwptut(3pm)            User Contributed Perl Documentation           lwptut(3pm)



NAME
       lwptut -- An LWP Tutorial

DESCRIPTION
       LWP (short for &quot;Library for WWW in Perl&quot;) is a very popular group of Perl
       modules for accessing data on the Web. Like most Perl module-
       distributions, each of LWP&apos;s component modules comes with documentation
       that is a complete reference to its interface. However, there are so many
       modules in LWP that it&apos;s hard to know where to start looking for
       information on how to do even the simplest most common things.

       Really introducing you to using LWP would require a whole book -- a book
       that just happens to exist, called Perl &amp; LWP. But this article should
       give you a taste of how you can go about some common tasks with LWP.

   Getting documents with LWP::Simple
       If you just want to get what&apos;s at a particular URL, the simplest way to
       do it is LWP::Simple&apos;s functions.

       In a Perl program, you can call its &quot;get($url)&quot; function.  It will try
       getting that URL&apos;s content.  If it works, then it&apos;ll return the content;
       but if there&apos;s some error, it&apos;ll return undef.

         my $url = &apos;http://www.npr.org/programs/fa/?todayDate=current&apos;;
           # Just an example: the URL for the most recent /Fresh Air/ show

         use LWP::Simple;
         my $content = get $url;
         die &quot;Couldn&apos;t get $url&quot; unless defined $content;

         # Then go do things with $content, like this:

         if($content =~ m/jazz/i) {
           print &quot;They&apos;re talking about jazz today on Fresh Air!\n&quot;;
         }
         else {
           print &quot;Fresh Air is apparently jazzless today.\n&quot;;
         }

       The handiest variant on &quot;get&quot; is &quot;getprint&quot;, which is useful in Perl one-
       liners.  If it can get the page whose URL you provide, it sends it to
       STDOUT; otherwise it complains to STDERR.

         % perl -MLWP::Simple -e &quot;getprint &apos;http://www.cpan.org/RECENT&apos;&quot;

       That is the URL of a plain text file that lists new files in CPAN in the
       past two weeks.  You can easily make it part of a tidy little shell
       command, like this one that mails you the list of new &quot;Acme::&quot; modules:

         % perl -MLWP::Simple -e &quot;getprint &apos;http://www.cpan.org/RECENT&apos;&quot;  \
            | grep &quot;/by-module/Acme&quot; | mail -s &quot;New Acme modules! Joy!&quot; $USER

       There are other useful functions in LWP::Simple, including one function
       for running a HEAD request on a URL (useful for checking links, or
       getting the last-revised time of a URL), and two functions for
       saving/mirroring a URL to a local file. See the LWP::Simple documentation
       for the full details, or chapter 2 of Perl &amp; LWP for more examples.

   The Basics of the LWP Class Model
       LWP::Simple&apos;s functions are handy for simple cases, but its functions
       don&apos;t support cookies or authorization, don&apos;t support setting header
       lines in the HTTP request, generally don&apos;t support reading header lines
       in the HTTP response (notably the full HTTP error message, in case of an
       error). To get at all those features, you&apos;ll have to use the full LWP
       class model.

       While LWP consists of dozens of classes, the main two that you have to
       understand are LWP::UserAgent and HTTP::Response. LWP::UserAgent is a
       class for &quot;virtual browsers&quot; which you use for performing requests, and
       HTTP::Response is a class for the responses (or error messages) that you
       get back from those requests.

       The basic idiom is &quot;$response = $browser-&gt;get($url)&quot;, or more fully
       illustrated:

         # Early in your program:

         use LWP 5.64; # Loads all important LWP classes, and makes
                       #  sure your version is reasonably recent.

         my $browser = LWP::UserAgent-&gt;new;

         ...

         # Then later, whenever you need to make a get request:
         my $url = &apos;http://www.npr.org/programs/fa/?todayDate=current&apos;;

         my $response = $browser-&gt;get( $url );
         die &quot;Can&apos;t get $url -- &quot;, $response-&gt;status_line
          unless $response-&gt;is_success;

         die &quot;Hey, I was expecting HTML, not &quot;, $response-&gt;content_type
          unless $response-&gt;content_type eq &apos;text/html&apos;;
            # or whatever content-type you&apos;re equipped to deal with

         # Otherwise, process the content somehow:

         if($response-&gt;decoded_content =~ m/jazz/i) {
           print &quot;They&apos;re talking about jazz today on Fresh Air!\n&quot;;
         }
         else {
           print &quot;Fresh Air is apparently jazzless today.\n&quot;;
         }

       There are two objects involved: $browser, which holds an object of class
       LWP::UserAgent, and then the $response object, which is of class
       HTTP::Response. You really need only one browser object per program; but
       every time you make a request, you get back a new HTTP::Response object,
       which will have some interesting attributes:

       &#183;   A status code indicating success or failure (which you can test with
           &quot;$response-&gt;is_success&quot;).

       &#183;   An HTTP status line that is hopefully informative if there&apos;s failure
           (which you can see with &quot;$response-&gt;status_line&quot;, returning something
           like &quot;404 Not Found&quot;).

       &#183;   A MIME content-type like &quot;text/html&quot;, &quot;image/gif&quot;, &quot;application/xml&quot;,
           etc., which you can see with &quot;$response-&gt;content_type&quot;

       &#183;   The actual content of the response, in &quot;$response-&gt;decoded_content&quot;.
           If the response is HTML, that&apos;s where the HTML source will be; if
           it&apos;s a GIF, then &quot;$response-&gt;decoded_content&quot; will be the binary GIF
           data.

       &#183;   And dozens of other convenient and more specific methods that are
           documented in the docs for HTTP::Response, and its superclasses
           HTTP::Message and HTTP::Headers.

   Adding Other HTTP Request Headers
       The most commonly used syntax for requests is &quot;$response =
       $browser-&gt;get($url)&quot;, but in truth, you can add extra HTTP header lines
       to the request by adding a list of key-value pairs after the URL, like
       so:

         $response = $browser-&gt;get( $url, $key1, $value1, $key2, $value2, ... );

       For example, here&apos;s how to send some more Netscape-like headers, in case
       you&apos;re dealing with a site that would otherwise reject your request:

         my @ns_headers = (
          &apos;User-Agent&apos; =&gt; &apos;Mozilla/4.76 [en] (Win98; U)&apos;,
          &apos;Accept&apos; =&gt; &apos;image/gif, image/x-xbitmap, image/jpeg, image/pjpeg, image/png, */*&apos;,
          &apos;Accept-Charset&apos; =&gt; &apos;iso-8859-1,*,utf-8&apos;,
          &apos;Accept-Language&apos; =&gt; &apos;en-US&apos;,
         );

         ...

         $response = $browser-&gt;get($url, @ns_headers);

       If you weren&apos;t reusing that array, you could just go ahead and do this:

         $response = $browser-&gt;get($url,
          &apos;User-Agent&apos; =&gt; &apos;Mozilla/4.76 [en] (Win98; U)&apos;,
          &apos;Accept&apos; =&gt; &apos;image/gif, image/x-xbitmap, image/jpeg, image/pjpeg, image/png, */*&apos;,
          &apos;Accept-Charset&apos; =&gt; &apos;iso-8859-1,*,utf-8&apos;,
          &apos;Accept-Language&apos; =&gt; &apos;en-US&apos;,
         );

       If you were only ever changing the &apos;User-Agent&apos; line, you could just
       change the $browser object&apos;s default line from &quot;libwww-perl/5.65&quot; (or the
       like) to whatever you like, using the LWP::UserAgent &quot;agent&quot; method:

          $browser-&gt;agent(&apos;Mozilla/4.76 [en] (Win98; U)&apos;);

   Enabling Cookies
       A default LWP::UserAgent object acts like a browser with its cookies
       support turned off. There are various ways of turning it on, by setting
       its &quot;cookie_jar&quot; attribute. A &quot;cookie jar&quot; is an object representing a
       little database of all the HTTP cookies that a browser can know about. It
       can correspond to a file on disk (the way Netscape uses its cookies.txt
       file), or it can be just an in-memory object that starts out empty, and
       whose collection of cookies will disappear once the program is finished
       running.

       To give a browser an in-memory empty cookie jar, you set its &quot;cookie_jar&quot;
       attribute like so:

         $browser-&gt;cookie_jar({});

       To give it a copy that will be read from a file on disk, and will be
       saved to it when the program is finished running, set the &quot;cookie_jar&quot;
       attribute like this:

         use HTTP::Cookies;
         $browser-&gt;cookie_jar( HTTP::Cookies-&gt;new(
           &apos;file&apos; =&gt; &apos;/some/where/cookies.lwp&apos;,
               # where to read/write cookies
           &apos;autosave&apos; =&gt; 1,
               # save it to disk when done
         ));

       That file will be an LWP-specific format. If you want to be access the
       cookies in your Netscape cookies file, you can use the
       HTTP::Cookies::Netscape class:

         use HTTP::Cookies;
           # yes, loads HTTP::Cookies::Netscape too

         $browser-&gt;cookie_jar( HTTP::Cookies::Netscape-&gt;new(
           &apos;file&apos; =&gt; &apos;c:/Program Files/Netscape/Users/DIR-NAME-HERE/cookies.txt&apos;,
               # where to read cookies
         ));

       You could add an &quot;&apos;autosave&apos; =&gt; 1&quot; line as further above, but at time of
       writing, it&apos;s uncertain whether Netscape might discard some of the
       cookies you could be writing back to disk.

   Posting Form Data
       Many HTML forms send data to their server using an HTTP POST request,
       which you can send with this syntax:

        $response = $browser-&gt;post( $url,
          [
            formkey1 =&gt; value1,
            formkey2 =&gt; value2,
            ...
          ],
        );

       Or if you need to send HTTP headers:

        $response = $browser-&gt;post( $url,
          [
            formkey1 =&gt; value1,
            formkey2 =&gt; value2,
            ...
          ],
          headerkey1 =&gt; value1,
          headerkey2 =&gt; value2,
        );

       For example, the following program makes a search request to AltaVista
       (by sending some form data via an HTTP POST request), and extracts from
       the HTML the report of the number of matches:

         use strict;
         use warnings;
         use LWP 5.64;
         my $browser = LWP::UserAgent-&gt;new;

         my $word = &apos;tarragon&apos;;

         my $url = &apos;http://search.yahoo.com/yhs/search&apos;;
         my $response = $browser-&gt;post( $url,
           [ &apos;q&apos; =&gt; $word,  # the Altavista query string
             &apos;fr&apos; =&gt; &apos;altavista&apos;, &apos;pg&apos; =&gt; &apos;q&apos;, &apos;avkw&apos; =&gt; &apos;tgz&apos;, &apos;kl&apos; =&gt; &apos;XX&apos;,
           ]
         );
         die &quot;$url error: &quot;, $response-&gt;status_line
          unless $response-&gt;is_success;
         die &quot;Weird content type at $url -- &quot;, $response-&gt;content_type
          unless $response-&gt;content_is_html;

         if( $response-&gt;decoded_content =~ m{([0-9,]+)(?:&lt;.*?&gt;)? results for} ) {
           # The substring will be like &quot;996,000&lt;/strong&gt; results for&quot;
           print &quot;$word: $1\n&quot;;
         }
         else {
           print &quot;Couldn&apos;t find the match-string in the response\n&quot;;
         }

   Sending GET Form Data
       Some HTML forms convey their form data not by sending the data in an HTTP
       POST request, but by making a normal GET request with the data stuck on
       the end of the URL.  For example, if you went to &quot;www.imdb.com&quot; and ran a
       search on &quot;Blade Runner&quot;, the URL you&apos;d see in your browser window would
       be:

         http://www.imdb.com/find?s=all&amp;q=Blade+Runner

       To run the same search with LWP, you&apos;d use this idiom, which involves the
       URI class:

         use URI;
         my $url = URI-&gt;new( &apos;http://www.imdb.com/find&apos; );
           # makes an object representing the URL

         $url-&gt;query_form(  # And here the form data pairs:
           &apos;q&apos; =&gt; &apos;Blade Runner&apos;,
           &apos;s&apos; =&gt; &apos;all&apos;,
         );

         my $response = $browser-&gt;get($url);

       See chapter 5 of Perl &amp; LWP for a longer discussion of HTML forms and of
       form data, and chapters 6 through 9 for a longer discussion of extracting
       data from HTML.

   Absolutizing URLs
       The URI class that we just mentioned above provides all sorts of methods
       for accessing and modifying parts of URLs (such as asking sort of URL it
       is with &quot;$url-&gt;scheme&quot;, and asking what host it refers to with
       &quot;$url-&gt;host&quot;, and so on, as described in the docs for the URI class.
       However, the methods of most immediate interest are the &quot;query_form&quot;
       method seen above, and now the &quot;new_abs&quot; method for taking a probably-
       relative URL string (like &quot;../foo.html&quot;) and getting back an absolute URL
       (like &quot;http://www.perl.com/stuff/foo.html&quot;), as shown here:

         use URI;
         $abs = URI-&gt;new_abs($maybe_relative, $base);

       For example, consider this program that matches URLs in the HTML list of
       new modules in CPAN:

         use strict;
         use warnings;
         use LWP;
         my $browser = LWP::UserAgent-&gt;new;

         my $url = &apos;http://www.cpan.org/RECENT.html&apos;;
         my $response = $browser-&gt;get($url);
         die &quot;Can&apos;t get $url -- &quot;, $response-&gt;status_line
          unless $response-&gt;is_success;

         my $html = $response-&gt;decoded_content;
         while( $html =~ m/&lt;A HREF=\&quot;(.*?)\&quot;/g ) {
           print &quot;$1\n&quot;;
         }

       When run, it emits output that starts out something like this:

         MIRRORING.FROM
         RECENT
         RECENT.html
         authors/00whois.html
         authors/01mailrc.txt.gz
         authors/id/A/AA/AASSAD/CHECKSUMS
         ...
&#12;       However, if you actually want to have those be absolute URLs, you can use
       the URI module&apos;s &quot;new_abs&quot; method, by changing the &quot;while&quot; loop to this:

         while( $html =~ m/&lt;A HREF=\&quot;(.*?)\&quot;/g ) {
           print URI-&gt;new_abs( $1, $response-&gt;base ) ,&quot;\n&quot;;
         }

       (The &quot;$response-&gt;base&quot; method from HTTP::Message is for returning what
       URL should be used for resolving relative URLs -- it&apos;s usually just the
       same as the URL that you requested.)

       That program then emits nicely absolute URLs:

         http://www.cpan.org/MIRRORING.FROM
         http://www.cpan.org/RECENT
         http://www.cpan.org/RECENT.html
         http://www.cpan.org/authors/00whois.html
         http://www.cpan.org/authors/01mailrc.txt.gz
         http://www.cpan.org/authors/id/A/AA/AASSAD/CHECKSUMS
         ...

       See chapter 4 of Perl &amp; LWP for a longer discussion of URI objects.

       Of course, using a regexp to match hrefs is a bit simplistic, and for
       more robust programs, you&apos;ll probably want to use an HTML-parsing module
       like HTML::LinkExtor or HTML::TokeParser or even maybe HTML::TreeBuilder.

   Other Browser Attributes
       LWP::UserAgent objects have many attributes for controlling how they
       work.  Here are a few notable ones:

       &#183;   &quot;$browser-&gt;timeout(15);&quot;

           This sets this browser object to give up on requests that don&apos;t
           answer within 15 seconds.

       &#183;   &quot;$browser-&gt;protocols_allowed( [ &apos;http&apos;, &apos;gopher&apos;] );&quot;

           This sets this browser object to not speak any protocols other than
           HTTP and gopher. If it tries accessing any other kind of URL (like an
           &quot;ftp:&quot; or &quot;mailto:&quot; or &quot;news:&quot; URL), then it won&apos;t actually try
           connecting, but instead will immediately return an error code 500,
           with a message like &quot;Access to &apos;ftp&apos; URIs has been disabled&quot;.

       &#183;   &quot;use LWP::ConnCache; $browser-&gt;conn_cache(LWP::ConnCache-&gt;new());&quot;

           This tells the browser object to try using the HTTP/1.1 &quot;Keep-Alive&quot;
           feature, which speeds up requests by reusing the same socket
           connection for multiple requests to the same server.

       &#183;   &quot;$browser-&gt;agent( &apos;SomeName/1.23 (more info here maybe)&apos; )&quot;

           This changes how the browser object will identify itself in the
           default &quot;User-Agent&quot; line is its HTTP requests.  By default, it&apos;ll
           send &quot;libwww-perl/versionnumber&quot;, like &quot;libwww-perl/5.65&quot;.  You can
           change that to something more descriptive like this:

             $browser-&gt;agent( &apos;SomeName/3.14 (contact@robotplexus.int)&apos; );

           Or if need be, you can go in disguise, like this:

             $browser-&gt;agent( &apos;Mozilla/4.0 (compatible; MSIE 5.12; Mac_PowerPC)&apos; );

       &#183;   &quot;push @{ $ua-&gt;requests_redirectable }, &apos;POST&apos;;&quot;

           This tells this browser to obey redirection responses to POST
           requests (like most modern interactive browsers), even though the
           HTTP RFC says that should not normally be done.

       For more options and information, see the full documentation for
       LWP::UserAgent.

   Writing Polite Robots
       If you want to make sure that your LWP-based program respects robots.txt
       files and doesn&apos;t make too many requests too fast, you can use the
       LWP::RobotUA class instead of the LWP::UserAgent class.

       LWP::RobotUA class is just like LWP::UserAgent, and you can use it like
       so:

         use LWP::RobotUA;
         my $browser = LWP::RobotUA-&gt;new(&apos;YourSuperBot/1.34&apos;, &apos;you@yoursite.com&apos;);
           # Your bot&apos;s name and your email address

         my $response = $browser-&gt;get($url);

       But HTTP::RobotUA adds these features:

       &#183;   If the robots.txt on $url&apos;s server forbids you from accessing $url,
           then the $browser object (assuming it&apos;s of class LWP::RobotUA) won&apos;t
           actually request it, but instead will give you back (in $response) a
           403 error with a message &quot;Forbidden by robots.txt&quot;.  That is, if you
           have this line:

             die &quot;$url -- &quot;, $response-&gt;status_line, &quot;\nAborted&quot;
              unless $response-&gt;is_success;

           then the program would die with an error message like this:

             http://whatever.site.int/pith/x.html -- 403 Forbidden by robots.txt
             Aborted at whateverprogram.pl line 1234

       &#183;   If this $browser object sees that the last time it talked to $url&apos;s
           server was too recently, then it will pause (via &quot;sleep&quot;) to avoid
           making too many requests too often. How long it will pause for, is by
           default one minute -- but you can control it with the
           &quot;$browser-&gt;delay( minutes )&quot; attribute.

           For example, this code:

             $browser-&gt;delay( 7/60 );

           ...means that this browser will pause when it needs to avoid talking
           to any given server more than once every 7 seconds.

       For more options and information, see the full documentation for
       LWP::RobotUA.

   Using Proxies
       In some cases, you will want to (or will have to) use proxies for
       accessing certain sites and/or using certain protocols. This is most
       commonly the case when your LWP program is running (or could be running)
       on a machine that is behind a firewall.

       To make a browser object use proxies that are defined in the usual
       environment variables (&quot;HTTP_PROXY&quot;, etc.), just call the &quot;env_proxy&quot; on
       a user-agent object before you go making any requests on it.
       Specifically:

         use LWP::UserAgent;
         my $browser = LWP::UserAgent-&gt;new;

         # And before you go making any requests:
         $browser-&gt;env_proxy;

       For more information on proxy parameters, see the LWP::UserAgent
       documentation, specifically the &quot;proxy&quot;, &quot;env_proxy&quot;, and &quot;no_proxy&quot;
       methods.

   HTTP Authentication
       Many web sites restrict access to documents by using &quot;HTTP
       Authentication&quot;. This isn&apos;t just any form of &quot;enter your password&quot;
       restriction, but is a specific mechanism where the HTTP server sends the
       browser an HTTP code that says &quot;That document is part of a protected
       &apos;realm&apos;, and you can access it only if you re-request it and add some
       special authorization headers to your request&quot;.

       For example, the Unicode.org admins stop email-harvesting bots from
       harvesting the contents of their mailing list archives, by protecting
       them with HTTP Authentication, and then publicly stating the username and
       password (at &quot;http://www.unicode.org/mail-arch/&quot;) -- namely username
       &quot;unicode-ml&quot; and password &quot;unicode&quot;.

       For example, consider this URL, which is part of the protected area of
       the web site:

         http://www.unicode.org/mail-arch/unicode-ml/y2002-m08/0067.html

       If you access that with a browser, you&apos;ll get a prompt like &quot;Enter
       username and password for &apos;Unicode-MailList-Archives&apos; at server
       &apos;www.unicode.org&apos;&quot;.

       In LWP, if you just request that URL, like this:

         use LWP;
         my $browser = LWP::UserAgent-&gt;new;

         my $url =
          &apos;http://www.unicode.org/mail-arch/unicode-ml/y2002-m08/0067.html&apos;;
         my $response = $browser-&gt;get($url);

         die &quot;Error: &quot;, $response-&gt;header(&apos;WWW-Authenticate&apos;) || &apos;Error accessing&apos;,
           #  (&apos;WWW-Authenticate&apos; is the realm-name)
           &quot;\n &quot;, $response-&gt;status_line, &quot;\n at $url\n Aborting&quot;
          unless $response-&gt;is_success;

       Then you&apos;ll get this error:

         Error: Basic realm=&quot;Unicode-MailList-Archives&quot;
          401 Authorization Required
          at http://www.unicode.org/mail-arch/unicode-ml/y2002-m08/0067.html
          Aborting at auth1.pl line 9.  [or wherever]

       ...because the $browser doesn&apos;t know any the username and password for
       that realm (&quot;Unicode-MailList-Archives&quot;) at that host
       (&quot;www.unicode.org&quot;).  The simplest way to let the browser know about this
       is to use the &quot;credentials&quot; method to let it know about a username and
       password that it can try using for that realm at that host.  The syntax
       is:

         $browser-&gt;credentials(
           &apos;servername:portnumber&apos;,
           &apos;realm-name&apos;,
          &apos;username&apos; =&gt; &apos;password&apos;
         );

       In most cases, the port number is 80, the default TCP/IP port for HTTP;
       and you usually call the &quot;credentials&quot; method before you make any
       requests.  For example:

         $browser-&gt;credentials(
           &apos;reports.mybazouki.com:80&apos;,
           &apos;web_server_usage_reports&apos;,
           &apos;plinky&apos; =&gt; &apos;banjo123&apos;
         );

       So if we add the following to the program above, right after the
       &quot;$browser = LWP::UserAgent-&gt;new;&quot; line...

         $browser-&gt;credentials(  # add this to our $browser &apos;s &quot;key ring&quot;
           &apos;www.unicode.org:80&apos;,
           &apos;Unicode-MailList-Archives&apos;,
           &apos;unicode-ml&apos; =&gt; &apos;unicode&apos;
         );
&#12;       ...then when we run it, the request succeeds, instead of causing the
       &quot;die&quot; to be called.

   Accessing HTTPS URLs
       When you access an HTTPS URL, it&apos;ll work for you just like an HTTP URL
       would -- if your LWP installation has HTTPS support (via an appropriate
       Secure Sockets Layer library).  For example:

         use LWP;
         my $url = &apos;https://www.paypal.com/&apos;;   # Yes, HTTPS!
         my $browser = LWP::UserAgent-&gt;new;
         my $response = $browser-&gt;get($url);
         die &quot;Error at $url\n &quot;, $response-&gt;status_line, &quot;\n Aborting&quot;
          unless $response-&gt;is_success;
         print &quot;Whee, it worked!  I got that &quot;,
          $response-&gt;content_type, &quot; document!\n&quot;;

       If your LWP installation doesn&apos;t have HTTPS support set up, then the
       response will be unsuccessful, and you&apos;ll get this error message:

         Error at https://www.paypal.com/
          501 Protocol scheme &apos;https&apos; is not supported
          Aborting at paypal.pl line 7.   [or whatever program and line]

       If your LWP installation does have HTTPS support installed, then the
       response should be successful, and you should be able to consult
       $response just like with any normal HTTP response.

       For information about installing HTTPS support for your LWP installation,
       see the helpful README.SSL file that comes in the libwww-perl
       distribution.

   Getting Large Documents
       When you&apos;re requesting a large (or at least potentially large) document,
       a problem with the normal way of using the request methods (like
       &quot;$response = $browser-&gt;get($url)&quot;) is that the response object in memory
       will have to hold the whole document -- in memory. If the response is a
       thirty megabyte file, this is likely to be quite an imposition on this
       process&apos;s memory usage.

       A notable alternative is to have LWP save the content to a file on disk,
       instead of saving it up in memory.  This is the syntax to use:

         $response = $ua-&gt;get($url,
                                &apos;:content_file&apos; =&gt; $filespec,
                             );

       For example,

         $response = $ua-&gt;get(&apos;http://search.cpan.org/&apos;,
                                &apos;:content_file&apos; =&gt; &apos;/tmp/sco.html&apos;
                             );

       When you use this &quot;:content_file&quot; option, the $response will have all the
       normal header lines, but &quot;$response-&gt;content&quot; will be empty.

       Note that this &quot;:content_file&quot; option isn&apos;t supported under older
       versions of LWP, so you should consider adding &quot;use LWP 5.66;&quot; to check
       the LWP version, if you think your program might run on systems with
       older versions.

       If you need to be compatible with older LWP versions, then use this
       syntax, which does the same thing:

         use HTTP::Request::Common;
         $response = $ua-&gt;request( GET($url), $filespec );

SEE ALSO
       Remember, this article is just the most rudimentary introduction to LWP
       -- to learn more about LWP and LWP-related tasks, you really must read
       from the following:

       &#183;   LWP::Simple -- simple functions for getting/heading/mirroring URLs
&#12;       &#183;   LWP -- overview of the libwww-perl modules

       &#183;   LWP::UserAgent -- the class for objects that represent &quot;virtual
           browsers&quot;

       &#183;   HTTP::Response -- the class for objects that represent the response
           to a LWP response, as in &quot;$response = $browser-&gt;get(...)&quot;

       &#183;   HTTP::Message and HTTP::Headers -- classes that provide more methods
           to HTTP::Response.

       &#183;   URI -- class for objects that represent absolute or relative URLs

       &#183;   URI::Escape -- functions for URL-escaping and URL-unescaping strings
           (like turning &quot;this &amp; that&quot; to and from &quot;this%20%26%20that&quot;).

       &#183;   HTML::Entities -- functions for HTML-escaping and HTML-unescaping
           strings (like turning &quot;C. &amp; E. Brontee&quot; to and from &quot;C. &amp;amp; E.
           Bront&amp;euml;&quot;)

       &#183;   HTML::TokeParser and HTML::TreeBuilder -- classes for parsing HTML

       &#183;   HTML::LinkExtor -- class for finding links in HTML documents

       &#183;   The book Perl &amp; LWP by Sean M. Burke.  O&apos;Reilly &amp; Associates, 2002.
           ISBN: 0-596-00178-9, &lt;http://oreilly.com/catalog/perllwp/&gt;.  The
           whole book is also available free online:
           &lt;http://lwp.interglacial.com&gt;.

COPYRIGHT
       Copyright 2002, Sean M. Burke.  You can redistribute this document and/or
       modify it, but only under the same terms as Perl itself.

AUTHOR
       Sean M. Burke &quot;sburke@cpan.org&quot;



perl v5.14.2                       2012-02-11                        lwptut(3pm)

</pre></body></html>
